%%%%% consistency-thms.elf
%%%%% theorems about consistency between clsmap, fldmap, methmap, and oflist
%%%%% Chao Sun
%%%%% $Id: consistency-thms.elf,v 1.10 2011/04/13 19:45:39 csun Exp $
%%%%% Jun 17, 2010


% theorems about shift

%theorem shift-preserves-clsmap-fldmap :
        forall* {CM} {FM1} {FM2} {N}
        forall  {CM-FM1: clsmap-fldmap CM FM1}
            		{SH: fldmap`shift N FM1 FM2}
        exists  {CM-FM2: clsmap-fldmap CM FM2}
        true.

- : shift-preserves-clsmap-fldmap clsmap-fldmap/0 _ clsmap-fldmap/0.

- : shift-preserves-clsmap-fldmap
     (clsmap-fldmap/null CM-FM1) _ (clsmap-fldmap/null CM-FM1).

- : shift-preserves-clsmap-fldmap 
     (clsmap-fldmap/+ CM-FM1 IN) _ (clsmap-fldmap/+ CM-FM1 IN).

%worlds () (shift-preserves-clsmap-fldmap _ _ _).
%total  {} (shift-preserves-clsmap-fldmap _ _ _).


% This theorem says if under a "bigger" clsmap CM0, CM 
% is well-defined, and class C in CM has fldmap FM, then all
% classes in FM is well-defined under CM0

%theorem lookup-implies-clsmap-fldmap : 
        forall* {M} {CM} {FM} {C}
        forall  {M-CM: clsmap-clsmap M CM}
                {L: clsmap`lookup CM C FM}
        exists  {M-FM: clsmap-fldmap M FM}
        true.

- : lookup-implies-clsmap-fldmap (clsmap-clsmap/+ _ _ M-FM _)
     (clsmap`lookup/= _) M-FM.

- : lookup-implies-clsmap-fldmap (clsmap-clsmap/+ M-CMP SH _ _)
     (clsmap`lookup/> L P0) M-FM
     <- nat`plus-swap-succ P0 P1
     <- nat`plus-commutative P1 P2
     <- clsmap`shift-preserves-lookup* L SH P2 L2
     <- lookup-implies-clsmap-fldmap M-CMP L2 M-FM.

%worlds () (lookup-implies-clsmap-fldmap _ _ _).
%total  (M-CM) (lookup-implies-clsmap-fldmap M-CM _ _).


%theorem lookup-implies-clsmap-ty :
        forall* {CM} {FM} {F} {T}
        forall  {CM-FM: clsmap-fldmap CM FM}
                {FML: fldmap`lookup FM F T}
        exists  {CM-NT : clsmap-ty CM T}
        true.

- : lookup-implies-clsmap-ty (clsmap-fldmap/null _) _ clsmap-ty/null.

- : lookup-implies-clsmap-ty (clsmap-fldmap/+ CM-FM N1-IN) 
     (fldmap`lookup/= _) (clsmap-ty/notnull N1-IN).

- : lookup-implies-clsmap-ty (clsmap-fldmap/+ CM-FM _) 
     (fldmap`lookup/> LP _) CM-T
     <- lookup-implies-clsmap-ty CM-FM LP CM-T.

- : lookup-implies-clsmap-ty (clsmap-fldmap/null CM-FM)
     (fldmap`lookup/> LP _) CM-T
     <- lookup-implies-clsmap-ty CM-FM LP CM-T.

%worlds () (lookup-implies-clsmap-ty _ _ _).
%total  (CM-FM) (lookup-implies-clsmap-ty CM-FM _ _).


%theorem lookup-implies-clsmap-methty :
        forall* {CM} {MM} {MT} {M}
        forall  {CM-MM: clsmap-methmap CM MM}
                {MM-LP: methmap`lookup MM M MT}
        exists  {CM-MT: clsmap-methty CM MT}
        true.

- : lookup-implies-clsmap-methty
     (clsmap-methmap/meth _ _ CM-MT _) (methmap`lookup/= _) CM-MT.

- : lookup-implies-clsmap-methty
     (clsmap-methmap/const _ _ (CM-MT) CML) (methmap`lookup/= _) 
     (clsmap-methty/args CM-MT (clsmap-ty/notnull (clsmap`domain?/in CML))).

- : lookup-implies-clsmap-methty
     (clsmap-methmap/meth CM-MMP N<<MM=MMP _ _) 
     (methmap`lookup/> MML P1) CM-MT
     <- nat`plus-swap-succ P1 P2
     <- nat`plus-commutative P2 P3
     <- methmap`shift-preserves-lookup* MML N<<MM=MMP P3 MMPL
     <- lookup-implies-clsmap-methty CM-MMP MMPL CM-MT.

- : lookup-implies-clsmap-methty
     (clsmap-methmap/const CM-MMP N<<MM=MMP _ _)
     (methmap`lookup/> MML P1) CM-MT
     <- nat`plus-swap-succ P1 P2
     <- nat`plus-commutative P2 P3
     <- methmap`shift-preserves-lookup* MML N<<MM=MMP P3 MMPL
     <- lookup-implies-clsmap-methty CM-MMP MMPL CM-MT.

%worlds (objvar) (lookup-implies-clsmap-methty _ _ _).
%total  (L) (lookup-implies-clsmap-methty L _ _).


%theorem cxt-lookup-implies-clsmap-ty : 
    forall* {CM} {N} {B: cxt N} {T} {L} {O}
    forall  {CM-B: clsmap-cxt CM B}
            {B-L: cxt-lookup B L O T}
    exists  {CM-T: clsmap-ty CM T}
    true.

- : cxt-lookup-implies-clsmap-ty
    (clsmap-cxt/cons CM-T _) cxt-lookup/hit CM-T.

- : cxt-lookup-implies-clsmap-ty
    (clsmap-cxt/cons _ CM-B) (cxt-lookup/miss B-L _) CM-T
    <- cxt-lookup-implies-clsmap-ty CM-B B-L CM-T.

%worlds (objvar) (cxt-lookup-implies-clsmap-ty _ _ _).
%total (CM-B) (cxt-lookup-implies-clsmap-ty CM-B _ _).


%theorem calltyping-implies-clsmap-ty :
    forall* {CM} {L} {AO} {MT} {T} {GM} {XM}
    forall  {CM-MT: clsmap-methty CM MT}
            {RCTYP: ref-calltyping L AO MT (out/expr T GM XM)}
    exists  {CM-T: clsmap-ty CM T}
    true.

- : calltyping-implies-clsmap-ty
    (clsmap-methty/base CM-T) ref-calltyping/call CM-T.

- : calltyping-implies-clsmap-ty
    (clsmap-methty/args CM-MT _)
    (ref-calltyping/args/borrow _ _ _ _ _ _ _ RTYP-A) CM-T
    <- calltyping-implies-clsmap-ty CM-MT RTYP-A CM-T.
    
- : calltyping-implies-clsmap-ty
    (clsmap-methty/args CM-MT _)
    (ref-calltyping/args/unique _ _ _ _ _ _ RTYP-A) CM-T
    <- calltyping-implies-clsmap-ty CM-MT RTYP-A CM-T.

- : calltyping-implies-clsmap-ty
    (clsmap-methty/args CM-MT _)
    (ref-calltyping/args/shared _ RTYP-A) CM-T
    <- calltyping-implies-clsmap-ty CM-MT RTYP-A CM-T.

%worlds () (calltyping-implies-clsmap-ty _ _ _).
%total (CM-MT) (calltyping-implies-clsmap-ty CM-MT _ _).

    
%theorem typing-implies-clsmap-ty :
    forall* {CM} {MM} {N} {B: cxt N} {E} {T} {L1} {L2} {GM} {XM}
    forall  {CM-CM: clsmap-clsmap CM CM}
            {CM-MM: clsmap-methmap CM MM}
            {CM-B : clsmap-cxt CM B}
            {TYP: ref-typing L1 CM MM B E (out/expr T GM XM) L2}
    exists  {CM-T: clsmap-ty CM T}
    true.

- : typing-implies-clsmap-ty _ _ _ _ clsmap-ty/null.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B (ref-typing/lit B-L) CM-T
    <- cxt-lookup-implies-clsmap-ty CM-B B-L CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/read/shared _ _ _ FM-L CM-L _) CM-T
    <- lookup-implies-clsmap-fldmap CM-CM CM-L CM-FM
    <- lookup-implies-clsmap-ty CM-FM FM-L CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/read/unique _ _ _ FM-L CM-L _) CM-T
    <- lookup-implies-clsmap-fldmap CM-CM CM-L CM-FM
    <- lookup-implies-clsmap-ty CM-FM FM-L CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/write _ _ _ _ _ _ _ RTYP-E2 _) CM-T
    <- typing-implies-clsmap-ty CM-CM CM-MM CM-B RTYP-E2 CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/let _ _ _ _ _ ([x] RTYP-F x) RTYP-E) CM-T
    <- typing-implies-clsmap-ty CM-CM CM-MM CM-B RTYP-E CM-T1
    <- ({x} typing-implies-clsmap-ty 
         CM-CM CM-MM (clsmap-cxt/cons CM-T1 CM-B) (RTYP-F x) CM-T).

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/if _ RTYP-E2 _ _) CM-T
    <- typing-implies-clsmap-ty CM-CM CM-MM CM-B RTYP-E2 CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/call RCTYP _ MM-L _) CM-T
    <- lookup-implies-clsmap-methty CM-MM MM-L CM-MT
    <- calltyping-implies-clsmap-ty CM-MT RCTYP CM-T.

- : typing-implies-clsmap-ty CM-CM CM-MM CM-B
    (ref-typing/const RCTYP _ MM-L _ _) CM-T
    <- lookup-implies-clsmap-methty CM-MM MM-L (clsmap-methty/args CM-MT _)
    <- calltyping-implies-clsmap-ty CM-MT RCTYP CM-T.

%worlds (objvar) (typing-implies-clsmap-ty _ _ _ _ _).
%total  (TYP) (typing-implies-clsmap-ty _ _ _ TYP _).