%%% converting *map to permission

% ==============================================
% ================ Definitions =================
% ==============================================

%abbrev fracperm = fraction -> permission.


set2facts : object -> set -> formula -> type.

set2facts/0 : set2facts _ set/0 f.

set2facts/U
  : set`not-member SP N
    -> set`add SP N S
    -> set2facts O SP G
    -> set2facts O S (disj (objequal O (object/ N)) G).


apply-efx : bool -> efx -> permission -> fracperm -> type.

apply-efx/read : apply-efx _ efx/read Pi ([f] (scale f Pi)).

apply-efx/write : apply-efx _ efx/write Pi ([_] Pi).

apply-efx/consume/true : apply-efx true efx/consume Pi ([_] Pi).

apply-efx/consume/false : apply-efx false efx/consume Pi ([_] empty).


%{ efxmap to permission.
The boolean is used to control whether converted
permission is input or output.
Entry 0.0 should be converted to shared permission.
clsmap should contain a special key 0, which maps
to a special fldmap. }%


inner-efxmap2perm : bool -> object -> fldmap -> predmap ->
  inner-efxmap -> fracperm -> type.

inner-efxmap2perm/0 : inner-efxmap2perm _ _ _ _ inner-efxmap/0 ([_] empty).

inner-efxmap2perm/U
  : inner-efxmap`fresh MP F
    -> inner-efxmap`update MP F X M
    -> fldmap`lookup FM F T
    -> ty2perm PM T PF
    -> apply-efx P X (unitperm (precise-exists O F PF)) Pi1
    -> inner-efxmap2perm P O FM PM MP Pi2
    -> inner-efxmap2perm P O FM PM M ([f] (Pi1 f), Pi2 f).


%{ converting a efxmap to fractional permissions
note that here to make this judgment total, we need
to have the context consistent with the clsmap. }%

efxmap2perm : bool -> clsmap -> predmap -> cxt ->
  efxmap -> fracperm -> type.

efxmap2perm/0 : efxmap2perm _ _ _ _ efxmap/0 ([_] empty).

efxmap2perm/U
  : efxmap`fresh XMP N
    -> efxmap`update XMP N M XM
    -> cxt`lookup B (object/ N) (ty/ _ _ C)
    -> clsmap`lookup CM C FM
    -> inner-efxmap2perm P (object/ N) FM PM M Pi1
    -> efxmap2perm P CM PM B XMP Pi2
    -> efxmap2perm P CM PM B XM ([f] (Pi1 f) , Pi2 f).


%abbrev nested-perm : object -> permission =
    [r] (unitperm (nonlinear (nested (allperm r) null all))).

maybe-efx2perm : maybe-efx -> fracperm -> type.

maybe-efx2perm/nothing : maybe-efx2perm maybe-efx/nothing ([_] empty).

maybe-efx2perm/just
  : maybe-efx2perm (maybe-efx/just X) PF
    <- apply-efx true X (unitperm (basic null all null)) PF.


effects2input : clsmap -> predmap -> cxt -> effects -> fracperm -> type.

effects2input/
  : efxmap2perm true CM PM B XM ([f] Pi1 f)
    -> maybe-efx2perm MX ([f] Pi2 f)
    -> effects2input CM PM B (effects/ XM MX) ([f] Pi1 f , Pi2 f).


%{
06-16-13: efxmap may contain inner-efxmap/0 as value. Do we need to
remove them first? Maybe not, since tgtmap in reftype should definitely
be well-formed.
}%

effects2output : clsmap -> predmap -> cxt ->
    reftype -> effects -> (fraction -> expr-output) -> type.

effects2output/shared
  : efxmap2perm false CM PM B XM ([f] Pi1 f)
    -> maybe-efx2perm MX ([f] Pi2 f)
    -> ty2perm PM (ty/ NN annot/shared C) PF
    -> effects2output CM PM B
      (reftype/ NN C targets/shared) (effects/ XM MX)
      ([f] output/exists [r]
        (output/expr r % shared don't need scaling
          (combine (PF r) (combine (Pi1 f) (Pi2 f))))).

%%% different cases for unique targets

effects2output/unique/0
  : set`size S z
    -> tgtmap`size GM z
    -> efxmap2perm false CM PM B XM ([f] Pi1 f)
    -> maybe-efx2perm MX ([f] Pi2 f)
    -> ty2perm PM (ty/ NN annot/unique C) PF
    -> effects2output CM PM B
      (reftype/ NN C (targets/unique S GM false)) (effects/ XM MX)
      ([f] output/exists [r]
        (output/expr r
          (combine (PF r)
            (combine (Pi1 f) (Pi2 f))))).

effects2output/unique/0f
  : set`size S (s _)
    -> tgtmap`size GM z
    -> efxmap2perm false CM PM B XM ([f] Pi1 f)
    -> ({r} set2facts r S (GF r))
    -> maybe-efx2perm MX ([f] Pi2 f)
    -> ty2perm PM (ty/ NN annot/borrow C) PF % no permission
    -> effects2output CM PM B
      (reftype/ NN C (targets/unique S GM false)) (effects/ XM MX)
      ([f] output/exists [r]
        (output/expr r
          (combine (scale f (PF r))
            (combine (unitperm (nonlinear (GF r)))
              (combine (Pi1 f) (Pi2 f)))))).

% we don't require set to be empty here.

effects2output/unique/0t
  : tgtmap`size GM z
    -> efxmap2perm false CM PM B XM ([f] Pi1 f)
    -> ({r} set2facts r S (GF r))
    -> maybe-efx2perm MX ([f] Pi2 f)
    -> ty2perm PM (ty/ NN annot/unique C) PF
    -> effects2output CM PM B
      (reftype/ NN C (targets/unique S GM true)) (effects/ XM MX)
      ([f] output/exists [o]
        (output/exists [r]
          (output/expr r
            (combine (unitperm (nonlinear (disj (objequal o r) (GF o))))
              (combine (scale f (PF r))
                (combine (unitperm (encumbered (scale f (PF r)) (Pi2 f)))
                  (Pi1 f))))))).

effects2output/unique/+f
  : tgtmap`size GM (s _)
    -> select-tgtmap XM GM XM1 XM2
    -> efxmap2perm false CM PM B XM1 ([f] Pi1 f)
    -> efxmap2perm false CM PM B XM2 ([f] Pi2 f)
    -> ({r} set2facts r S (GF r))
    -> maybe-efx2perm MX ([f] Pi3 f)
    -> ty2perm PM (ty/ NN annot/unique C) PF
    -> effects2output CM PM B
      (reftype/ NN C (targets/unique S GM false)) (effects/ XM MX)
      ([f] output/exists [o]
        (output/exists [r]
          (output/expr o
            (combine (unitperm (nonlinear (disj (objequal o r) (GF o))))
              (combine (scale f (PF r))
                (combine (unitperm (encumbered (scale f (PF r)) (Pi2 f)))
                  (combine (Pi1 f) (Pi3 f)))))))).

effects2output/unique/+t
  : tgtmap`size GM (s _)
    -> select-tgtmap XM GM XM1 XM2
    -> efxmap2perm false CM PM B XM1 ([f] Pi1 f)
    -> efxmap2perm false CM PM B XM2 ([f] Pi2 f)
    -> ({r} set2facts r S (GF r))
    -> maybe-efx2perm MX ([f] Pi3 f)
    -> ty2perm PM (ty/ NN annot/unique C) PF
    -> effects2output CM PM B
      (reftype/ NN C (targets/unique S GM true)) (effects/ XM MX)
      ([f] output/exists [o]
        (output/exists [r]
          (output/expr o
            (combine (unitperm (nonlinear (disj (objequal o r) (GF o))))
              (combine (scale f (PF r))
                (combine
                  (unitperm
                    (encumbered
                      (scale f (PF r)) (combine (Pi2 f) (Pi3 f))))
                  (Pi1 f))))))).


%{ Context => Permission
Here for unique references, we need to generate encumbered permission
for all fields of its class. This is because we carve the field permissions
out of *all* permission before we check method body, and use each
field permission individually. But, in case we want to consume a unique
parameter, we need to get back the all permission, with the encumbered
permission. }%

fldmap2perm : predmap -> fldmap -> object -> permission -> type.

fldmap2perm/0 : fldmap2perm _ fldmap/0 _ empty.

fldmap2perm/U
  : fldmap2perm PM FMP O Pi
    -> fldmap`fresh FMP F
    -> fldmap`update FMP F T FM
    -> ty2perm PM T PF
    -> fldmap2perm PM FM O (unitperm (precise-exists O F PF) , Pi).


cxt2perm : clsmap -> predmap -> cxt -> permission -> type.

cxt2perm/nil : cxt2perm _ _ cxt/nil empty.

cxt2perm/cons/unique
  : cxt2perm CM PM B Pi
    -> clsmap`lookup CM C FM
    -> ({o} fldmap2perm PM FM o (AF o))
    -> nn2perm NN AF PF
    -> predmap`lookup PM C CP
    -> cxt2perm CM PM (cxt/cons O (ty/ NN annot/unique C) B) ((PF CP O) , Pi).

cxt2perm/cons/shared
  : cxt2perm CM PM B Pi
    -> ty2perm PM (ty/ NN annot/shared C) PF
    -> cxt2perm CM PM (cxt/cons O (ty/ NN annot/shared C) B) ((PF O) , Pi).

cxt2perm/cons/borrow
  : cxt2perm CM PM B Pi
    -> ty2perm PM (ty/ NN annot/borrow C) PF
    -> cxt2perm CM PM (cxt/cons O (ty/ NN annot/borrow C) B) ((PF O) , Pi).


% The first set of permissions are those from read and write effects,
% the second set of permissions are those from consumed effects.

methty2proctype* : predmap ->
    fracperm -> permission -> methty -> proctype -> type.

methty2proctype/base
  : methty2proctype* PM ([f] Pi f) Pic (methty/base Rty MX)
    (proctype/forall [f]
      (proctype/base
        (combine (Pi f)
          (combine Pic (Pis f)))
        (output/exists [ret]
          (output/expr ret
            (combine (PF ret)
              (combine (Pi f) (Pis f)))))))
    <- ty2perm PM Rty PF
    <- maybe-efx2perm MX ([f] Pis f).


methty2proctype/args/unique
  : methty2proctype* PM ([f] Pi f) Pic
    (methty/arg (ty/ NN annot/unique C) set/0 set/0 MT) (proctype/arg PTF)
    <- ty2perm PM (ty/ NN annot/unique C) PF
    <- ({x} methty2proctype* PM ([f] Pi f) (Pic , (PF x)) MT (PTF x)).

methty2proctype/args/borrow
  : methty2proctype* PM ([f] Pi1 f) Pic
    (methty/arg (ty/ NN annot/borrow C) Rs Ws MT) (proctype/arg PTF)
    <- set2inner-efxmap Rs efx/read Mr
    <- set2inner-efxmap Ws efx/write Mw
    <- inner-efxmap`join Mr Mw M
    <- ty2perm PM (ty/ NN annot/borrow C) PF
    <- ({x} inner-efxmap2perm true x FM PM M ([f] Pi2 x f))
    <- ({x} methty2proctype* PM
         ([f] (Pi1 f , (Pi2 x f))) (Pic , PF x) MT (PTF x)).

methty2proctype/args/shared
  : methty2proctype* PM ([f] Pi f) Pic
    (methty/arg (ty/ NN annot/shared C) set/0 set/0 MT) (proctype/arg PTF)
    <- ty2perm PM (ty/ NN annot/shared C) PF
    <- ({x} methty2proctype* PM ([f] Pi f) Pic MT (PTF x)).


%abbrev methty2proctype
  = [PM][MT][PT] methty2proctype* PM ([_] empty) empty MT PT.


consty2proctype*
  : predmap -> fldmap -> fracperm
    -> permission -> methty -> (object -> proctype) -> type.


%abbrev obj-notnull : object -> permission
    = ([r] unitperm (nonlinear (neg (objequal r (object/ z))))).

consty2proctype/base
  : consty2proctype* PM FM ([f] Pi f) Pic
    (methty/base (ty/ nn/yes annot/unique C) MX)
    ([t] (proctype/forall [f]
           (proctype/base
             (combine (obj-notnull t)
               (combine (OS t)
                 (combine (Pi f)
                   (combine Pic (Pis f)))))
             (output/exists [ret]
               (output/expr ret
                 (combine (PF ret)
                   (combine (Pi f) (Pis f))))))))
    <- ty2perm PM (ty/ nn/yes annot/unique C) PF
    <- fldmap`domain FM S
    <- ({t} allocFields t S (OS t)).


consty2proctype/args/unique
  : consty2proctype* PM FM ([f] Pi f) Pic
    (methty/arg (ty/ NN annot/unique C) set/0 set/0 CT)
    ([t] proctype/arg (PTF t))
    <- ty2perm PM (ty/ NN annot/unique C) PF
    <- ({x} consty2proctype* PM FM ([f] Pi f) (Pic , PF x) CT ([t] (PTF t x))).

consty2proctype/args/borrow
  : consty2proctype* PM FM ([f] Pi1 f) Pic
    (methty/arg (ty/ NN annot/borrow C) Rs Ws CT)
    ([t] proctype/arg (CTF t))
    <- set2inner-efxmap Rs efx/read Mr
    <- set2inner-efxmap Ws efx/write Mw
    <- inner-efxmap`join Mr Mw M
    <- ty2perm PM (ty/ NN annot/borrow C) PF
    <- ({x} inner-efxmap2perm true x FM PM M ([f] Pi2 x f))
    <- ty2perm PM (ty/ NN annot/unique C) PF
    <- ({x} consty2proctype* PM FM ([f] (Pi1 f , (Pi2 x f)))
         (Pic , PF x) CT ([t] (CTF t x))).

consty2proctype/args/shared
  : consty2proctype* PM FM ([f] Pi f) Pic
    (methty/arg (ty/ NN annot/shared C) set/0 set/0 CT)
    ([t] proctype/arg (CTF t))
    <- ty2perm PM (ty/ NN annot/shared C) PF
    <- ({x} consty2proctype* PM FM ([f] Pi f) Pic CT (CTF x)).

%abbrev consty2proctype
   = [PM][FM][CT][PTF] consty2proctype* PM FM ([_] empty) empty CT PTF.


methmap2progtype : clsmap -> predmap -> methmap -> progtype -> type.

methmap2progtype/0 : methmap2progtype _ _ methmap`map/0 progtype`map/0.

methmap2progtype/U/c
  : methmap2progtype CM PM MM W
    <- methmap`fresh MMP N
    <- methmap`update MMP N
      (methty/arg (ty/ nn/yes annot/unique C) set/0 set/0 CT) MM
    <- clsmap`lookup CM N FM
    <- consty2proctype PM FM CT PTF
    <- methmap2progtype CM PM MMP WP
    <- progtype`update WP N (proctype/arg PTF) W.

methmap2progtype/U/m
  : methmap2progtype CM PM MM W
    <- methmap`fresh MMP N
    <- methmap`update MMP N MT MM
    <- methty2proctype PM MT PT
    <- methmap2progtype CM PM MMP WP
    <- progtype`update WP N PT W.


env2progtype : clsmap -> methmap -> progtype -> type.

env2progtype/
  : env2progtype CM MM W
    <- clsmap2predmap CM PM
    <- methmap2progtype CM PM MM W.



% ==============================================
% ================= Theorems ===================
% ==============================================

% =========================================
% First, extra permission-related theorems
% =========================================


%theorem permission`eq-implies-equiv
  : forall* {Pi1} {Pi2}
    forall {EQ: permission`eq Pi1 Pi2}
    exists {EQV: equiv Pi1 Pi2}
    true.

- : permission`eq-implies-equiv permission`eq/ equiv/reflexive.

%worlds (objvar | fracvar) (permission`eq-implies-equiv _ _).
%total {} (permission`eq-implies-equiv _ _).


%theorem permission`combine-preserves-eq
  : forall* {Pi1} {Pi2} {Pi3} {Pi4}
    forall {EQ: permission`eq Pi1 Pi3}
    {EQ: permission`eq Pi2 Pi4}
    exists {EQ: permission`eq (Pi1 , Pi2) (Pi3 , Pi4)}
    true.

- : permission`combine-preserves-eq
    permission`eq/ permission`eq/ permission`eq/.

%worlds (objvar) (permission`combine-preserves-eq _ _ _).
%total {} (permission`combine-preserves-eq _ _ _).


%theorem permission`implies-respects-eq-left
  : forall* {P1} {P2} {P}
    forall {EQ: permission`eq P1 P2}
    {IMP: implies P1 P}
    exists {IMP: implies P2 P}
    true.

- : permission`implies-respects-eq-left permission`eq/ IMP IMP.

%worlds () (permission`implies-respects-eq-left _ _ _).
%total {} (permission`implies-respects-eq-left _ _ _).


%theorem permission`implies-respects-eq-right
  : forall* {P1} {P2} {P}
    forall {EQ: permission`eq P1 P2}
    {IMP: implies P P1}
    exists {IMP: implies P P2}
    true.

- : permission`implies-respects-eq-right permission`eq/ IMP IMP.

%worlds () (permission`implies-respects-eq-right _ _ _).
%total {} (permission`implies-respects-eq-right _ _ _).



% ================
% false-implies-XX
% ================

%theorem false-implies-set2facts
  : forall* {O} {S} {G}
    forall {V:void}
    exists {S=>G: set2facts O S G}
    true.

%worlds () (false-implies-set2facts _ _).
%total {} (false-implies-set2facts _ _).


%theorem false-implies-apply-efx
  : forall* {B} {X} {Pi} {Pi2}
    forall {V:void}
    exists {AP: apply-efx B X Pi Pi2}
    true.

%worlds () (false-implies-apply-efx _ _).
%total {} (false-implies-apply-efx _ _).

%theorem false-implies-inner-efxmap2perm
  : forall* {P} {O} {FM} {PM} {M} {Pi}
    forall {V:void}
    exists {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    true.

%worlds (fracvar) (false-implies-inner-efxmap2perm _ _).
%total {} (false-implies-inner-efxmap2perm _ _).


%theorem false-implies-efxmap2perm
  : forall* {CM} {PM} {B} {XM} {P} {Pi}
    forall  {V: void}
    exists  {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    true.

%worlds (fracvar) (false-implies-efxmap2perm _ _).
%total {} (false-implies-efxmap2perm _ _).


% ================================
% totality (constructive) theorems
% ================================

%theorem set2facts-total*
  : forall {O}{S} exists {G} {S=>G: set2facts O S G} true.

- : set2facts-total* _ _ f set2facts/0.

- : set2facts-total*
    O _ _ (set2facts/U NM A SP=>G)
    <- set`lookup-implies-not-member-update
      (set`lookup/= nat`eq/) SP NM A
    <- set2facts-total* O SP _ SP=>G.

%worlds (objvar) (set2facts-total* _ _ _ _).
%trustme %total (S) (set2facts-total* _ S _ _).

%abbrev set2facts-total = set2facts-total* _ _ _.


%theorem apply-efx-total*
  : forall {P} {X} {Pi}
    exists {PF} {AP: apply-efx P X Pi PF}
    true.

- : apply-efx-total* _ efx/read _ _ apply-efx/read.

- : apply-efx-total* _ efx/write _ _ apply-efx/write.

- : apply-efx-total* true efx/consume Pi ([_] Pi) apply-efx/consume/true.

- : apply-efx-total* false efx/consume Pi ([_] empty) apply-efx/consume/false.

%worlds (fracvar | objvar) (apply-efx-total* _ _ _ _ _).
%total {} (apply-efx-total* _ _ _ _ _).

%abbrev apply-efx-total = apply-efx-total* _ _ _ _.


%theorem maybe-efx2perm-total*
  : forall {MX}
    exists {Pi} {MX=>Pi: maybe-efx2perm MX Pi}
    true.

- : maybe-efx2perm-total* maybe-efx/nothing _ maybe-efx2perm/nothing.

- : maybe-efx2perm-total* (maybe-efx/just _) _ (maybe-efx2perm/just AP)
    <- apply-efx-total AP.

%worlds (objvar) (maybe-efx2perm-total* _ _ _).
%total {} (maybe-efx2perm-total* _ _ _).

%abbrev maybe-efx2perm-total = maybe-efx2perm-total* _ _.


%{ fldmap2perm is NOT total - the field map has to be
consistent with the clsmap. }%

%theorem clsmap-fldmap-implies-fldmap2perm
  : forall* {CM} {PM} {FM} {O}
    forall {CM2PM: clsmap2predmap CM PM}
    {CM-FM: clsmap-fldmap CM FM}
    exists {Pi:permission} {FM=>Pi: fldmap2perm PM FM O Pi}
    true.

%theorem clsmap-fldmap-implies-fldmap2perm/L
  : forall* {CM} {PM} {FM} {O}
    forall {N} {SZ: fldmap`size FM N}
    {CM2PM: clsmap2predmap CM PM}
    {CM-FM: clsmap-fldmap CM FM}
    exists {Pi:permission} {FM=>Pi: fldmap2perm PM FM O Pi}
    true.

- : clsmap-fldmap-implies-fldmap2perm/L
    _ fldmap`size/0 _ clsmap-fldmap/0 _ fldmap2perm/0.

- : clsmap-fldmap-implies-fldmap2perm/L _ (fldmap`size/+ SZ)
    CM2PM (clsmap-fldmap/+ CM-FMP CM-T) _ (fldmap2perm/U FMS=>PS F U T2PF)
    <- fldmap`shift-total SH-FMP=FMS
    <- fldmap`shift-preserves-size SZ SH-FMP=FMS SZS
    <- shift-preserves-clsmap-fldmap CM-FMP SH-FMP=FMS CM-FMS
    <- fldmap`shift-implies-fresh SH-FMP=FMS F
    <- fldmap`shift-implies-update SH-FMP=FMS U
    <- clsmap-ty-implies-ty2perm CM2PM CM-T _ T2PF
    <- clsmap-fldmap-implies-fldmap2perm/L _ SZS CM2PM CM-FMS _ FMS=>PS.

- : clsmap-fldmap-implies-fldmap2perm CM2PM CM-FM P FM2P
    <- fldmap`size-total SZ
    <- clsmap-fldmap-implies-fldmap2perm/L _ SZ CM2PM CM-FM P FM2P.

%worlds (objvar) (clsmap-fldmap-implies-fldmap2perm/L _ _ _ _ _ _).
%total (N) (clsmap-fldmap-implies-fldmap2perm/L N _ _ _ _ _).

%worlds (objvar) (clsmap-fldmap-implies-fldmap2perm _ _ _ _).
%total {} (clsmap-fldmap-implies-fldmap2perm _ _ _ _).


%theorem clsmap-cxt-implies-cxt2perm
  : forall* {CM} {PM} {B}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {CM-B: clsmap-cxt CM B}
    exists {Pi:permission} {B=>Pi: cxt2perm CM PM B Pi}
    true.

- : clsmap-cxt-implies-cxt2perm _ _ clsmap-cxt/nil _ cxt2perm/nil.

%worlds (objvar) (clsmap-cxt-implies-cxt2perm _ _ _ _ _).
%trustme %total (CM-B) (clsmap-cxt-implies-cxt2perm _ _ CM-B _ _).


%theorem domain-leq-implies-inner-efxmap2perm/L
  : forall* {CM} {PM} {C} {FM} {M} {P} {O} {FS} {MS}
    forall {N} {SZ: inner-efxmap`size M N}
    {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {CM-L: clsmap`lookup CM C FM}
    {DM-FM: fldmap`domain FM FS}
    {DM-M: inner-efxmap`domain M MS}
    {LE: set`leq MS FS}
    exists {Pi} {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    true.

- : domain-leq-implies-inner-efxmap2perm/L
    z _ _ _ _ _ _ _ ([_] empty) inner-efxmap2perm/0.

- : domain-leq-implies-inner-efxmap2perm/L (s N) SZ
    CM-CM CM2PM CM->C=FM DM-FM=FS DM-M=MS MS<=FS _
    (inner-efxmap2perm/U N!<MP MP+F+X=M FM->F=T T=>PF X+PF=>Pi1 MP=>Pi2)
    <- inner-efxmap`lookup-implies-fresh-update
      (inner-efxmap`lookup/= nat`eq/) MP N!<MP MP+F+X=M
    <- lookup-implies-clsmap-fldmap CM-CM CM->C=FM CM-FM
    <- inner-efxmap`domain-preserves-lookup
      (inner-efxmap`lookup/= nat`eq/) DM-M=MS N-in-MS
    <- set`member-respects-leq N-in-MS MS<=FS N-in-FS
    <- fldmap`domain-preserves-lookup-converse N-in-FS DM-FM=FS T FM->F=T
    <- lookup-implies-clsmap-ty CM-FM FM->F=T CM-T
    <- clsmap-ty-implies-ty2perm CM2PM CM-T _ T=>PF
    <- apply-efx-total X+PF=>Pi1
    <- inner-efxmap`fresh-update-implies-leq N!<MP MP+F+X=M MP<=M
    <- inner-efxmap`domain-total DM-MP=MPS
    <- inner-efxmap`domain-preserves-leq* MP<=M DM-MP=MPS DM-M=MS MPS<=MS
    <- set`leq-transitive MPS<=MS MS<=FS MPS<=FS
    <- inner-efxmap`fresh-update-increases-size-converse SZ N!<MP MP+F+X=M SZP
    <- domain-leq-implies-inner-efxmap2perm/L N SZP
      CM-CM CM2PM CM->C=FM DM-FM=FS DM-MP=MPS MPS<=FS _ MP=>Pi2.

%worlds (fracvar | objvar)
(domain-leq-implies-inner-efxmap2perm/L _ _ _ _ _ _ _ _ _ _).
%total (N) (domain-leq-implies-inner-efxmap2perm/L N _ _ _ _ _ _ _ _ _).


%theorem domain-leq-implies-inner-efxmap2perm
  : forall* {CM} {PM} {C} {FM} {M} {P} {O} {FS} {MS}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {CM-L: clsmap`lookup CM C FM}
    {DM-FM: fldmap`domain FM FS}
    {DM-M: inner-efxmap`domain M MS}
    {LE: set`leq MS FS}
    exists {Pi} {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    true.

- : domain-leq-implies-inner-efxmap2perm
    CM-CM CM2PM CM-L DM-FM DM-M LE _ M=>Pi
    <- inner-efxmap`size-total SZ
    <- domain-leq-implies-inner-efxmap2perm/L
      _ SZ CM-CM CM2PM CM-L DM-FM DM-M LE _ M=>Pi.

%worlds (fracvar | objvar) (domain-leq-implies-inner-efxmap2perm _ _ _ _ _ _ _ _).
%total {} (domain-leq-implies-inner-efxmap2perm _ _ _ _ _ _ _ _).


%theorem clsmap-cxt-efxmap-implies-efxmap2perm
  : forall* {P} {CM} {PM} {B} {XM}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {B-XM: clsmap-cxt-efxmap CM B XM}
    exists {Pi} {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    true.

- : clsmap-cxt-efxmap-implies-efxmap2perm _ _ clsmap-cxt-efxmap/0 _ efxmap2perm/0.

- : clsmap-cxt-efxmap-implies-efxmap2perm CM-CM CM2PM
    (clsmap-cxt-efxmap/U B-XM F U BL L DM-FM DM-M LE)
    ([f] (Pi1 f) , Pi2 f) (efxmap2perm/U F U BL L M2P XM2P)
    <- domain-leq-implies-inner-efxmap2perm
      CM-CM CM2PM L DM-FM DM-M LE Pi1 M2P
    <- clsmap-cxt-efxmap-implies-efxmap2perm CM-CM CM2PM B-XM Pi2 XM2P.

%worlds (fracvar | objvar) (clsmap-cxt-efxmap-implies-efxmap2perm _ _ _ _ _).
%total (T) (clsmap-cxt-efxmap-implies-efxmap2perm _ _ T _ _).


%theorem clsmap-cxt-efxmap-implies-effects2input
  : forall* {CM} {PM} {B} {XM} {MX}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM} {B-XM: clsmap-cxt-efxmap CM B XM}
    exists {Pi} {XX=>Pi: effects2input CM PM B (effects/ XM MX) Pi}
    true.

- : clsmap-cxt-efxmap-implies-effects2input CM-CM CM2PM B-XM _
    (effects2input/ XM=>Pi1 MX=>Pi2)
    <- clsmap-cxt-efxmap-implies-efxmap2perm CM-CM CM2PM B-XM _ XM=>Pi1
    <- maybe-efx2perm-total MX=>Pi2.

%worlds () (clsmap-cxt-efxmap-implies-effects2input _ _ _ _ _).
%total {} (clsmap-cxt-efxmap-implies-effects2input _ _ _ _ _).


%% TODO: GM needs to be wf too!

%theorem clsmap-cxt-efxmap-implies-effects2output
  : forall* {B} {CM} {PM} {XM} {MX}
    forall {RT} {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {B-XM: clsmap-cxt-efxmap CM B XM}
    {CM-RT: clsmap-cxt-reftype CM B RT}
    exists {Out}
    {XX=>Out: effects2output CM PM B RT (effects/ XM MX) ([f] Out f)}
    true.

%worlds (objvar) (clsmap-cxt-efxmap-implies-effects2output _ _ _ _ _ _ _).
%trustme %total {} (clsmap-cxt-efxmap-implies-effects2output _ _ _ _ _ _ _).


% =======================
% XX-respects-eq theorems
% =======================

%theorem apply-efx-respects-eq
  : forall* {B1} {B2} {X1} {X2} {P1} {P2} {S1} {S2}
    forall {AP: apply-efx B1 X1 P1 S1}
    {E: bool`eq B1 B2} {E: efx`eq X1 X2}
    {E: permission`eq P1 P2} {E: ({f} permission`eq (S1 f) (S2 f))}
    exists {AP: apply-efx B2 X2 P2 S2}
    true.

- : apply-efx-respects-eq apply-efx/read bool`eq/
    efx`eq/ permission`eq/ ([_] permission`eq/) apply-efx/read.

- : apply-efx-respects-eq apply-efx/write bool`eq/
    efx`eq/ permission`eq/ ([_] permission`eq/) apply-efx/write.

- : apply-efx-respects-eq apply-efx/consume/false bool`eq/
    efx`eq/ permission`eq/ ([_] permission`eq/) apply-efx/consume/false.

- : apply-efx-respects-eq apply-efx/consume/true bool`eq/
    efx`eq/ permission`eq/ ([_] permission`eq/) apply-efx/consume/true.

%worlds () (apply-efx-respects-eq _ _ _ _ _ _).
%total {} (apply-efx-respects-eq _ _ _ _ _ _).


%theorem inner-efxmap2perm-respects-eq
  : forall* {P1} {P2} {O1} {O2} {FM1} {FM2}
    {PM1} {PM2} {M1} {M2} {Pi1} {Pi2}
    forall {M1=>Pi1: inner-efxmap2perm P1 O1 FM1 PM1 M1 Pi1}
    {E: bool`eq P1 P2} {O: object`eq O1 O2}
    {E: fldmap`eq FM1 FM2} {E: predmap`eq PM1 PM2}
    {E: inner-efxmap`eq M1 M2} {E: ({f} permission`eq (Pi1 f) (Pi2 f))}
    exists {M2=>Pi2: inner-efxmap2perm P2 O2 FM2 PM2 M2 Pi2}
    true.

- : inner-efxmap2perm-respects-eq M=>Pi bool`eq/ object`eq/ fldmap`eq/
    predmap`eq/ inner-efxmap`eq/ ([_] permission`eq/) M=>Pi.

%worlds (fracvar) (inner-efxmap2perm-respects-eq _ _ _ _ _ _ _ _).
%total {} (inner-efxmap2perm-respects-eq _ _ _ _ _ _ _ _).


%theorem efxmap2perm-respects-eq
  : forall* {CM1} {PM1} {B1} {XM1} {Pi1} {P1}
    {CM2} {PM2} {B2} {XM2} {Pi2} {P2}
    forall  {XM1=>Pi1: efxmap2perm P1 CM1 PM1 B1 XM1 Pi1}
    {E: bool`eq P1 P2} {E: clsmap`eq CM1 CM2} {E: predmap`eq PM1 PM2}
    {E: cxt`eq B1 B2} {E: efxmap`eq XM1 XM2}
    {E: {f} permission`eq (Pi1 f) (Pi2 f)}
    exists  {XM2=>Pi2: efxmap2perm P2 CM2 PM2 B2 XM2 Pi2}
    true.

- : efxmap2perm-respects-eq XM=>Pi bool`eq/ clsmap`eq/ predmap`eq/
    cxt`eq/ efxmap`eq/ ([_] permission`eq/) XM=>Pi.

%worlds (fracvar) (efxmap2perm-respects-eq _ _ _ _ _ _ _ _).
%total {} (efxmap2perm-respects-eq _ _ _ _ _ _ _ _).


%theorem effects2output-respects-eq
  : forall* {CM} {PM} {B} {NN} {C} {GS} {XM} {MX}
    {NN1} {C1} {XM1} {GS1} {MX1} {Out}
    forall {XX2O: effects2output CM PM B (reftype/ NN C GS) (effects/ XM MX) Out}
    {E: nonnull`eq NN NN1} {E: nat`eq C C1}
    {E: targets`eq GS GS1} {E: efxmap`eq XM XM1} {E: maybe-efx`eq MX MX1}
    exists
    {XX2O: effects2output CM PM B
           (reftype/ NN1 C1 GS1) (effects/ XM1 MX1) Out}
    true.

- : effects2output-respects-eq XX nonnull`eq/ nat`eq/ targets`eq/ efxmap`eq/
    maybe-efx`eq/ XX.

%worlds () (effects2output-respects-eq _ _ _ _ _ _ _).
%total { } (effects2output-respects-eq _ _ _ _ _ _ _).


% ===================
% uniqueness theorems
% ===================

%% set2facts is NOT unique, but results are "equivalent"

%theorem set2facts-equiv
  : forall* {O1} {O2} {S1} {S2} {G1} {G2}
    forall {S1=>G1: set2facts O1 S1 G1}
    {S2=>G2: set2facts O2 S2 G2}
    {E: object`eq O1 O2} {E: set`eq S1 S2}
    exists {EQV: equiv (unitperm (nonlinear G1)) (unitperm (nonlinear G2))}
    true.

%worlds (objvar | fracvar) (set2facts-equiv _ _ _ _ _).
%trustme %total {} (set2facts-equiv _ _ _ _ _).


%theorem apply-efx-deterministic
  : forall* {P1} {P2} {X1} {X2} {Pi1} {Pi2} {PF1} {PF2}
    forall {AP1: apply-efx P1 X1 Pi1 PF1}
    {AP2: apply-efx P2 X2 Pi2 PF2}
    {E: bool`eq P1 P2}
    {E: efx`eq X1 X2}
    {E: permission`eq Pi1 Pi2}
    exists {E: {f} permission`eq (PF1 f) (PF2 f)}
    true.

- : apply-efx-deterministic AP1 AP2
    bool`eq/ efx`eq/ permission`eq/ ([_] permission`eq/).

%worlds () (apply-efx-deterministic _ _ _ _ _ _).
%total {} (apply-efx-deterministic _ _ _ _ _ _).

%abbrev apply-efx-unique = apply-efx-deterministic.


%theorem inner-efxmap2perm/U-inversion
  : forall* {P} {M} {MP} {O} {FM} {PM} {N} {X} {Pi}
    forall {M2P: inner-efxmap2perm P O FM PM M ([f] (Pi f))}
    {FS: inner-efxmap`fresh MP N}
    {U: inner-efxmap`update MP N X M}
    exists {T} {L: fldmap`lookup FM N T}
    {PF} {T2PF: ty2perm PM T PF}
    {Pi1} {AP: apply-efx P X (unitperm (precise-exists O N PF)) Pi1}
    {Pi2} {M2P: inner-efxmap2perm P O FM PM MP Pi2}
    {EQV: {f} equiv ((Pi1 f) , (Pi2 f)) (Pi f)}
    true.

%theorem inner-efxmap2perm/U-inversion/L
  : forall* {P} {M} {O} {FM} {PM} {N1} {N2} {T1} {B}
    {X1} {X2} {MP1} {MP2} {PF1} {Pi11} {Pi21}
    forall {FS1: inner-efxmap`fresh MP1 N1}
    {U1: inner-efxmap`update MP1 N1 X1 M}
    {L1: fldmap`lookup FM N1 T1}
    {T2PF1: ty2perm PM T1 PF1}
    {AP1: apply-efx P X1 (unitperm (precise-exists O N1 PF1)) Pi11}
    {M2P1: inner-efxmap2perm P O FM PM MP1 Pi21}
    {FS2: inner-efxmap`fresh MP2 N2}
    {U2: inner-efxmap`update MP2 N2 X2 M}
    {EQ: nat`eq? N2 N1 B}
    exists {T2} {L2: fldmap`lookup FM N2 T2}
    {PF2} {T2PF2: ty2perm PM T2 PF2}
    {Pi12} {AP2: apply-efx P X2 (unitperm (precise-exists O N2 PF2)) Pi12}
    {Pi22} {M2P2: inner-efxmap2perm P O FM PM MP2 Pi22}
    {EQV: {f} equiv ((Pi12 f) , (Pi22 f)) ((Pi11 f) , (Pi21 f))}
    true.

- : inner-efxmap2perm/U-inversion/L F1 U1 L1 T2PF1 AP1 M2P1
    F2 U2 nat`eq?/yes _ L1 _ T2PF1 _ AP2 _ M2P2 ([_] equiv/reflexive)
    <- inner-efxmap`fresh-update-cancels
      F1 U1 F2 U2 nat`eq/ inner-efxmap`eq/ D1=D2 M1=M2
    <- inner-efxmap2perm-respects-eq M2P1 bool`eq/ object`eq/
      fldmap`eq/ predmap`eq/ M1=M2 ([_] permission`eq/) M2P2
    <- apply-efx-respects-eq AP1 bool`eq/ D1=D2 permission`eq/
      ([_] permission`eq/) AP2.

- : inner-efxmap2perm/U-inversion/L F1 U1 L1 T2PF1 AP1 M2P1
    F2 U2 (nat`eq?/no N2<>N1) _ L2 _ T2PF2 _ AP2 _
    (inner-efxmap2perm/U F4 U4 L1 T2PF1 AP1 M2P2)
    ([f] equiv/transitive (equiv/roll2) (equiv/combine equiv/reflexive (EQV2 f)))
    <- inner-efxmap`update-commutes-converse U2 U1 N2<>N1 _ U4 U3
    <- inner-efxmap`update-preserves-fresh-converse F2 U4 F3
    <- inner-efxmap`update-preserves-fresh-converse F1 U3 F4
    <- inner-efxmap2perm/U-inversion M2P1 F3 U3 _ L2 _ T2PF2 _ AP2 _ M2P2 EQV2.

- : inner-efxmap2perm/U-inversion
    (inner-efxmap2perm/U F1 U1 L1 T2PF1 AP1 M2P1)
    F2 U2 _ L2 _ T2PF2 _ AP2 _ M2P2 EQV
    <- nat`eq?-total E?
    <- inner-efxmap2perm/U-inversion/L
      F1 U1 L1 T2PF1 AP1 M2P1 F2 U2 E? _ L2 _ T2PF2 _ AP2 _ M2P2 EQV.

%worlds ()
(inner-efxmap2perm/U-inversion _ _ _ _ _ _ _ _ _ _ _ _)
(inner-efxmap2perm/U-inversion/L _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%total (A B)
(inner-efxmap2perm/U-inversion A _ _ _ _ _ _ _ _ _ _ _)
(inner-efxmap2perm/U-inversion/L _ _ _ _ _ B _ _ _ _ _ _ _ _ _ _ _ _).


%theorem inner-efxmap2perm-equiv
  : forall* {P1} {P2} {O1} {O2} {FM1} {FM2}
    {PM1} {PM2} {M1} {M2} {Pi1} {Pi2}
    forall {M1=>Pi1: inner-efxmap2perm P1 O1 FM1 PM1 M1 Pi1}
    {M2=>Pi2: inner-efxmap2perm P2 O2 FM2 PM2 M2 Pi2}
    {E: bool`eq P1 P2}
    {E: object`eq O1 O2}
    {E: fldmap`eq FM1 FM2}
    {E: predmap`eq PM1 PM2}
    {E: inner-efxmap`eq M1 M2}
    exists {EQV: {f} equiv (Pi1 f) (Pi2 f)}
    true.

%worlds () (inner-efxmap2perm-equiv _ _ _ _ _ _ _ _).
%trustme %total {} (inner-efxmap2perm-equiv _ _ _ _ _ _ _ _).


%theorem efxmap2perm/U-inversion
  : forall* {P} {M} {MP} {CM} {PM} {N} {X} {Pi} {B}
    forall {M2P: efxmap2perm P CM PM B M ([f] (Pi f))}
    {FS: efxmap`fresh MP N}
    {U: efxmap`update MP N X M}
    exists {NN} {A} {C} {BL: cxt`lookup B (object/ N) (ty/ NN A C)}
    {FM} {CML: clsmap`lookup CM C FM}
    {Pi1} {X2P: inner-efxmap2perm P (object/ N) FM PM X Pi1}
    {Pi2} {M2P: efxmap2perm P CM PM B MP Pi2}
    {EQV: {f} equiv ((Pi1 f) , (Pi2 f)) (Pi f)}
    true.

%theorem efxmap2perm/U-inversion/L
  : forall* {P} {BP} {M} {CM} {PM} {N1} {N} {B} {X1} {X}
    {MP1} {MP} {Pi11} {Pi21} {NN1} {A1} {C1} {FM1}
    forall {FS1: efxmap`fresh MP1 N1}
    {U1: efxmap`update MP1 N1 X1 M}
    {BL: cxt`lookup B (object/ N1) (ty/ NN1 A1 C1)}
    {CML: clsmap`lookup CM C1 FM1}
    {X2P: inner-efxmap2perm P (object/ N1) FM1 PM X1 Pi11}
    {M2P: efxmap2perm P CM PM B MP1 Pi21}
    {FS2: efxmap`fresh MP N}
    {U2: efxmap`update MP N X M}
    {EQ: nat`eq? N N1 BP}
    exists {NN} {A} {C} {BL: cxt`lookup B (object/ N) (ty/ NN A C)}
    {FM} {CML: clsmap`lookup CM C FM}
    {Pi1} {X2P: inner-efxmap2perm P (object/ N) FM PM X Pi1}
    {Pi2} {M2P: efxmap2perm P CM PM B MP Pi2}
    {EQV: {f} equiv ((Pi1 f) , (Pi2 f)) ((Pi11 f) , (Pi21 f))}
    true.

- : efxmap2perm/U-inversion/L F1 U1 BL1 CML1 X2P1 M2P1 F2 U2 nat`eq?/yes
    _ _ _ BL1 _ CML1 _ X2P2 _ M2P2 ([_] equiv/reflexive)
    <- efxmap`fresh-update-cancels
      F1 U1 F2 U2 nat`eq/ efxmap`eq/ X1=X2 M1=M2
    <- efxmap2perm-respects-eq M2P1 bool`eq/ clsmap`eq/
      predmap`eq/ cxt`eq/ M1=M2 ([_] permission`eq/) M2P2
    <- inner-efxmap2perm-respects-eq X2P1 bool`eq/ object`eq/
      fldmap`eq/ predmap`eq/ X1=X2 ([_] permission`eq/) X2P2.


- : efxmap2perm/U-inversion/L F1 U1 BL1 CML1 X2P1 M2P1 F2 U2 (nat`eq?/no N<>N1)
    _ _ _ BL2 _ CML2 _ X2P2 _
    (efxmap2perm/U F4 U4 BL1 CML1 X2P1 M2P2)
    ([f] equiv/transitive (equiv/roll2) (equiv/combine equiv/reflexive (EQV2 f)))
    <- efxmap`update-commutes-converse U2 U1 N<>N1 _ U4 U3
    <- efxmap`update-preserves-fresh-converse F2 U4 F3
    <- efxmap`update-preserves-fresh-converse F1 U3 F4
    <- efxmap2perm/U-inversion M2P1 F3 U3 _ _ _ BL2 _ CML2 _ X2P2 _ M2P2 EQV2.

- : efxmap2perm/U-inversion
    (efxmap2perm/U F1 U1 BL1 CML1 X2P1 M2P1)
    F2 U2 _ _ _ BL2 _ CML2 _ X2P2 _ M2P2 EQV
    <- nat`eq?-total E?
    <- efxmap2perm/U-inversion/L
      F1 U1 BL1 CML1 X2P1 M2P1 F2 U2 E? _ _ _ BL2 _ CML2 _ X2P2 _ M2P2 EQV.

%worlds ()
(efxmap2perm/U-inversion _ _ _ _ _ _ _ _ _ _ _ _ _ _)
(efxmap2perm/U-inversion/L _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%total (A B)
(efxmap2perm/U-inversion A _ _ _ _ _ _ _ _ _ _ _ _ _)
(efxmap2perm/U-inversion/L _ _ _ _ _ B _ _ _ _ _ _ _ _ _ _ _ _ _ _).


%theorem efxmap2perm-equiv
  : forall* {P1} {P2} {CM1} {CM2} {PM1} {PM2} {B1} {B2}
    {XM1} {XM2} {Pi1} {Pi2}
    forall {XM1=>Pi1: efxmap2perm P1 CM1 PM1 B1 XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P2 CM2 PM2 B2 XM2 Pi2}
    {E: bool`eq P1 P2} {E: clsmap`eq CM1 CM2}
    {E: predmap`eq PM1 PM2} {E: cxt`eq B1 B2} {E: efxmap`eq XM1 XM2}
    exists {EQV: {f} equiv (Pi1 f) (Pi2 f)}
    true.

%worlds (fracvar) (efxmap2perm-equiv _ _ _ _ _ _ _ _).
%trustme %total {} (efxmap2perm-equiv _ _ _ _ _ _ _ _).


% ==========================
% XX-fresh-update-implies-XX
% ==========================


%% fresh-update-implies-equiv-converse

%theorem inner-efxmap`fresh-update-implies-equiv-converse
  : forall* {M} {MP} {F} {X} {P} {O} {FM} {PM} {Pi1}
    forall {FS: inner-efxmap`fresh MP F}
    {U: inner-efxmap`update MP F X M}
    {M=>Pi1: inner-efxmap2perm P O FM PM M Pi1}
    exists {T} {Pi2} {Pi3} {FM-L: fldmap`lookup FM F T}
    {PF} {T2PF: ty2perm PM T PF}
    {AP: apply-efx P X (unitperm (precise-exists O F PF)) Pi2}
    {MP=>Pi3: inner-efxmap2perm P O FM PM MP Pi3}
    {EQV: ({f} equiv (Pi1 f) ((Pi2 f) , Pi3 f))}
    true.

- : inner-efxmap`fresh-update-implies-equiv-converse
    inner-efxmap`fresh/0 inner-efxmap`update/0 %{=>}%
    (inner-efxmap2perm/U
      inner-efxmap`fresh/0 inner-efxmap`update/0
      FM-L T2P AP inner-efxmap2perm/0)
    _ _ _ FM-L _ T2P AP inner-efxmap2perm/0 ([_] equiv/reflexive).

%theorem inner-efxmap`fresh-update-implies-equiv-converse/U
  : forall* {M} {MP} {MS} {F1} {F2} {X1} {X2} {FM} {T2}
    {PM} {PF2} {P} {O} {Pi1} {Pi2} {B}
    forall {FS1: inner-efxmap`fresh MP F1}
    {U1: inner-efxmap`update MP F1 X1 M}
    {FS2: inner-efxmap`fresh MS F2}
    {U2: inner-efxmap`update MS F2 X2 M}
    {FM-L2: fldmap`lookup FM F2 T2}
    {T2PF2: ty2perm PM T2 PF2}
    {AP2: apply-efx P X2 (unitperm (precise-exists O F2 PF2)) Pi1}
    {MS=>Pi2: inner-efxmap2perm P O FM PM MS Pi2}
    {E?: nat`eq? F1 F2 B}
    exists {T1} {FM-L1: fldmap`lookup FM F1 T1}
    {PF1} {T2PF1: ty2perm PM T1 PF1}
    {Pi3} {AP: apply-efx P X1 (unitperm (precise-exists O F1 PF1)) Pi3}
    {Pi4} {MP=>Pi4: inner-efxmap2perm P O FM PM MP Pi4}
    {EQV: ({f} equiv ((Pi1 f) , Pi2 f) ((Pi3 f) , Pi4 f))}
    true.

- : inner-efxmap`fresh-update-implies-equiv-converse/U
    F!<MP MP+F=M F!<MS MS+F=M FM->F T=>PF PF+X1=>P1 MS=>P2
    (nat`eq?/yes) %{=>}% _ FM->F _ T=>PF _ PF+X2=>P1 _ MP=>P2 ([_] equiv/reflexive)
    <- inner-efxmap`fresh-update-cancels F!<MS MS+F=M F!<MP MP+F=M
      nat`eq/ inner-efxmap`eq/ X2=X1 MS=MP
    <- apply-efx-respects-eq
      PF+X1=>P1 bool`eq/ X2=X1 permission`eq/ ([_] permission`eq/) PF+X2=>P1
    <- inner-efxmap2perm-respects-eq
      MS=>P2 bool`eq/ object`eq/ fldmap`eq/ predmap`eq/ MS=MP
      ([_] permission`eq/) MP=>P2.

- : inner-efxmap`fresh-update-implies-equiv-converse/U
     F1!<MP MP+F1=M F2!<MS MS+F2=M FM->F2 T2=>PF2 PF2=>P1 MS=>P2
    (nat`eq?/no F1!=F2) %{=>}% _ FM->F1 _ T1=>PF1 _ PF1=>P3
    _ (inner-efxmap2perm/U F2!<MK MK+F2=MP FM->F2 T2=>PF2 PF2=>P1 MK=>P5)
    ([f] equiv/transitive
      (equiv/combine
        equiv/reflexive (EQV f))
      (equiv/roll2))
    <- inner-efxmap`update-commutes-converse
      MP+F1=M MS+F2=M F1!=F2 MK MK+F2=MP MK+F1=MS
    <- inner-efxmap`update-preserves-fresh-converse F1!<MP MK+F2=MP F1!<MK
    <- inner-efxmap`update-preserves-fresh-converse F2!<MS MK+F1=MS F2!<MK
    <- inner-efxmap`fresh-update-implies-equiv-converse
      F1!<MK MK+F1=MS MS=>P2 _ _ _ FM->F1 _ T1=>PF1 PF1=>P3 MK=>P5 EQV.

- : inner-efxmap`fresh-update-implies-equiv-converse F1!<MP MP+F1=M
    (inner-efxmap2perm/U F2!<MS MS+F2=M T1 T2 T3 T4) _ _ _ Q1 _ Q2 Q3 Q4 EQV
    <- nat`eq?-total EQ?
    <- inner-efxmap`fresh-update-implies-equiv-converse/U
      F1!<MP MP+F1=M F2!<MS MS+F2=M T1 T2 T3 T4 EQ? _ Q1 _ Q2 _ Q3 _ Q4 EQV.

%worlds ()
(inner-efxmap`fresh-update-implies-equiv-converse
  _ _ _ _ _ _ _ _ _ _ _ _)
(inner-efxmap`fresh-update-implies-equiv-converse/U
  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%total (T1 T2)
(inner-efxmap`fresh-update-implies-equiv-converse
  _ _ T1 _ _ _ _ _ _ _ _ _)
(inner-efxmap`fresh-update-implies-equiv-converse/U
  _ _ _ _ _ _ _ T2 _ _ _ _ _ _ _ _ _ _).


%theorem efxmap`fresh-update-implies-equiv-converse
  : forall* {CM} {PM} {B} {MP} {N} {D} {P} {M} {Pi3}
    forall {FS: efxmap`fresh MP N}
    {U: efxmap`update MP N D M}
    {M=>Pi3: efxmap2perm P CM PM B M Pi3}
    exists {Pi1} {Pi2}
    {NN} {A} {C} {B-L: cxt`lookup B (object/ N) (ty/ NN A C)}
    {FM} {CM-L: clsmap`lookup CM C FM}
    {D=>Pi1: inner-efxmap2perm P (object/ N) FM PM D Pi1}
    {MP=>Pi2: efxmap2perm P CM PM B MP Pi2}
    {EQV: ({f} equiv (Pi3 f) ((Pi1 f) , (Pi2 f)))}
    true.

- : efxmap`fresh-update-implies-equiv-converse
    efxmap`fresh/0 efxmap`update/0 %{=>}%
    (efxmap2perm/U
      efxmap`fresh/0 efxmap`update/0
      B-L CM-L DT efxmap2perm/0)
    _ _ _ _ _ B-L _ CM-L DT efxmap2perm/0 ([_] equiv/reflexive).

%theorem efxmap`fresh-update-implies-equiv-converse/U
  : forall* {M} {MP} {MS} {N1} {N2} {D1} {D2} {B}
    {NN2} {A2} {C2} {CM} {FM2} {PM} {Pi1} {P} {Pi2} {IN?}
    forall {FS1: efxmap`fresh MP N1}
    {U1: efxmap`update MP N1 D1 M}
    {FS2: efxmap`fresh MS N2}
    {U2: efxmap`update MS N2 D2 M}
    {B-L2: cxt`lookup B (object/ N2) (ty/ NN2 A2 C2)}
    {CM-L2: clsmap`lookup CM C2 FM2}
    {D2=>Pi1: inner-efxmap2perm P (object/ N2) FM2 PM D2 Pi1}
    {MS=>Pi2: efxmap2perm P CM PM B MS Pi2}
    {E?: nat`eq? N1 N2 IN?}
    exists {NN1} {A1} {C1}
    {B-L1: cxt`lookup B (object/ N1) (ty/ NN1 A1 C1)}
    {FM1} {CM-L1: clsmap`lookup CM C1 FM1}
    {Pi3} {Pi4} {D1=>Pi3: inner-efxmap2perm P (object/ N1) FM1 PM D1 Pi3}
    {MP=>Pi4: efxmap2perm P CM PM B MP Pi4}
    {EQV: ({z} equiv ((Pi1 z) , Pi2 z) ((Pi3 z) , Pi4 z))}
    true.

- : efxmap`fresh-update-implies-equiv-converse/U
    N!<MP MP+N=M N!<MS MS+N=M B-L2 CM-L2 M2P2 XM2P2 nat`eq?/yes
    %{=>}% _ _ _ B-L2 _ CM-L2 _ _ M2P1 XM2P1 ([_] equiv/reflexive)
    <- efxmap`fresh-update-cancels
      N!<MS MS+N=M N!<MP MP+N=M nat`eq/ efxmap`eq/ D2=D1 MS=MP
    <- inner-efxmap2perm-respects-eq M2P2
      bool`eq/ object`eq/ fldmap`eq/ predmap`eq/
      D2=D1 ([_] permission`eq/) M2P1
    <- efxmap2perm-respects-eq XM2P2
      bool`eq/ clsmap`eq/ predmap`eq/
      cxt`eq/ MS=MP ([_] permission`eq/) XM2P1.

- : efxmap`fresh-update-implies-equiv-converse/U
    N1!<MP MP+N1=M N2!<MS MS+N2=M B->N2 CM->C2 D2=>P1 MS=>P2
    (nat`eq?/no N1!=N2) %{=>}% _ _ _ B->N1 _ CM->C1 _ _ D1=>P3
    (efxmap2perm/U N2!<MK MK+N2=MP B->N2 CM->C2 D2=>P1 MK=>P5)
    ([f] equiv/transitive
      (equiv/combine
        equiv/reflexive (EQV f))
      (equiv/roll2))
    <- efxmap`update-commutes-converse
      MP+N1=M MS+N2=M N1!=N2 MK MK+N2=MP MK+N1=MS
    <- efxmap`update-preserves-fresh-converse N1!<MP MK+N2=MP N1!<MK
    <- efxmap`update-preserves-fresh-converse N2!<MS MK+N1=MS N2!<MK
    <- efxmap`fresh-update-implies-equiv-converse
      N1!<MK MK+N1=MS MS=>P2 _ _ _ _ _ B->N1 _ CM->C1 D1=>P3 MK=>P5 EQV.

- : efxmap`fresh-update-implies-equiv-converse F1!<MP MP+F1=M
    (efxmap2perm/U F2!<MS MS+F2=M T1 T2 T3 T4) _ _ _ _ _ Q1 _ Q2 Q3 Q4 EQV
    <- nat`eq?-total EQ?
    <- efxmap`fresh-update-implies-equiv-converse/U
      F1!<MP MP+F1=M F2!<MS MS+F2=M T1 T2 T3 T4 EQ? _ _ _ Q1 _ Q2 _ _ Q3 Q4 EQV.

%worlds (fracvar)
(efxmap`fresh-update-implies-equiv-converse
  _ _ _ _  _ _ _ _ _ _ _ _ _ _)
(efxmap`fresh-update-implies-equiv-converse/U
  _ _ _ _ _ _ _ _ _ _ _  _ _ _ _ _ _ _ _ _).
%total (T1 T2)
(efxmap`fresh-update-implies-equiv-converse
  _ _ T1 _ _ _ _ _ _ _ _  _ _ _)
(efxmap`fresh-update-implies-equiv-converse/U
  _ _ _ _ _ _ _ T2 _ _ _ _ _ _ _ _ _  _ _ _).



% ============================================
% theorems about leq - basically:
%
% if XM1 is less or equal than XM2, and
% XM1 ==> Pi1, XM2 ==> Pi2.
% then exists Pi3, such that Pi2 <==> Pi1 , Pi3
%
% =============================================

%theorem can-split-permission
  : forall* {Q1} {Pi}
    forall {Q1<1: rat`lst Q1 one}
    exists {Q2} {ADD: rat`add Q1 Q2 one}
    {EQV: equiv Pi ((scale (fraction/ Q1) Pi) , (scale (fraction/ Q2) Pi))}
    true.

- : can-split-permission (Q1<1:rat`lst Q1 one) Q2 Q1+Q2=1
    (equiv/transitive
      (equiv/symmetric equiv/one)
      (equiv/symmetric (equiv/add Q1+Q2=1)))
    <- rat`grt-implies-add Q1<1 Q2 Q2+Q1=1
    <- rat`add-commutative Q2+Q1=1 Q1+Q2=1.

%worlds () (can-split-permission _ _ _ _).
%total {} (can-split-permission _ _ _ _).


%% This turns out to be unprovable: we have no information
%% about the fraction variable - it could even be greater than 1!

%theorem apply-efx-preserves-permission-leq
  : forall* {X1} {X2} {Pi} {Pi1} {Pi2}
    forall {X1<=X2: efx`leq X1 X2}
    {AP-X1: apply-efx true X1 Pi Pi1}
    {AP-X2: apply-efx true X2 Pi Pi2}
    exists {Pi3}
    {Pi2=>Pi1+Pi3: {f} equiv (Pi2 f) (Pi1 f , Pi3 f)}
    true.

%worlds (fracvar) (apply-efx-preserves-permission-leq _ _ _ _ _).
%trustme %total {} (apply-efx-preserves-permission-leq _ _ _ _ _).


%theorem inner-efxmap`leq-implies-permission-leq
  : forall* {CM} {PM} {FM} {C} {MP} {P} {O} {M} {P1} {MPS} {FS}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {CM-L: clsmap`lookup CM C FM}
    {DM-FM=FS: fldmap`domain FM FS}
    {DM-M=MS: inner-efxmap`domain MP MPS}
    {LE: set`leq MPS FS}
    {M=>P1: inner-efxmap2perm P O FM PM M P1}
    {M<=MP: inner-efxmap`leq M MP}
    exists {P2} {P3} {MP=>P2: inner-efxmap2perm P O FM PM MP P2}
    {EQV: ({f} equiv (P2 f) ((P1 f) , (P3 f)))}
    true.

%theorem inner-efxmap`leq-implies-permission-leq/L
  : forall* {CM} {PM} {FM} {C} {M1} {M2} {Pi1} {P} {O} {MS2} {FS}
    forall {N} {SZ: inner-efxmap`size M2 N}
    {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {CM-L: clsmap`lookup CM C FM}
    {DM-FM=FS: fldmap`domain FM FS}
    {DM-M2=MS2: inner-efxmap`domain M2 MS2}
    {LE: set`leq MS2 FS}
    {M=>P1: inner-efxmap2perm P O FM PM M1 Pi1}
    {M1<=M2: inner-efxmap`leq M1 M2}
    exists {Pi2} {Pi3} {M2=>Pi2: inner-efxmap2perm P O FM PM M2 Pi2}
    {EQV: ({f} equiv (Pi2 f) ((Pi1 f) , (Pi3 f)))}
    true.

%worlds (fracvar | objvar)
(inner-efxmap`leq-implies-permission-leq/L _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%trustme %total (N)
(inner-efxmap`leq-implies-permission-leq/L N _ _ _ _ _ _ _ _ _ _ _ _ _).

- : inner-efxmap`leq-implies-permission-leq A B C D E G K J _ _ H I
    <- inner-efxmap`size-total SZ
    <- inner-efxmap`leq-implies-permission-leq/L _ SZ A B C D E G K J _ _ H I.

%worlds (fracvar)
(inner-efxmap`leq-implies-permission-leq _ _ _ _ _ _ _ _ _ _ _ _).
%total {} (inner-efxmap`leq-implies-permission-leq _ _ _ _ _ _ _ _ _ _ _ _).


%theorem efxmap`leq-implies-permission-leq/L
  : forall* {XM1} {XM2} {CM} {PM} {B} {P} {Pi1}
    forall {N} {SZ: efxmap`size XM2 N}
    {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {B-XM2: clsmap-cxt-efxmap CM B XM2}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM1<=XM2: efxmap`leq XM1 XM2}
    exists {Pi2} {Pi3} {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {EQV: ({f} equiv (Pi2 f) ((Pi1 f) , Pi3 f))}
    true.

- : efxmap`leq-implies-permission-leq/L _ _
    CM-CM CM2PM B-XM2 efxmap2perm/0 efxmap`leq/0 _ _ XM=>Pi
    ([_] equiv/transitive
      (equiv/symmetric equiv/identity)
      (equiv/commute))
    <- clsmap-cxt-efxmap-implies-efxmap2perm CM-CM CM2PM B-XM2 _ XM=>Pi.

%theorem efxmap`leq-implies-permission-leq/U
  : forall* {CM} {PM} {FM} {XM1} {XM2} {XM2P} {FS}
    {P} {N} {M} {B} {NN} {A} {C} {Pi1} {IN?} {MS}
    forall {K} {SZ: efxmap`size XM2 K}
    {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {F: efxmap`fresh XM2P N}
    {U: efxmap`update XM2P N M XM2}
    {B-L: cxt`lookup B (object/ N) (ty/ NN A C)}
    {CM-L: clsmap`lookup CM C FM}
    {DM-FM=FS: fldmap`domain FM FS}
    {DM-M=MS: inner-efxmap`domain M MS}
    {LE: set`leq MS FS}
    {B-XM2P: clsmap-cxt-efxmap CM B XM2P}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM1<=XM2: efxmap`leq XM1 XM2}
    {IN?: efxmap`domain? XM1 N IN?}
    exists {Pi2} {Pi3} {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {EQV: ({z} equiv (Pi2 z) ((Pi1 z) , (Pi3 z)))}
    true.

%worlds (fracvar)
(efxmap`leq-implies-permission-leq/L _ _ _ _ _ _ _ _ _ _ _)
(efxmap`leq-implies-permission-leq/U _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%trustme %total (N1 N2)
(efxmap`leq-implies-permission-leq/U N2 _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _)
(efxmap`leq-implies-permission-leq/L N1 _ _ _ _ _ _ _ _ _ _).


% the major theorem

%theorem efxmap`leq-implies-permission-leq
  : forall* {CM} {PM} {B} {XM1} {XM2} {P} {Pi1}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {B-XM2: clsmap-cxt-efxmap CM B XM2}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM1<=XM2: efxmap`leq XM1 XM2}
    exists {Pi2} {Pi3} {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {EQV: ({z} equiv (Pi2 z) ((Pi1 z) , (Pi3 z)))}
    true.

- : efxmap`leq-implies-permission-leq CM-CM CM2PM
    B-XM2 XM1=>Pi1 XM1<=XM2 Pi2 Pi3 XM2=>Pi2 EQV
    <- efxmap`size-total SZ
    <- leq-preserves-clsmap-cxt-efxmap B-XM2 XM1<=XM2 B-XM1
    <- efxmap`leq-implies-permission-leq/L _ SZ CM-CM CM2PM
      B-XM2 XM1=>Pi1 XM1<=XM2 _ _ XM2=>Pi2 EQV.

%worlds (fracvar) (efxmap`leq-implies-permission-leq _ _ _ _ _ _ _ _ _).
%total {} (efxmap`leq-implies-permission-leq _ _ _ _ _ _ _ _ _).


% conversed version of the major theorem

%theorem efxmap`leq-implies-permission-leq-converse
  : forall* {B} {P} {Pi2} {CM} {PM} {XM1} {XM2}
    forall {CM-CM: clsmap-clsmap CM CM}
    {CM2PM: clsmap2predmap CM PM}
    {B-XM2: clsmap-cxt-efxmap CM B XM2}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {XM1<=XM2: efxmap`leq XM1 XM2}
    exists {Pi1} {Pi3} {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {EQV: ({f} equiv (Pi2 f) ((Pi1 f) , (Pi3 f)))}
    true.

- : efxmap`leq-implies-permission-leq-converse
    CM-CM CM2PM B-XM2 XM2=>Pi2 XM1<=XM2 Pi1 _ XM1=>Pi1
    ([f] equiv/transitive (equiv/symmetric (EQV2 f)) (EQV1 f))
    <- leq-preserves-clsmap-cxt-efxmap B-XM2 XM1<=XM2 B-XM1
    <- clsmap-cxt-efxmap-implies-efxmap2perm CM-CM CM2PM B-XM1 Pi1 XM1=>Pi1
    <- efxmap`leq-implies-permission-leq
      CM-CM CM2PM B-XM2 XM1=>Pi1 XM1<=XM2 Pi2 Pi3 XM2=>Pi2' EQV1
    <- efxmap2perm-equiv XM2=>Pi2' XM2=>Pi2
       bool`eq/ clsmap`eq/ predmap`eq/ cxt`eq/ efxmap`eq/ EQV2.

%worlds (fracvar)
(efxmap`leq-implies-permission-leq-converse _ _ _ _ _ _ _ _ _).
%total {} (efxmap`leq-implies-permission-leq-converse _ _ _ _ _ _ _ _ _).


%theorem maybe-efx-leq-implies-permission-leq
  : forall* {MX1} {MX2} {P1} {P2}
    forall {LE: maybe-efx`leq MX1 MX2}
    {MX1=>P1: maybe-efx2perm MX1 P1}
    {MX2=>P2: maybe-efx2perm MX2 P2}
    exists {P3}
    {EQV: {f} equiv (P2 f) (P1 f , P3 f)}
    true.

- : maybe-efx-leq-implies-permission-leq
    maybe-efx`leq/1 maybe-efx2perm/nothing maybe-efx2perm/nothing
    _ ([_] equiv/symmetric equiv/identity).

- : maybe-efx-leq-implies-permission-leq
    maybe-efx`leq/1 maybe-efx2perm/nothing
    (maybe-efx2perm/just AP-X) _
    ([_] (equiv/transitive
           (equiv/symmetric equiv/identity)
           (equiv/commute))).

- : maybe-efx-leq-implies-permission-leq
    (maybe-efx`leq/2 X1<=X2) (maybe-efx2perm/just AP-X1)
    (maybe-efx2perm/just AP-X2) _ EQV
    <- apply-efx-preserves-permission-leq X1<=X2 AP-X1 AP-X2 _ EQV.

%worlds () (maybe-efx-leq-implies-permission-leq _ _ _ _ _).
%total (L) (maybe-efx-leq-implies-permission-leq L _ _ _ _).


% =======================================
% deep-disjoint-join-implies-equiv:
%
% If we have two effect maps XM1 and XM2,
%   and if the following condition holds
%
% 1. XM1^XM2
% 2. XM1+XM2 = XM
% 3. XM1 ==> Pi1
% 4. XM2 ==> Pi2
% 5. XM  ==> Pi
%
% then we can derive:
%
% Pi1 , Pi2 <==> Pi
%
% =======================================


%theorem inner-efxmap`disjoint-join-implies-equiv
  : forall* {M1} {M2} {M} {P} {O} {FM} {PM} {Pi}
    forall {DJ: inner-efxmap`disjoint M1 M2}
    {J: inner-efxmap`join M1 M2 M}
    {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    exists {Pi1} {Pi2}
    {M1=>Pi1: inner-efxmap2perm P O FM PM M1 Pi1}
    {M2=>Pi2: inner-efxmap2perm P O FM PM M2 Pi2}
    {EQV: ({z} equiv (Pi z) ((Pi1 z) , (Pi2 z)))}
    true.

- : inner-efxmap`disjoint-join-implies-equiv
    _ _ inner-efxmap2perm/0 _ _ inner-efxmap2perm/0 inner-efxmap2perm/0
    ([_] equiv/symmetric equiv/identity).

%theorem inner-efxmap`disjoint-join-implies-equiv/L
  : forall* {M1} {M2} {M} {MP} {F} {X} {T} {PF}
    {P} {O} {FM} {PM} {Pix} {Piy} {B}
    forall {DJ: inner-efxmap`disjoint M1 M2}
    {J: inner-efxmap`join M1 M2 M}
    {FS: inner-efxmap`fresh MP F}
    {U: inner-efxmap`update MP F X M}
    {FML: fldmap`lookup FM F T}
    {T=>PF: ty2perm PM T PF}
    {AP: apply-efx P X (unitperm (precise-exists O F PF)) Pix}
    {M=>Pi: inner-efxmap2perm P O FM PM MP Piy}
    {IN: inner-efxmap`domain? M1 F B}
    exists {Pi1} {Pi2}
    {M1=>Pi1: inner-efxmap2perm P O FM PM M1 Pi1}
    {M2=>Pi2: inner-efxmap2perm P O FM PM M2 Pi2}
    {EQV: ({z} equiv ((Pix z) , (Piy z)) ((Pi1 z) , (Pi2 z)))}
    true.

- : inner-efxmap`disjoint-join-implies-equiv/L
    M1^M2 (M1+M2=M:inner-efxmap`join M1 M2 M)
    F!<MP (MP+F+X=M:inner-efxmap`update MP F X M)
    FM->F=T T=>PF PF=>Pix MP=>Piy
    (inner-efxmap`domain?/in M1->F) _ _
    (inner-efxmap2perm/U F!<M1P M1P+F+X=M1 FM->F=T T=>PF PF=>Pix M1P=>Pi1x)
    M2=>Pi2 ([z] equiv/transitive
              (equiv/combine equiv/reflexive (Piy<=>Pi1x+Pi2 z))
              (equiv/associate))
    <- inner-efxmap`disjoint-lookup-implies-fresh M1->F M1^M2 F!<M2
    <- inner-efxmap`update-implies-lookup MP+F+X=M M->F=X
    <- inner-efxmap`fresh-join-right-preserves-lookup-converse
      M->F=X F!<M2 M1+M2=M M1->F=X
    <- inner-efxmap`lookup-implies-fresh-update M1->F=X M1P F!<M1P M1P+F+X=M1
    <- inner-efxmap`fresh-update-implies-unit-join F!<M1P M1P+F+X=M1 M1P+X=M1
    <- inner-efxmap`fresh-implies-unit-disjoint F!<M1P M1P^X
    <- inner-efxmap`disjoint-join-commutative M1P^X M1P+X=M1 X+M1P=M1
    <- inner-efxmap`join-associative X+M1P=M1 M1+M2=M MPx M1P+M2=MPx X+MPx=M
    <- inner-efxmap`join-preserves-fresh* F!<M1P F!<M2 M1P+M2=MPx F!<MPx
    <- inner-efxmap`fresh-implies-unit-disjoint F!<MPx MPx^X
    <- inner-efxmap`disjoint-symmetric MPx^X X^MPx
    <- inner-efxmap`disjoint-join-commutative X^MPx X+MPx=M MPx+X=M
    <- inner-efxmap`fresh-unit-join-implies-update F!<MPx MPx+X=M MPx+F+X=M
    <- inner-efxmap`fresh-update-cancels F!<MPx MPx+F+X=M F!<MP MP+F+X=M nat`eq/
      inner-efxmap`eq/ _ MPx=MP
    <- inner-efxmap`join-respects-eq M1P+M2=MPx inner-efxmap`eq/
      inner-efxmap`eq/ MPx=MP M1P+M2=MP
    <- inner-efxmap`fresh-update-implies-leq F!<M1P M1P+F+X=M1 M1P<=M1
    <- inner-efxmap`disjoint-respects-geq* M1^M2 M1P<=M1 M1P^M2
    <- inner-efxmap`disjoint-join-implies-equiv
      M1P^M2 M1P+M2=MP MP=>Piy Pi1x Pi2 M1P=>Pi1x M2=>Pi2 Piy<=>Pi1x+Pi2.

- : inner-efxmap`disjoint-join-implies-equiv/L
    M1^M2 (M1+M2=M:inner-efxmap`join M1 M2 M)
    F!<MP (MP+F+X=M:inner-efxmap`update MP F X M)
    FM->F=T T=>PF PF=>Pix MP=>Piy
    (inner-efxmap`domain?/out F!<M1) _ _
    M1=>Pi1 (inner-efxmap2perm/U F!<M2P M2P+F+X=M2 FM->F=T T=>PF PF=>Pix M2P=>Pi2x)
    ([z] equiv/transitive (equiv/combine equiv/reflexive (Piy<=>Pi1+Pi2x z))
      (equiv/roll2))
    <- inner-efxmap`update-implies-lookup MP+F+X=M M->F=X
    <- inner-efxmap`fresh-join-left-preserves-lookup-converse
      F!<M1 M->F=X M1+M2=M M2->F=X
    <- inner-efxmap`lookup-implies-fresh-update M2->F=X M2P F!<M2P M2P+F+X=M2
    <- inner-efxmap`fresh-update-implies-unit-join F!<M2P M2P+F+X=M2 M2P+X=M2
    <- inner-efxmap`join-associative-converse M2P+X=M2 M1+M2=M MPx M1+M2P=MPx MPx+X=M
    <- inner-efxmap`join-preserves-fresh* F!<M1 F!<M2P M1+M2P=MPx F!<MPx
    <- inner-efxmap`fresh-unit-join-implies-update F!<MPx MPx+X=M MPx+F+X=M
    <- inner-efxmap`fresh-update-cancels F!<MPx MPx+F+X=M F!<MP MP+F+X=M nat`eq/
      inner-efxmap`eq/ _ MPx=MP
    <- inner-efxmap`join-respects-eq M1+M2P=MPx inner-efxmap`eq/
      inner-efxmap`eq/ MPx=MP M1+M2P=MP
    <- inner-efxmap`fresh-update-implies-leq F!<M2P M2P+F+X=M2 M2P<=M2
    <- inner-efxmap`disjoint-symmetric M1^M2 M2^M1
    <- inner-efxmap`disjoint-respects-geq* M2^M1 M2P<=M2 M2P^M1
    <- inner-efxmap`disjoint-symmetric M2P^M1 M1^M2P
    <- inner-efxmap`disjoint-join-implies-equiv
      M1^M2P M1+M2P=MP MP=>Piy Pi1 Pi2x M1=>Pi1 M2P=>Pi2x Piy<=>Pi1+Pi2x.

- : inner-efxmap`disjoint-join-implies-equiv
    X J (inner-efxmap2perm/U F!<MP MP+F+X=M FM->F=T T=>PF P+X=>Pi1
          MP=>Pi2)
    _ _ M1=>Pi1 M2=>Pi2 EQV
    <- inner-efxmap`domain?-total IN?
    <- inner-efxmap`disjoint-join-implies-equiv/L
      X J F!<MP MP+F+X=M FM->F=T T=>PF P+X=>Pi1 MP=>Pi2 IN? _ _ M1=>Pi1 M2=>Pi2 EQV.

%worlds ()
(inner-efxmap`disjoint-join-implies-equiv _ _ _ _ _ _ _ _)
(inner-efxmap`disjoint-join-implies-equiv/L _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%total (T1 T2) (inner-efxmap`disjoint-join-implies-equiv _ _ T1 _ _ _ _ _)
(inner-efxmap`disjoint-join-implies-equiv/L _ _ _ _ _ _ _ T2 _ _ _ _ _ _).


%theorem inner-efxmap`disjoint-join-implies-equiv-converse
  : forall* {M1} {M2} {M} {P} {O} {FM} {PM} {Pi1} {Pi2}
    forall {DJ: inner-efxmap`disjoint M1 M2}
    {J: inner-efxmap`join M1 M2 M}
    {M1=>Pi1: inner-efxmap2perm P O FM PM M1 Pi1}
    {M2=>Pi2: inner-efxmap2perm P O FM PM M2 Pi2}
    exists {Pi} {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    {EQV: ({z} equiv ((Pi1 z) , (Pi2 z)) (Pi z))}
    true.

%theorem inner-efxmap`disjoint-join-implies-equiv-converse/L
  : forall* {M1} {M2} {M} {P} {O} {FM} {PM} {Pi1} {Pi2}
    forall {N1} {SZ1: inner-efxmap`size M1 N1}
    {N2} {SZ2: inner-efxmap`size M2 N2}
    {DJ: inner-efxmap`disjoint M1 M2}
    {J: inner-efxmap`join M1 M2 M}
    {M1=>Pi1: inner-efxmap2perm P O FM PM M1 Pi1}
    {M2=>Pi2: inner-efxmap2perm P O FM PM M2 Pi2}
    exists {Pi} {M=>Pi: inner-efxmap2perm P O FM PM M Pi}
    {EQV: ({z} equiv ((Pi1 z) , (Pi2 z)) (Pi z))}
    true.

- : inner-efxmap`disjoint-join-implies-equiv-converse
    M1^M2 M1UM2=M M1=>Pi1 M2=>Pi2 Pi M=>Pi EQV
    <- inner-efxmap`size-total SZ-M1
    <- inner-efxmap`size-total SZ-M2
    <- inner-efxmap`disjoint-join-implies-equiv-converse/L
      _ SZ-M1 _ SZ-M2 M1^M2 M1UM2=M M1=>Pi1 M2=>Pi2 Pi M=>Pi EQV.

%worlds (fracvar)
(inner-efxmap`disjoint-join-implies-equiv-converse/L _ _ _ _ _ _ _ _ _ _ _).
%trustme %total {N1 N2}
(inner-efxmap`disjoint-join-implies-equiv-converse/L N1 _ N2 _ _ _ _ _ _ _ _).

%worlds (fracvar)
(inner-efxmap`disjoint-join-implies-equiv-converse _ _ _ _ _ _ _).
%total {}
(inner-efxmap`disjoint-join-implies-equiv-converse _ _ _ _ _ _ _).



%theorem efxmap`deep-disjoint-join-implies-equiv
  : forall* {M1} {M2} {M} {P} {CM} {PM} {B} {Pi}
    forall {DJ: efxmap`deep-disjoint M1 M2}
    {J: efxmap`join M1 M2 M}
    {M=>Pi: efxmap2perm P CM PM B M Pi}
    exists {Pi1} {Pi2}
    {M1=>Pi1: efxmap2perm P CM PM B M1 Pi1}
    {M2=>Pi2: efxmap2perm P CM PM B M2 Pi2}
    {EQV: ({z} equiv (Pi z) ((Pi1 z) , (Pi2 z)))}
    true.

- : efxmap`deep-disjoint-join-implies-equiv
    _ _ efxmap2perm/0 _ _ efxmap2perm/0 efxmap2perm/0
    ([_] (equiv/symmetric equiv/identity)).


%theorem efxmap`deep-disjoint-join-implies-equiv/L
  : forall* {M1} {M2} {M} {MP} {N} {D} {NN} {A} {C}
    {P} {CM} {PM} {B} {FM} {Pix} {Piy} {B1} {B2}
    forall {DJ: efxmap`deep-disjoint M1 M2}
    {J: efxmap`join M1 M2 M}
    {FS: efxmap`fresh MP N}
    {U: efxmap`update MP N D M}
    {BL: cxt`lookup B (object/ N) (ty/ NN A C)}
    {CML: clsmap`lookup CM C FM}
    {D=>Pi: inner-efxmap2perm P (object/ N) FM PM D Pix}
    {MP=>Pi: efxmap2perm P CM PM B MP Piy}
    {IN1: efxmap`domain? M1 N B1}
    {IN2: efxmap`domain? M2 N B2}
    exists {Pi1} {Pi2}
    {M1=>Pi1: efxmap2perm P CM PM B M1 Pi1}
    {M2=>Pi2: efxmap2perm P CM PM B M2 Pi2}
    {EQV: ({z} equiv ((Pix z) , (Piy z)) ((Pi1 z) , (Pi2 z)))}
    true.

- : efxmap`deep-disjoint-join-implies-equiv/L
    M1^M2 M1+M2=M N!<MP MP+N+D=M BL CML D=>Pix MP=>Piy
    (efxmap`domain?/in M1->N=D1) (efxmap`domain?/in M2->N=D2)
    _ _ (efxmap2perm/U N!<M1P M1P+N+D1=M1 BL CML D1=>Pix1 M1P=>Piy1)
    (efxmap2perm/U N!<M2P M2P+N+D2=M2 BL CML D2=>Pix2 M2P=>Piy2)
    ([z] (equiv/transitive4
           (equiv/combine (EQV1 z) (EQV2 z))
           (equiv/symmetric equiv/associate)
           (equiv/combine equiv/reflexive equiv/roll2)
           (equiv/associate)))
    <- efxmap`deep-disjoint-lookup-implies-disjoint M1^M2 M1->N=D1 M2->N=D2 D1^D2
    <- efxmap`update-implies-lookup MP+N+D=M M->N=D
    <- efxmap`join-commutes-lookup M1+M2=M M1->N=D1 M2->N=D2 M->N=D D1+D2=D
    <- efxmap`lookup-implies-fresh-update M1->N=D1 M1P N!<M1P M1P+N+D1=M1
    <- efxmap`lookup-implies-fresh-update M2->N=D2 M2P N!<M2P M2P+N+D2=M2
    <- inner-efxmap`disjoint-join-implies-equiv
      D1^D2 D1+D2=D D=>Pix Pix1 Pix2 D1=>Pix1 D2=>Pix2 EQV1
    <- efxmap`fresh-update-preserves-deep-disjoint-join-converse
      N!<M1P M1P+N+D1=M1 N!<M2P M2P+N+D2=M2
      N!<MP MP+N+D=M M1+M2=M M1^M2 D1+D2=D M1P^M2P M1P+M2P=MP
    <- efxmap`deep-disjoint-join-implies-equiv
      M1P^M2P M1P+M2P=MP MP=>Piy Piy1 Piy2 M1P=>Piy1 M2P=>Piy2 EQV2.

- : efxmap`deep-disjoint-join-implies-equiv/L
    M1^M2 M1+M2=M N!<MP MP+N+D=M BL CML D=>Pix MP=>Piy
    (efxmap`domain?/in _) (efxmap`domain?/out N!<M2)
    _ _ (efxmap2perm/U N!<M1P M1P+N+D=M1 BL CML D=>Pix M1P=>Piy1)
    M2=>Piy2
    ([z] (equiv/transitive (equiv/combine equiv/reflexive (EQV2 z)) (equiv/associate)))
    <- efxmap`update-implies-lookup MP+N+D=M M->N=D
    <- efxmap`fresh-join-right-preserves-lookup-converse M->N=D N!<M2 M1+M2=M M1->N=D
    <- efxmap`lookup-implies-fresh-update M1->N=D M1P N!<M1P M1P+N+D=M1
    <- efxmap`fresh-update1-preserves-deep-disjoint-join-converse
      N!<M1P M1P+N+D=M1 N!<M2 N!<MP MP+N+D=M M1+M2=M M1^M2 M1P^M2 M1P+M2=MP
    <- efxmap`deep-disjoint-join-implies-equiv
      M1P^M2 M1P+M2=MP MP=>Piy Piy1 Piy2 M1P=>Piy1 M2=>Piy2 EQV2.

- : efxmap`deep-disjoint-join-implies-equiv/L
    M1^M2 (M1+M2=M:efxmap`join M1 M2 M)
    N!<MP (MP+N+D=M:efxmap`update MP N D M) BL CML D=>Pix MP=>Piy
    (efxmap`domain?/out N!<M1) (efxmap`domain?/in _) _ _ M1=>Piy1
    (efxmap2perm/U N!<M2P (M2P+N+D=M2:efxmap`update M2P N D M2) BL CML D=>Pix M2P=>Piy2)
    ([z] equiv/transitive (equiv/combine equiv/reflexive (EQV2 z)) (equiv/roll2))
    <- efxmap`update-implies-lookup MP+N+D=M M->N=D
    <- efxmap`fresh-join-left-preserves-lookup-converse N!<M1 M->N=D M1+M2=M M2->N=D
    <- efxmap`lookup-implies-fresh-update M2->N=D M2P N!<M2P M2P+N+D=M2
    <- efxmap`fresh-update2-preserves-deep-disjoint-join-converse
      N!<M1 N!<M2P M2P+N+D=M2 N!<MP MP+N+D=M M1+M2=M M1^M2 M1^M2P M1+M2P=MP
    <- efxmap`deep-disjoint-join-implies-equiv
      M1^M2P M1+M2P=MP MP=>Piy Piy1 Piy2 M1=>Piy1 M2P=>Piy2 EQV2.

%theorem efxmap`deep-disjoint-join-implies-equiv/LF
  : forall* {P} {CM} {PM} {B} {M1} {M2} {Pix} {Piy}
    forall {F: void}
    exists {Pi1} {Pi2}
    {M1=>Pi1: efxmap2perm P CM PM B M1 Pi1}
    {M2=>Pi2: efxmap2perm P CM PM B M2 Pi2}
    {EQV: ({z} equiv ((Pix z) , (Piy z)) ((Pi1 z) , (Pi2 z)))}
    true.

%worlds () (efxmap`deep-disjoint-join-implies-equiv/LF _ _ _ _ _ _).
%total {} (efxmap`deep-disjoint-join-implies-equiv/LF _ _ _ _ _ _).


- : efxmap`deep-disjoint-join-implies-equiv/L
    _ M1+M2=M _ MP+N+D=M _ _ _ _
    (efxmap`domain?/out N!<M1) (efxmap`domain?/out N!<M2)
    _ _ T1 T2 EQV
    <- efxmap`join-preserves-fresh* N!<M1 N!<M2 M1+M2=M N!<M
    <- efxmap`update-implies-lookup MP+N+D=M M->N=D
    <- efxmap`fresh-lookup-not-equal N!<M M->N=D N<>N
    <- nat`ne-anti-reflexive N<>N F
    <- efxmap`deep-disjoint-join-implies-equiv/LF F _ _ T1 T2 EQV.

- : efxmap`deep-disjoint-join-implies-equiv
    X J (efxmap2perm/U FS U BL CML DT T)
    _ _ T1 T2 EQV
    <- efxmap`domain?-total D1
    <- efxmap`domain?-total D2
    <- efxmap`deep-disjoint-join-implies-equiv/L
      X J FS U BL CML DT T D1 D2 _ _ T1 T2 EQV.

%worlds ()
(efxmap`deep-disjoint-join-implies-equiv _ _ _ _ _ _ _ _)
(efxmap`deep-disjoint-join-implies-equiv/L _ _ _ _ _ _ _ _ _ _ _ _ _ _ _).
%total (T1 T2)
(efxmap`deep-disjoint-join-implies-equiv _ _ T1 _ _ _ _ _)
(efxmap`deep-disjoint-join-implies-equiv/L _ _ _ _ _ _ _ T2 _ _ _ _ _ _ _).


%theorem efxmap`deep-disjoint-join-implies-equiv-converse
  : forall* {XM1} {XM2} {XM} {P} {CM} {PM} {B} {Pi1} {Pi2}
    forall {DJ: efxmap`deep-disjoint XM1 XM2}
    {J: efxmap`join XM1 XM2 XM}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    exists {Pi} {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    {EQV: ({z} equiv ((Pi1 z) , (Pi2 z)) (Pi z))}
    true.

%theorem efxmap`deep-disjoint-join-implies-equiv-converse/L
  : forall* {XM1} {XM2} {XM} {P} {CM} {PM} {B} {Pi1} {Pi2}
    forall {N1} {SZ1: efxmap`size XM1 N1}
    {N2} {SZ2: efxmap`size XM2 N2}
    {DJ: efxmap`deep-disjoint XM1 XM2}
    {J: efxmap`join XM1 XM2 XM}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    exists {Pi} {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    {EQV: ({z} equiv ((Pi1 z) , (Pi2 z)) (Pi z))}
    true.

- : efxmap`deep-disjoint-join-implies-equiv-converse
    XM1^XM2 XM1UXM2=XM XM1=>Pi1 XM2=>Pi2 Pi XM=>Pi EQV
    <- efxmap`size-total SZ1
    <- efxmap`size-total SZ2
    <- efxmap`deep-disjoint-join-implies-equiv-converse/L
      _ SZ1 _ SZ2 XM1^XM2 XM1UXM2=XM XM1=>Pi1 XM2=>Pi2 Pi XM=>Pi EQV.

%worlds (fracvar)
(efxmap`deep-disjoint-join-implies-equiv-converse/L _ _ _ _ _ _ _ _ _ _ _).
%trustme %total {N1 N2}
(efxmap`deep-disjoint-join-implies-equiv-converse/L N1 _ N2 _ _ _ _ _ _ _ _).

%worlds (fracvar) (efxmap`deep-disjoint-join-implies-equiv-converse _ _ _ _ _ _ _).
%total {} (efxmap`deep-disjoint-join-implies-equiv-converse _ _ _ _ _ _ _).


%theorem efxmap`deep-disjoint-join-implies-equiv*
  : forall* {XM1} {XM2} {XM} {P} {CM} {PM} {B} {Pi1} {Pi2} {Pi}
    forall {DJ: efxmap`deep-disjoint XM1 XM2}
    {J: efxmap`join XM1 XM2 XM}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    exists {EQV: ({z} equiv ((Pi1 z) , (Pi2 z)) (Pi z))}
    true.

- : efxmap`deep-disjoint-join-implies-equiv*
    XM1^XM2 XM1UXM2=XM XM1=>Pi1 XM2=>Pi2 XM=>Pi
    ([f] equiv/transitive (EQV1 f) (EQV2 f))
    <- efxmap`deep-disjoint-join-implies-equiv-converse
    XM1^XM2 XM1UXM2=XM XM1=>Pi1 XM2=>Pi2 _ XM=>Pi' EQV1
    <- efxmap2perm-equiv XM=>Pi' XM=>Pi
      bool`eq/ clsmap`eq/ predmap`eq/ cxt`eq/ efxmap`eq/ EQV2.

%worlds (fracvar) (efxmap`deep-disjoint-join-implies-equiv* _ _ _ _ _ _).
%total {} (efxmap`deep-disjoint-join-implies-equiv* _ _ _ _ _ _).


%theorem select-tgtmap-implies-permission-combine
  : forall* {XM1} {XM2} {XM} {GM2} {P} {CM} {PM} {B} {Pi}
    forall {R: select-tgtmap XM GM2 XM1 XM2}
    {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    exists {Pi1} {Pi2} {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    {EQV: ({f} equiv (Pi f) ((Pi1 f) , (Pi2 f)))}
    true.

- : select-tgtmap-implies-permission-combine
    XM/GM2=XM1+XM2 XM=>Pi _ _ XM1=>Pi1 XM2=>Pi2
    ([f] (Pi1+Pi2<=>Pi f))
    <- select-tgtmap-implies-deep-disjoint-join XM/GM2=XM1+XM2 XM1^XM2 XM1+XM2=XM
    <- efxmap`deep-disjoint-join-implies-equiv
      XM1^XM2 XM1+XM2=XM XM=>Pi _ _ XM1=>Pi1 XM2=>Pi2 Pi1+Pi2<=>Pi.

%worlds () (select-tgtmap-implies-permission-combine _ _ _ _ _ _ _).
%total {} (select-tgtmap-implies-permission-combine _ _ _ _ _ _ _).


%theorem select-tgtmap-implies-permission-combine-converse
  :forall* {XM1} {XM2} {XM} {GM2} {P} {CM} {PM} {B} {Pi1} {Pi2}
    forall {R: select-tgtmap XM GM2 XM1 XM2}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    exists {Pi} {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    {EQV: {f} equiv ((Pi1 f) , (Pi2 f)) (Pi f)}
    true.

- : select-tgtmap-implies-permission-combine-converse
    SG T1 T2 _ T EQV
    <- select-tgtmap-implies-deep-disjoint-join SG X J
    <- efxmap`deep-disjoint-join-implies-equiv-converse X J T1 T2 _ T EQV.

%worlds () (select-tgtmap-implies-permission-combine-converse _ _ _ _ _ _).
%total {} (select-tgtmap-implies-permission-combine-converse _ _ _ _ _ _). 


%theorem select-tgtmap-implies-permission-combine*
  : forall* {XM1} {XM2} {XM} {GM2} {P} {CM} {PM} {B} {Pi1} {Pi2} {Pi}
    forall {R: select-tgtmap XM GM2 XM1 XM2}
    {XM=>Pi: efxmap2perm P CM PM B XM Pi}
    {XM1=>Pi1: efxmap2perm P CM PM B XM1 Pi1}
    {XM2=>Pi2: efxmap2perm P CM PM B XM2 Pi2}
    exists {EQV: ({f} equiv (Pi f) ((Pi1 f) , (Pi2 f)))}
    true.

- : select-tgtmap-implies-permission-combine*
    XM/GM2=XM1+XM2 XM=>Pi XM1=>Pi1 XM2=>Pi2
    ([f] equiv/symmetric (Pi1+Pi2<=>Pi f))
    <- select-tgtmap-implies-deep-disjoint-join XM/GM2=XM1+XM2 XM1^XM2 XM1+XM2=XM
    <- efxmap`deep-disjoint-join-implies-equiv*
      XM1^XM2 XM1+XM2=XM XM1=>Pi1 XM2=>Pi2 XM=>Pi Pi1+Pi2<=>Pi.

%worlds () (select-tgtmap-implies-permission-combine* _ _ _ _ _).
%total {} (select-tgtmap-implies-permission-combine* _ _ _ _ _).


%theorem efxmap`leq-partition
  : forall* {XM1} {XM} {S}
    forall {LEQ: efxmap`leq XM1 XM}
    {DM: efxmap`domain XM1 S}
    exists {XM2} {XM3} {DJ: efxmap`deep-disjoint XM2 XM3}
    {J: efxmap`join XM2 XM3 XM}
    {DM: efxmap`domain XM2 S}
    true.

%worlds () (efxmap`leq-partition _ _ _ _ _ _ _).
%trustme %total { } (efxmap`leq-partition _ _ _ _ _ _ _).



% ======================================
% no-consume
% if a efxmap is no-consume, then its
% converted input and output permissions
% are the same.
% ======================================

%theorem apply-efx-no-consume-same-permission
  : forall* {X} {Pi} {Pi2}
    forall {NC-X: efx`not-consume X}
    {AP: apply-efx true X Pi Pi2}
    exists {AP: apply-efx false X Pi Pi2}
    true.

- : apply-efx-no-consume-same-permission
    efx`not-consume/read apply-efx/read apply-efx/read.

- : apply-efx-no-consume-same-permission
    efx`not-consume/write apply-efx/write apply-efx/write.

%worlds () (apply-efx-no-consume-same-permission _ _ _).
%total {} (apply-efx-no-consume-same-permission _ _ _).


%theorem apply-efx-no-consume-same-permission-converse
  : forall* {X} {Pi} {Pi2}
    forall {NC-X: efx`not-consume X}
    {AP: apply-efx false X Pi Pi2}
    exists {AP: apply-efx true X Pi Pi2}
    true.

- : apply-efx-no-consume-same-permission-converse
    efx`not-consume/read apply-efx/read apply-efx/read.

- : apply-efx-no-consume-same-permission-converse
    efx`not-consume/write apply-efx/write apply-efx/write.

%worlds () (apply-efx-no-consume-same-permission-converse _ _ _).
%total {} (apply-efx-no-consume-same-permission-converse _ _ _).


%theorem inner-efxmap`no-consume-same-permission
  : forall* {O} {FM} {PM} {M} {Pi}
    forall {NC-M: inner-efxmap`no-consume M}
    {M=>Pi: inner-efxmap2perm true O FM PM M Pi}
    exists {M=>Pi: inner-efxmap2perm false O FM PM M Pi}
    true.

- : inner-efxmap`no-consume-same-permission
    inner-efxmap`no-consume/0 inner-efxmap2perm/0 inner-efxmap2perm/0.

- : inner-efxmap`no-consume-same-permission NC-M
    (inner-efxmap2perm/U N!<MP MP+N+X=M FM->N=T T=>PF X=>PF MP=>P2)
    (inner-efxmap2perm/U N!<MP MP+N+X=M FM->N=T T=>PF X=>PF2 MP=>P2P)
    <- inner-efxmap`update-implies-lookup MP+N+X=M M->N=X
    <- inner-efxmap`lookup-implies-not-consume NC-M M->N=X NC-X
    <- inner-efxmap`fresh-update-preserves-no-consume-converse
      NC-M N!<MP MP+N+X=M NC-MP
    <- inner-efxmap`no-consume-same-permission NC-MP MP=>P2 MP=>P2P
    <- apply-efx-no-consume-same-permission NC-X X=>PF X=>PF2.

%worlds () (inner-efxmap`no-consume-same-permission _ _ _).
%total (T) (inner-efxmap`no-consume-same-permission _ T _).


%theorem inner-efxmap`no-consume-same-permission-converse
  : forall* {O} {FM} {PM} {M} {Pi}
    forall {NC-M: inner-efxmap`no-consume M}
    {M=>Pi: inner-efxmap2perm false O FM PM M Pi}
    exists {M=>Pi: inner-efxmap2perm true O FM PM M Pi}
    true.

- : inner-efxmap`no-consume-same-permission-converse
    inner-efxmap`no-consume/0 inner-efxmap2perm/0 inner-efxmap2perm/0.

- : inner-efxmap`no-consume-same-permission-converse NC-M
    (inner-efxmap2perm/U N!<MP MP+N+X=M FM->N=T T=>PF X=>PF MP=>P2)
    (inner-efxmap2perm/U N!<MP MP+N+X=M FM->N=T T=>PF X=>PF2 MP=>P2P)
    <- inner-efxmap`update-implies-lookup MP+N+X=M M->N=X
    <- inner-efxmap`lookup-implies-not-consume NC-M M->N=X NC-X
    <- inner-efxmap`fresh-update-preserves-no-consume-converse
      NC-M N!<MP MP+N+X=M NC-MP
    <- inner-efxmap`no-consume-same-permission-converse NC-MP MP=>P2 MP=>P2P
    <- apply-efx-no-consume-same-permission-converse NC-X X=>PF X=>PF2.

%worlds () (inner-efxmap`no-consume-same-permission-converse _ _ _).
%total (T) (inner-efxmap`no-consume-same-permission-converse _ T _).


%theorem efxmap`no-consume-same-permission
  : forall* {XM} {CM} {PM} {B} {Pi}
    forall {NC-XM: efxmap`no-consume XM}
    {XM=>Pi: efxmap2perm true CM PM B XM ([f] Pi f)}
    exists {XM=>Pi: efxmap2perm false CM PM B XM ([f] Pi f)}
    true.

- : efxmap`no-consume-same-permission
    efxmap`no-consume/0 efxmap2perm/0 efxmap2perm/0.

- : efxmap`no-consume-same-permission NC-XM
    (efxmap2perm/U N!<XMP XMP+N+M=XM B->N CM->C=FM T+M=>Pi1 T+XMP=>Pi2)
    (efxmap2perm/U N!<XMP XMP+N+M=XM B->N CM->C=FM F+M=>Pi1 F+XMP=>Pi2)
    <- efxmap`update-implies-lookup XMP+N+M=XM XM->N=M
    <- efxmap`lookup-implies-no-consume NC-XM XM->N=M NC-M
    <- inner-efxmap`no-consume-same-permission NC-M T+M=>Pi1 F+M=>Pi1
    <- efxmap`fresh-update-preserves-no-consume-converse
      NC-XM N!<XMP XMP+N+M=XM NC-XMP
    <- efxmap`no-consume-same-permission NC-XMP T+XMP=>Pi2 F+XMP=>Pi2.

%worlds () (efxmap`no-consume-same-permission _ _ _).
%total (T) (efxmap`no-consume-same-permission _ T _).


%theorem efxmap`no-consume-same-permission-converse
  : forall* {XM} {CM} {PM} {B} {Pi}
    forall {NC-XM: efxmap`no-consume XM}
    {XM=>Pi: efxmap2perm false CM PM B XM ([f] Pi f)}
    exists {XM=>Pi: efxmap2perm true CM PM B XM ([f] Pi f)}
    true.

- : efxmap`no-consume-same-permission-converse
    efxmap`no-consume/0 efxmap2perm/0 efxmap2perm/0.

- : efxmap`no-consume-same-permission-converse NC-XM
    (efxmap2perm/U N!<XMP XMP+N+M=XM B->N CM->C=FM F+M=>Pi1 F+XMP=>Pi2)
    (efxmap2perm/U N!<XMP XMP+N+M=XM B->N CM->C=FM T+M=>Pi1 T+XMP=>Pi2)
    <- efxmap`update-implies-lookup XMP+N+M=XM XM->N=M
    <- efxmap`lookup-implies-no-consume NC-XM XM->N=M NC-M
    <- inner-efxmap`no-consume-same-permission-converse NC-M F+M=>Pi1 T+M=>Pi1
    <- efxmap`fresh-update-preserves-no-consume-converse
      NC-XM N!<XMP XMP+N+M=XM NC-XMP
    <- efxmap`no-consume-same-permission-converse NC-XMP F+XMP=>Pi2 T+XMP=>Pi2.

%worlds () (efxmap`no-consume-same-permission-converse _ _ _).
%total (T) (efxmap`no-consume-same-permission-converse _ T _).


%theorem equiv-empty-implies-permission-equiv
  : forall* {CM} {PM} {B} {G} {M} {Pi}
    forall {M2G: efxmap2tgtmap M G}
    {EG: equiv-empty G}
    {M2P: efxmap2perm false CM PM B M ([f] Pi f)}
    exists {EQV: {f} equiv (Pi f) empty}
    true.

- : equiv-empty-implies-permission-equiv
    efxmap2tgtmap/0 equiv-empty/0 efxmap2perm/0 ([_] equiv/reflexive) .

- : equiv-empty-implies-permission-equiv
    (efxmap2tgtmap/U MP=>GP DM-D=S N!<MP MP+N+D=M GP+N+S=>G) EE-G M=>Pi
    ([f] equiv/transitive3
      (equiv/symmetric (Pi1+Pi2<=>Pi f))
      (equiv/combine (EQV1 f) (EQV2 f))
      (equiv/identity))
    <- efxmap2tgtmap-preserves-fresh N!<MP MP=>GP N!<GP
    <- equiv-empty/U-inversion EE-G N!<GP GP+N+S=>G EE-GP S-EQx
    <- set`eq-symmetric S-EQx S-EQ
    <- inner-efxmap`domain-respects-eq DM-D=S inner-efxmap`eq/ S-EQ DM-D=0
    <- inner-efxmap`domain-empty-implies-empty DM-D=0 D-EQ
    <- efxmap2perm/U-inversion M=>Pi N!<MP MP+N+D=M _ _ _
      BL _ CML Pi1 D=>Pi1 Pi2 MP=>Pi2 Pi1+Pi2<=>Pi
    <- inner-efxmap2perm-equiv D=>Pi1 inner-efxmap2perm/0
      bool`eq/ object`eq/ fldmap`eq/ predmap`eq/ D-EQ EQV1
    <- equiv-empty-implies-permission-equiv MP=>GP EE-GP MP=>Pi2 EQV2.

%worlds () (equiv-empty-implies-permission-equiv _ _ _ _).
%total (T) (equiv-empty-implies-permission-equiv T _ _ _).


