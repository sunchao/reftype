% Explicit Type Context
% $Id: cxt.elf,v 1.2 2013/05/07 05:54:47 csun Exp csun $

% definition

%abbrev utargets = [L] (targets/ (set/1 L) tgtmap/0 false).

%abbrev stargets = (targets/ set/0 tgtmap/0 true).


cxt : type. %name cxt B.

cxt/nil : cxt.

cxt/cons : object -> reftype -> cxt -> cxt.


% equalities

cxt-eq : cxt -> cxt -> type.

cxt-eq/ : cxt-eq B B.


% lookups 

cxt-lookup : cxt -> object -> reftype -> type.

cxt-lookup/hit : cxt-lookup (cxt/cons O RT _) O RT.

cxt-lookup/miss
  : cxt-lookup (cxt/cons _ _ B) O RT
    <- cxt-lookup B O RT.


% lookup object

cxt-lookup-loc : cxt -> nat -> object -> ty -> type.

cxt-lookup-loc/hit
  : cxt-lookup-loc (cxt/cons O (reftype/ T (utargets L)) _) L O T.

cxt-lookup-loc/miss
  : cxt-lookup-loc (cxt/cons _ _ B) L O T
    <- cxt-lookup-loc B L O T.


% well-formed context

wf-cxt : cxt -> type.

wf-cxt-h : nat -> cxt -> type.

wf-cxt-h/nil : wf-cxt-h _ cxt/nil.

wf-cxt-h/cons/u
  : wf-cxt-h N (cxt/cons _ (reftype/ _ (utargets M)) B)
    <- nat`gt N M
    <- wf-cxt-h M B.

wf-cxt-h/cons/s
  : wf-cxt-h N (cxt/cons _ (reftype/ _ stargets) B)
    <- wf-cxt-h N B.


wf-cxt/nil : wf-cxt cxt/nil.

wf-cxt/cons/u
  : wf-cxt (cxt/cons _ (reftype/ _ (utargets N)) B)
    <- wf-cxt-h N B.

wf-cxt/cons/s
  : wf-cxt (cxt/cons _ (reftype/ _ stargets) B)
    <- wf-cxt B.


% theorems

false-implies-object-eq 
  : {V:void} {O1:object} {O2}
    object`eq O1 O2
    -> type.

%mode false-implies-object-eq +V +O1 +O2 -E.
%worlds (objvar) (false-implies-object-eq _ _ _ _).
%total {} (false-implies-object-eq _ _ _ _).


false-implies-ty-eq
  : {V:void} {T1} {T2}
    ty`eq T1 T2
    -> type.

%mode false-implies-ty-eq +V +T1 +T2 -E.
%worlds (objvar) (false-implies-ty-eq _ _ _ _).
%total {} (false-implies-ty-eq _ _ _ _).


% lookup-loc

false-implies-cxt-lookup-loc
  : {V:void} cxt-lookup-loc B L O T -> type.

%mode +{V} +{B} +{L} -{O} -{T} -{LP: cxt-lookup-loc B L O T}
false-implies-cxt-lookup-loc V LP.
%worlds () (false-implies-cxt-lookup-loc _ _).
%total { } (false-implies-cxt-lookup-loc _ _).


cxt-lookup-loc-respects-eq
  : cxt-lookup-loc B1 L1 O1 T1
    -> cxt-eq B1 B2
    -> nat`eq L1 L2
    -> object`eq O1 O2
    -> ty`eq T1 T2
    -> cxt-lookup-loc B2 L2 O2 T2
    -> type.

%mode cxt-lookup-loc-respects-eq +L1 +BE +NE +OE +TE -L2.

- : cxt-lookup-loc-respects-eq L cxt-eq/ nat`eq/ object`eq/ ty`eq/ L.

%worlds () (cxt-lookup-loc-respects-eq _ _ _ _ _ _).
%total  {} (cxt-lookup-loc-respects-eq _ _ _ _ _ _).


cxt-lookup-loc-unique
  : wf-cxt B1
    -> wf-cxt B2
    -> cxt-lookup-loc B1 L1 O1 T1
    -> cxt-lookup-loc B2 L2 O2 T2
    -> cxt-eq B1 B2
    -> nat`eq L1 L2
    -> object`eq O1 O2
    -> ty`eq T1 T2
    -> type.

%mode cxt-lookup-loc-unique +D1 +D2 +D3 +D4 +D5 +D6 -D7 -D8.

%worlds (fracvar) (cxt-lookup-loc-unique _ _ _ _ _ _ _ _).
%trustme %total {} (cxt-lookup-loc-unique _ _ _ _ _ _ _ _).