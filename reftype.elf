%%% Reference Type

%%% Definitions

%%% Non-Nullness

nonnull : type. %name nonnull NN nn.

nonnull/yes : nonnull.

nonnull/may : nonnull.


nonnull`eq : nonnull -> nonnull -> type.

nonnull`eq/ : nonnull`eq NN NN.


nonnull`leq : nonnull -> nonnull -> type.

nonnull`leq/= : nonnull`leq NN1 NN2 <- nonnull`eq NN1 NN2.

nonnull`leq/< : nonnull`leq nonnull/may nonnull/yes.

%abbrev nonnull`sub = nonnull`leq.


%%% Annotation

annot : type.		%name annot A.

annot/unique : annot.

annot/shared : annot.

annot/borrow : annot.	% borrow CANNOT be used on fields


annot`eq : annot -> annot -> type.

annot`eq/ : annot`eq A A.


%%% Annotated Type

ty : type.		%name ty T.

ty/ : nonnull -> annot -> nat -> ty.

ty/null : ty.


%%% Targets

targets : type. %name targets G.

targets`eq : targets -> targets -> type.

targets`eq/ : targets`eq G G.


%%% Targets of Unique References:
%%%   first `set`: the set of all object targets - they should all be unique
%%%   second `set`: the capabilities needed for the targets

targets/unique : set -> set -> targets.

targets/shared : targets.


%%% Sub-relation on targets

targets`sub : targets -> targets -> type.

targets`sub/us : targets`sub (targets/unique _ _) targets/shared.

targets`sub/ss : targets`sub targets/shared targets/shared.

targets`sub/uu :
    set`leq U1 U2 ->
    set`leq M1 M2 ->
  targets`sub (targets/unique U1 M1) (targets/unique U2 M2).


%%% Reference Type

reftype : type.	%name reftype RT.

% reftype for "normal" references:
%  `nonnull`: the nonnull information of this reference.
%  `nat`: class id for this reference.
%  `targets`: all the possible targets of this reference.

reftype/ : nonnull -> nat -> targets -> reftype.


%%% Equality on Reference Type

reftype`eq : reftype -> reftype -> type.

reftype`eq/ : reftype`eq RT RT.


%%% Sub-relation between reftypes

reftype`sub : reftype -> reftype -> type.

reftype`sub/ :
    nonnull`leq NN1 NN2 ->
    targets`sub G1 G2 ->
  reftype`sub (reftype/ NN1 C G1) (reftype/ NN2 C G2).


%%% Not borrowed

not-borrow : annot -> type.

not-borrow/unique : not-borrow annot/unique.

not-borrow/shared : not-borrow annot/shared.


%%% Not unique

not-unique : annot -> type.

not-unique/borrow : not-unique annot/borrow.

not-unique/shared : not-unique annot/shared.


%%% Either unique or borrow

not-shared : annot -> type.

not-shared/unique : not-shared annot/unique.

not-shared/borrow : not-shared annot/borrow.



%%% Theorems

%theorem nonnull`eq-symmetric
  : forall* {NN1} {NN2}
    forall {E: nonnull`eq NN1 NN2}
    exists {E: nonnull`eq NN2 NN1}
    true.

- : nonnull`eq-symmetric nonnull`eq/ nonnull`eq/.

%worlds () (nonnull`eq-symmetric _ _).
%total { } (nonnull`eq-symmetric _ _).


%theorem nonnull`eq-transitive
  : forall* {NN1} {NN2} {NN3}
    forall {NN1=NN2: nonnull`eq NN1 NN2}
    {NN2=NN3: nonnull`eq NN2 NN3}
    exists {NN1=NN3: nonnull`eq NN1 NN3}
    true.

- : nonnull`eq-transitive nonnull`eq/ nonnull`eq/ nonnull`eq/.

%worlds () (nonnull`eq-transitive _ _ _).
%total { } (nonnull`eq-transitive _ _ _).


%theorem targets/unique-preserves-eq
  : forall* {S1} {S2} {R1} {R2}
    forall {E: set`eq S1 S2} {E: set`eq R1 R2}
    exists {E: targets`eq (targets/unique S1 R1) (targets/unique S2 R2)}
    true.

- : targets/unique-preserves-eq set`eq/ set`eq/ targets`eq/.

%worlds () (targets/unique-preserves-eq _ _ _).
%total {} (targets/unique-preserves-eq _ _ _).


%theorem nonnull`leq-respects-eq
  : forall* {NN} {NN'} {NN1} {NN1'}
    forall {LE: nonnull`leq NN NN1}
    {NN=NN': nonnull`eq NN NN'} {NN1=NN1': nonnull`eq NN1 NN1'}
    exists {LE: nonnull`leq NN' NN1'}
    true.

- : nonnull`leq-respects-eq LE nonnull`eq/ nonnull`eq/ LE.

%worlds () (nonnull`leq-respects-eq _ _ _ _).
%total { } (nonnull`leq-respects-eq _ _ _ _).


%theorem annot`shared-eq-unique-implies-false
  : forall {E: annot`eq annot/shared annot/unique}
    exists {F:void}
    true.

%worlds () (annot`shared-eq-unique-implies-false _ _).
%total { } (annot`shared-eq-unique-implies-false _ _).


%theorem not-unique-respects-eq
  : forall* {A} {A'}
    forall {NU: not-unique A} {AE: annot`eq A A'}
    exists {NU': not-unique A'}
    true.

- : not-unique-respects-eq NU annot`eq/ NU.

%worlds (objvar) (not-unique-respects-eq _ _ _).
%total { } (not-unique-respects-eq _ _ _).



%%% Exports

%abbrev nn	= nonnull.
%abbrev nn/yes	= nonnull/yes.
%abbrev nn/may	= nonnull/may.
%abbrev shared-ty = (ty/ nn/may annot/borrow z).