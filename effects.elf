%%% Effects is just a wrapper on efxmap and maybe-efx (for effects on shared). 
%%% $Id: effects.elf,v 1.1 2013/06/11 21:08:57 csun Exp csun $

%%% Definitions

effects : type.

effects/ : efxmap -> efx -> effects.

%abbrev no-effect = effects/ efxmap/0 efx/none.

%abbrev shared-effect = [X] (effects/ efxmap/0 X).



effects`join : effects -> effects -> effects -> type.

effects`join/
  : efxmap`join XM1 XM2 XM
    -> maybe-efx`join X1 X2 X
    -> effects`join
      (effects/ XM1 X1) (effects/ XM2 X2) (effects/ XM X).

effects`leq : effects -> effects -> type.

effects`leq/
  : efxmap`leq XM1 XM2 
    -> maybe-efx`leq X1 X2 
    -> effects`leq (effects/ XM1 X1) (effects/ XM2 X2).


effects`disjoint : effects -> effects -> type.

effects`disjoint/
  : efxmap`deep-disjoint XM1 XM2
    -> maybe-efx`disjoint X1 X2
    -> effects`disjoint (effects/ XM1 X1) (effects/ XM2 X2).


%theorem maybe-efx`join-implies-leq
  : forall* {X1} {X2} {X}
    forall {J: maybe-efx`join X1 X2 X}
    exists {L1: maybe-efx`leq X1 X}
    {L2: maybe-efx`leq X2 X}
    true.

- : maybe-efx`join-implies-leq
    maybe-efx`join/L maybe-efx`leq/1 (maybe-efx`leq/2 (leq/eq efx`eq/)).

- : maybe-efx`join-implies-leq
    maybe-efx`join/L maybe-efx`leq/1 maybe-efx`leq/1.

- : maybe-efx`join-implies-leq
    maybe-efx`join/R (maybe-efx`leq/2 (leq/eq efx`eq/)) maybe-efx`leq/1.

- : maybe-efx`join-implies-leq
    maybe-efx`join/R maybe-efx`leq/1 maybe-efx`leq/1.

- : maybe-efx`join-implies-leq
    (maybe-efx`join/J XJ) (maybe-efx`leq/2 L1) (maybe-efx`leq/2 L2)
    <- efx`join-implies-leq XJ L1 L2.

%worlds () (maybe-efx`join-implies-leq _ _ _).
%total {} (maybe-efx`join-implies-leq _ _ _).


%theorem effects`join-implies-leq
  : forall* {XX1} {XX2} {XX}
    forall {J1: effects`join XX1 XX2 XX}
    exists {L1: effects`leq XX1 XX}
    {L2: effects`leq XX2 XX}
    true.

- : effects`join-implies-leq 
    (effects`join/ XM1+XM2=XM X1+X2=X) 
    (effects`leq/ XM1<=XM X1<=X) (effects`leq/ XM2<=XM X2<=X)
    <- efxmap`join-implies-leq XM1+XM2=XM XM1<=XM XM2<=XM
    <- maybe-efx`join-implies-leq X1+X2=X X1<=X X2<=X.

%worlds () (effects`join-implies-leq _ _ _).
%total {} (effects`join-implies-leq _ _ _).
