%%% Type judgments for conservative type system based on FP.

%%% Type Checking Result

result : kind -> type.

%abbrev expr-result = result exprk.

%abbrev cond-result = result condk.

%abbrev args-result = result argsk.


result/expr : reftype -> env -> expr-result.

result/cond : env -> cond-result.

result/args/0 : env -> args-result.

result/args/+ : reftype -> env -> args-result -> args-result.


%%% Equality

result`eq : result K -> result K -> type.

result`eq/ : result`eq R R.


%%% Sub-relation on targets

targets`sub : set -> targets -> env -> targets -> env -> type.

targets`sub/ss : targets`sub _ targets/shared V targets/shared V.

%%% For the unique => shared case, all the object targets and field targets
%%% are consumed.
targets`sub/us :
    set+set2set S FS M ->
    set`split U0 S U1 ->
    set`split M0 M M1 ->
  targets`sub FS (targets/unique S _ _) (env/ U0 M0) targets/shared (env/ U1 M1).

%{
It's tricky to handle the case when either M1 or M2 contains shared. This is
because it's hard to add the information that the shared field is from
which class. It's not impossible though, just requires many changes.

This doesn't work for cases like
if (...) (new C).f else a
as well, since on the right hand side you can't get the fresh permissions.
targets`sub/uu :
    set`leq U1 U2 ->
    set`split M2 M1 Q ->
    set`split Q0 Q Q1 ->
    set`not-member M1 shared ->
    set`not-member M2 shared ->
    bool`leq W1 W2 ->
  targets`sub _ (env/ U Q0) (targets/unique U1 M1 W1) (targets/unique U2 M2 W2) (env/ U Q1).
}%

targets`sub/bb :
    set`leq S0 S1 ->
  targets`sub _ (targets/unique S0 set/0 false) V (targets/unique S1 set/0 false) V.


%%% Whether a reftype has encumbered permission.

has-encumbered : set -> bool -> type.

has-encumbered/1 : has-encumbered M false <- set`size M (s _).

has-encumbered/2 : has-encumbered _ true.


targets`sub/uu :
    set`leq S0 S1 ->
    has-encumbered M0 W0 ->
    set`split Q0 M Q1 ->
    set`combine M M0 M1 ->
    bool`leq W0 W1 ->
  targets`sub _ (targets/unique S0 M0 W0) (env/ U0 Q0)
    (targets/unique S1 M1 W1) (env/ U0 Q1).

targets`sub/fu :
    set`split Q0 M Q1 ->
  targets`sub _ targets/fresh (env/ U Q0) (targets/unique _ M true) (env/ U Q1).

targets`sub/fs : targets`sub _ targets/fresh V targets/shared V.

targets`sub/ff : targets`sub _ targets/fresh V targets/fresh V.


%%% Sub-relation between reftypes

expr-result-sub : clsmap -> expr-result -> expr-result -> type.

expr-result-sub/ :
    clsmap`lookup CM C FM ->
    fldmap`domain FM FS ->
    nonnull`sub NN1 NN2 ->
    targets`sub FS G0 V0 G1 V1 ->
  expr-result-sub CM (result/expr (reftype/ NN1 C G0) V0) (result/expr (reftype/ NN2 C G1) V1).

%%% TODO: for other result kinds?


%%% Type Checking Rules

reftyping : clsmap -> methmap -> cxt -> env -> term K -> result K -> type.


%%% ====================== Type Check Rules =======================

%%% Checking Literals

reftyping/lit :
    cxt`lookup-obj B O (s N) T ->
    ty2reftype (s N) T RT ->
  reftyping _ _ B V (lit O) (result/expr RT V).


%%% Reading a field

%%% TODO: thinking about removing this.
targets-capset : targets -> set -> type.

targets-capset/shared : targets-capset targets/shared _.

targets-capset/unique :
    set`disjoint M1 M2 ->
  targets-capset (targets/unique _ M1 _) M2.


readRefTyping : clsmap -> expr-result -> nat -> expr-result -> type.

readRefTyping/base :
    clsmap`lookup CM C FM ->
    fldmap`lookup FM F (ty/ NN A FC) ->
    make-tgts-and-caps M0 G0 F G1 M1 ->
    consider-ftype G1 M1 A G2 M2 ->
    targets-capset G0 M0 ->
  readRefTyping CM (result/expr (reftype/ nn/yes C G0) (env/ U M0)) F
    (result/expr (reftype/ NN FC G2) (env/ U M2)).

reftyping/read :
    reftyping CM MM B V E R ->
    readRefTyping CM R F R2 ->
  reftyping CM MM B V (read E F) R2.


%%% Writing a field

writeRefTyping : clsmap -> methmap -> cxt -> expr-result -> nat -> expr ->
    targets -> ty -> expr-result -> type.

%%% In subtype, we don't care about FS, since it's impossible for
%%% a field to be borrowed.
%%% TODO: looks like the result for this shouldn't have attached field caps.
%%% The result of writing a field should be unit
writeRefTyping/base :
    clsmap`lookup CM C FM ->
    fldmap`lookup FM F (ty/ NN A FC) ->
    make-tgts-and-caps M0 G0 F G1 M1 ->
    reftyping CM MM B (env/ U0 M1) E (result/expr RT V2) ->
    subtype CM _ RT V2 (ty/ NN A FC) _ (env/ U1 M3) ->
    consider-ftype G1 M3 A G2 M4 ->
  writeRefTyping CM MM B (result/expr (reftype/ nn/yes C G0) (env/ U0 M0))
    F E G1 (ty/ NN A FC) (result/expr (reftype/ NN FC G2) (env/ U1 M4)).

reftyping/write
  : reftyping CM MM B V1 E1 R1 ->
    writeRefTyping CM MM B R1 F E2 G T R2 ->
  reftyping CM MM B V1 (write E1 F E2) R2.


%%% Let

%{
For let, we have two approaches
1. change the context to map to reftype, instead of ty. So, the variable x
is mapped to the reftype from checking E. But, whenever consume something,
we need to trace down to the "root" of each binding. This approach is a bit
complicated.

2. suppose x is bound to (reftype/unique S G), we remove field capabilities for
all objects in S. For instance, if S = {a, b}, and in M we have (a.f,W),
(a.g, R), (b.f, R), then as result, we remove these from M, and add (x.f, R) to it.
In other words, we compute the most conservative capabilities we can get for x.
During the life time of x, all objects in S are not allowed to use, until x is being
released out of scope.

Currently, we use the second approach, since it's easier to implement.
In the future, we can come back here and switch to the first approach.
}%


%{
update-cxt : clsmap -> env -> ty -> (object -> env) -> type.

update-cxt/unique :
    clsmap`lookup CM C FM ->
    fldmap`domain FM FS ->
    nat+set2capmap (s N) FS cap/write M1 ->
    capmap-split M2 M1 M0 ->
    cxt`fresh B0 (s N) ->
    ({x} cxt`update B0 (s N) (cxt-info/ x (ty/ NN annot/unique C)) (B1 x)) ->
  update-cxt CM (env/ B0 M0) (ty/ NN annot/unique C) ([x] env/ (B1 x) M2).

update-cxt/shared :
    cxt`fresh B0 (s N) ->
    ({x} cxt`update B0 (s N) (cxt-info/ x (ty/ NN annot/shared C)) (B1 x)) ->
  update-cxt CM (env/ B0 M0) (ty/ NN annot/shared C) ([x] env/ (B1 x) M0).
}%

%%% Make sure the let variable is not in the result target (no leaking).

get-objset : set -> set -> type.

get-objset/0 : get-objset set/0 set/0.

get-objset/z :
    get-objset M' S ->
    set`not-member M' shared-tgt ->
    set`add M' shared-tgt M ->
  get-objset M S.

get-objset/U :
    get-objset M' S' ->
    nat2pair (s K) (pair/ N _) ->
    set`not-member M' (s K) ->
    set`add M' (s K) M ->
    set`add S' N S ->
  get-objset M S.

not-in-capset : nat -> set -> type.

not-in-capset/ :
    get-objset M S ->
    set`not-member S N ->
  not-in-capset N M.


not-in-targets : nat -> targets -> type.

not-in-targets/unique :
    set`not-member S N ->
    not-in-capset N M ->
  not-in-targets N (targets/unique S M _).

not-in-targets/shared : not-in-targets _ targets/shared.

not-in-targets/fresh : not-in-targets _ targets/fresh.


restore-pinned-caps : set -> targets -> set -> type.

restore-pinned-caps/shared : restore-pinned-caps K targets/shared K.

restore-pinned-caps/fresh : restore-pinned-caps K targets/fresh K.

restore-pinned-caps/unique
  : restore-pinned-caps K1 (targets/unique _ K2 _) K
    <- set`combine K1 K2 K.


%%% Reclaim capabilities encumbered by the let variable.

%%% At the moment we just be conservative, and don't allow ANY field
%%% from the let variable be consumed.

letRefTyping : clsmap -> methmap -> cxt ->
    expr-result -> (object -> term K) -> result K -> type.

%%% M5 = M0 - M2 + M3
letRefTyping/base/unique :
    clsmap`lookup CM C FM ->
    fldmap`domain FM FS ->
    set+set2set S FS M2 ->
    cxt`fresh B0 (s N) ->
    ({x} cxt`update B0 (s N) (cxt-info/ x (ty/ NN annot/unique C)) (B1 x)) ->
    set`split U0 S U1 ->
    set`not-member U1 (s N) ->
    set`add U1 (s N) U2 ->
    set`split M0 M2 M4 ->
    nat+set2set (s N) FS M3 ->
    set`split M5 M3 M4 ->
    ({x} reftyping CM MM (B1 x) (env/ U2 M5) (F x)
      (result/expr (reftype/ NN1 C1 G1) (env/ U3 M6))) ->
    not-in-targets (s N) G1 ->
    set`split M6 M3 M7 -> % M3 still in result
    set`split M8 M2 M7 ->
    set`split M9 M1 M8 ->
    set`not-member U4 (s N) ->
    set`add U4 (s N) U3 ->
    set`combine U4 S U5 ->
  letRefTyping CM MM B0 (result/expr (reftype/ NN C (targets/unique S M1 W)) (env/ U0 M0))
    F (result/expr (reftype/ NN1 C1 G1) (env/ U5 M9)).

%%% TODO: remove the not-in-targets constraints
letRefTyping/base/shared :
    clsmap`lookup CM C FM ->
    cxt`fresh B0 (s N) ->
    ({x} cxt`update B0 (s N) (cxt-info/ x (ty/ NN annot/shared C)) (B1 x)) ->
    ({x} reftyping CM MM (B1 x) V (F x)
      (result/expr (reftype/ NN1 C1 G1) V1)) ->
    not-in-targets (s N) G1 ->
  letRefTyping CM MM B0 (result/expr (reftype/ NN C targets/shared) V)
    F (result/expr (reftype/ NN1 C1 G1) V1).

reftyping/let :
    reftyping CM MM B V E R1 ->
    letRefTyping CM MM B R1 F R2 ->
  reftyping CM MM B V (let E F) R2.


reftyping/seq
  : reftyping CM MM B V0 (seq E1 E2) R
    <- reftyping CM MM B V0 E1 (result/expr (reftype/ _ _ G1) (env/ H1 K1))
    <- restore-pinned-caps K1 G1 K2
    <- reftyping CM MM B (env/ H1 K2) E2 R.


%{
This rule enables first consume an unique field, and then
restore its uniqueness by consuming another unique expression.

This is useful for checking expressions such as:
  (m.next = n.next; n.next = m)

It also enables assigning to oneself:
  (n.next = n.next.next)

Note, we cannot do:
  let (read E0 F) ([x] write E0 F (E1 x))

since when checking the second E0, we don't have the input
permissions. This perhaps can be resolved by:
  let E0 ([y] (let (read y F) ([x] write y F (E1 y x))))

However, I think this doesn't really make sense because
we could have: let (read (new C) F) ([x] write (new C) F (E1 x))

}%

reftyping/restore
  : reftyping CM MM B0 (env/ H0 K0) (let (read (lit O) F) ([x] write (lit O) F (E x)))
    (result/expr (reftype/ FNN FC G1) (env/ H3 K5))
    <- cxt`lookup-obj B0 O (s N0) T
    <- ty2reftype (s N0) T (reftype/ nn/yes C0 G0)
    <- clsmap`lookup CM C0 FM
    <- fldmap`lookup FM F (ty/ FNN annot/unique FC)
    <- clsmap`lookup CM FC FFM
    <- fldmap`domain FFM FS
    <- make-tgts-and-caps K0 G0 F G1 K1
    <- cxt`fresh B0 (s N)
    <- nat+set2set (s N) FS K2
    <- set`combine K1 K2 K3
    <- set`combine H0 (set/1 (s N)) H1
    <- ({x} cxt`update B0 (s N) (cxt-info/ x (ty/ FNN annot/unique FC)) (B1 x))
    <- ({x} reftyping CM MM (B1 x) (env/ H1 K3)
         (E x) (result/expr RT1 V1))
    <- subtype CM _ RT1 V1 (ty/ FNN annot/unique FC) _ (env/ H2 K4)
    <- set`remove H2 (set/1 (s N)) H3
    <- set`remove K4 K2 K5.

reftyping/swap
  : reftyping CM MM B (env/ H0 K0)
    (let (read (lit O) F) ([x] (seq (write (lit O) F E) (lit x))))
    (result/expr (reftype/ FNN FC targets/fresh) (env/ H1 K3))
    <- cxt`lookup-obj B O (s N0) T
    <- ty2reftype (s N0) T (reftype/ nn/yes C0 G0)
    <- clsmap`lookup CM C0 FM
    <- fldmap`lookup FM F (ty/ FNN annot/unique FC)
    <- make-tgts-and-caps K0 G0 F G1 K1
    <- reftyping CM MM B (env/ H0 K1) E (result/expr RT1 V1)
    <- subtype CM _ RT1 V1 (ty/ FNN annot/unique FC) _ (env/ H1 K2)
    <- restore-pinned-caps K2 G1 K3.


%%% Method Calls

%{
Type checking arguments

One issue we need to consider is aliasing between unique arguments.
For instance, if a method is annotated as "reads a.f, writes b.f", and
a and b are aliases to each other, then we cannot check the call.

Given this situation doesn't occur very often, and is also illegal in FP,
here we just simply disallow "duplicated" cap while checking method call effects.

For borrowed parameter type, we'll get back the associated capabilities after
the method call is checked. Consider:

@write b.f
foo(@borrow T b): ...

And both x and y are unique reference of type T, and both f and g are unique fields.
Then,

call foo(if a > 3 then x else y.g)

will need input capabilities (x.f, y.g)

After foo is checked, we should get permissions for x.f and y.g back.

}%


argsRefTyping : clsmap -> methmap -> cxt -> env -> args -> methty -> args-result -> type.

argsRefTyping/0 : argsRefTyping _ _ _ V args/0 (methty/base T) (result/args/0 V).

argsRefTyping/+ :
    reftyping CM MM B V0 E (result/expr RT V1) ->
    subtype CM FS RT V1 T AV V2 ->
    argsRefTyping CM MM B V2 A MT AR ->
  argsRefTyping CM MM B V0 (args/+ E A) (methty/arg T FS MT) (result/args/+ RT AV AR).


%{
How to check method call:
1. Check arguments, given an input type context and capability set.
   For each argument, type checking will yield a corresponding
   result reftype, and produce a output capability set. After checking all
   arguments, we get a output (unchanged) type context, and a set of
   remaining capabilities.
2. Check method type using the result/args and remaining capability set.
   For each formal argument, if it's unique, remove the associated capabilities
   from output capability set. Otherwise, we'll just return the same capabilities
   for the argument.

Note that both step 1 and 2 may modify the context and capability set (step 1
will only weaken the condition, while step 2 will only strengthen the
condition).

To make the rule interesting, we should need to prove that capabilities can be
reclaimed after they are not needed after the method call checking. This is
also needed for checking tLet.
}%

%%% Converting a ty to a reftype for a fresh new object.

ty2new-reftype : ty -> reftype -> type.

ty2new-reftype/unique :
    ty2new-reftype (ty/ NN annot/unique C) (reftype/ NN C targets/fresh).

ty2new-reftype/shared :
    ty2new-reftype (ty/ NN annot/shared C) (reftype/ NN C targets/shared).


%%% Look for encumbered capabilities and put them back in the holding capability set

restore-cap-targets : env -> targets -> env -> type.

restore-cap-targets/shared : restore-cap-targets V targets/shared V.

restore-cap-targets/unique :
    set`combine M0 M1 M ->
  restore-cap-targets (env/ U M0) (targets/unique _ M1 _) (env/ U M).


restore-cap-call : env -> reftype -> env -> ty -> env -> type.

restore-cap-call/uniqueORshared :
    not-borrow A ->
  restore-cap-call V _ _ (ty/ _ A _) V.

restore-cap-call/borrow :
    restore-cap-targets V0 (targets/unique S M W) V0' ->
    env`combine V0' V1 V2 ->
  restore-cap-call V0 (reftype/ _ _ (targets/unique S M W)) V1 (ty/ _ annot/borrow _) V2.


callRefTyping* : clsmap -> methmap -> env -> args-result -> methty -> expr-result -> type.

callRefTyping/arg/0 :
    env`combine V0 V1 V ->
    ty2new-reftype T RT ->
  callRefTyping* CM MM V0 (result/args/0 V1) (methty/base T) (result/expr RT V).

callRefTyping/arg/+ :
    restore-cap-call V0 RT AV T V1 ->
    callRefTyping* CM MM V1 AR MT R ->
  callRefTyping* CM MM V0 (result/args/+ RT AV AR) (methty/arg T _ MT) R.

%abbrev callRefTyping = [CM][MM][AR][MT][R] callRefTyping* CM MM empty-env AR MT R.


%%% This is defined in consistency.elf
clsmap-result : clsmap -> cxt -> result K -> type.


reftyping/call :
    clsmap`fresh CM M ->
    methmap`lookup MM M MT ->
    argsRefTyping CM MM B V A MT AR ->
    callRefTyping CM MM AR MT R ->
  reftyping CM MM B V (call M A) R.

reftyping/const :
    clsmap`lookup CM C FM ->
    fldmap`domain FM S ->
    set`not-member S all ->
    set`add S all S' ->
    methmap`lookup MM C (methty/arg (ty/ nn/yes annot/unique C) set/0 MT) ->
    argsRefTyping CM MM B V A MT AR ->
    callRefTyping CM MM AR MT R ->
  reftyping CM MM B V (call C (args/+ (alloc C S' set/0) A)) R.

reftyping/sub :
    reftyping CM MM B V0 E R0 ->
    expr-result-sub CM R0 R1 ->
    clsmap-result CM B R1 ->
  reftyping CM MM B V0 E R1.

reftyping/if :
    reftyping CM MM B V0 C (result/cond V1) ->
    reftyping CM MM B V1 E1 (result/expr RT V2) ->
    reftyping CM MM B V1 E2 (result/expr RT V2) ->
  reftyping CM MM B V0 (if C E1 E2) (result/expr RT V2).


%%% Conditionals

%%% After the "eql" statement, we need to reclaim the capabilities
%%% encumbered in `RT1` and `RT2`.

reftyping/eq :
    reftyping CM MM B V0 E1 (result/expr (reftype/ _ _ G1) V1) ->
    reftyping CM MM B V1 E2 (result/expr (reftype/ _ _ G2) V2) ->
    restore-cap-targets V2 G1 V3 ->
    restore-cap-targets V3 G2 V4 ->
  reftyping CM MM B V0 (eql E1 E2) (result/cond V4).

reftyping/not :
    reftyping CM MM B V C R ->
  reftyping CM MM B V (not C) R.

%%% For the second branch, the input capabilities need to be the same
%%% as output (e.g., no a.f = new C in C2). Otherwise, for the conditional
%%% in the output the two branches will have different permissions.
reftyping/and :
    reftyping CM MM B V0 C1 (result/cond V1) ->
    reftyping CM MM B V1 C2 (result/cond V1) ->
  reftyping CM MM B V0 (and C1 C2) (result/cond V1).



%%% Theorems

%theorem result/expr-eq-inversion
  : forall* {RT1} {V1} {RT2} {V2}
    forall {E: result`eq (result/expr RT1 V1) (result/expr RT2 V2)}
    exists {TE: reftype`eq RT1 RT2} {VE: env`eq V1 V2}
    true.

- : result/expr-eq-inversion result`eq/ reftype`eq/ env`eq/.

%worlds (gtermvar) (result/expr-eq-inversion _ _ _).
%total { } (result/expr-eq-inversion _ _ _).


%theorem result/cond-eq-inversion
  : forall* {V1} {V2}
    forall {E: result`eq (result/cond V1) (result/cond V2)}
    exists {VE: env`eq V1 V2}
    true.

- : result/cond-eq-inversion result`eq/ env`eq/.

%worlds (gtermvar) (result/cond-eq-inversion _ _).
%total { } (result/cond-eq-inversion _ _).


%theorem result/args/0-eq-inversion
  : forall* {V1} {V2}
    forall {E: result`eq (result/args/0 V1) (result/args/0 V2)}
    exists {VE: env`eq V1 V2}
    true.

- : result/args/0-eq-inversion result`eq/ env`eq/.

%worlds (gtermvar) (result/args/0-eq-inversion _ _).
%total { } (result/args/0-eq-inversion _ _).


%theorem result/args/+-eq-inversion
  : forall* {V1} {V2} {RT1} {RT2} {A1} {A2}
    forall {E: result`eq (result/args/+ RT1 V1 A1) (result/args/+ RT2 V2 A2)}
    exists {TE: reftype`eq RT1 RT2} {VE: env`eq V1 V2} {AE: result`eq A1 A2}
    true.

- : result/args/+-eq-inversion result`eq/ reftype`eq/ env`eq/ result`eq/.

%worlds (gtermvar) (result/args/+-eq-inversion _ _ _ _).
%total { } (result/args/+-eq-inversion _ _ _ _).


%theorem get-objset-respects-eq
  : forall* {M0} {S0} {M1} {S1}
    forall {M0=>S0: get-objset M0 S0}
    {M0=M1: set`eq M0 M1}
    {S0=S1: set`eq S0 S1}
    exists {M1=>S1: get-objset M1 S1}
    true.

- : get-objset-respects-eq M=>S set`eq/ set`eq/ M=>S.

%worlds () (get-objset-respects-eq _ _ _ _).
%total { } (get-objset-respects-eq _ _ _ _).


%theorem get-objset/z-inversion
  : forall* {S} {M} {M'}
    forall {CM-B-S: get-objset M S}
    {NM: set`not-member M' shared}
    {AD: set`add M' shared M}
    exists {CM-B-S': get-objset M' S}
    true.

- : get-objset/z-inversion
    (get-objset/z M0=>S Z!<M0 M0+Z=M)
    Z!<M' M'+Z=M M'=>S
    <- set`not-member-add-cancels Z!<M0 M0+Z=M Z!<M' M'+Z=M nat`eq/ set`eq/ M0=M'
    <- get-objset-respects-eq M0=>S M0=M' set`eq/ M'=>S.

- : get-objset/z-inversion
    (get-objset/U M0=>S' N+F=K
      (K!<M0:set`not-member M0 (s K))
      (M0+K=M:set`add M0 (s K) M)
      (S'+K=S:set`add S' _ S))
    (Z!<M':set`not-member M' shared) (M'+Z=M:set`add M' shared M)
    (get-objset/U M1=>S' N+F=K K!<M1 M1+K=M' S'+K=S)
    <- nat`succ-implies-gt-zero _ K>Z
    <- set`add-commutes-converse M'+Z=M M0+K=M (nat`ne/< K>Z) M1 M1+K=M' M1+Z=M0
    <- set`add-preserves-not-member-converse Z!<M' M1+K=M' Z!<M1
    <- get-objset/z-inversion M0=>S' Z!<M1 M1+Z=M0 M1=>S'
    <- set`add-preserves-not-member-converse K!<M0 M1+Z=M0 K!<M1.

%worlds (gtermvar) (get-objset/z-inversion _ _ _ _).
%total (A) (get-objset/z-inversion A _ _ _).


%theorem get-objset-preserves-member
  : forall* {M} {S} {K} {N} {F}
    forall {M=>S: get-objset M S}
    {K<-M: set`member M (s K)}
    {N+F=>K: nat2pair (s K) (pair/ N F)}
    exists {N<-S: set`member S N}
    true.

%theorem get-objset-preserves-member/L
  : forall* {M} {S} {K} {N} {F} {M'} {S'} {N'} {F'} {K'} {B}
    forall {M'=>S': get-objset M' S'}
    {N'+F'=>K': nat2pair (s K') (pair/ N' F')}
    {K'!<M': set`not-member M' (s K')}
    {M'+K'=M: set`add M' (s K') M}
    {S'+N'=S: set`add S' N' S}
    {K<-M: set`member M (s K)}
    {N+F=>K: nat2pair (s K) (pair/ N F)}
    {K=K'?: nat`eq? (s K) (s K') B}
    exists {N<-S: set`member S N}
    true.

- : get-objset-preserves-member/L
    _ K'=>N'+F' _ _ S'+N'=S _ K=>N+F (nat`eq?/yes) N<-S
    <- nat2pair-unique K=>N+F K'=>N'+F' nat`eq/ PE
    <- pair-eq-implies-eq PE (N=N':nat`eq N N') F=F'
    <- nat`eq-symmetric N=N' N'=N
    <- set`add-implies-member S'+N'=S N'<-S
    <- set`member-respects-eq N'<-S set`eq/ N'=N N<-S.

- : get-objset-preserves-member/L
    M'=>S' _ K'!<M' M'+K'=M S'+N'=S K<-M K=>N+F (nat`eq?/no (K<>K':nat`ne (s K) (s K'))) N<-S
    <- set`add-preserves-member-converse K<-M M'+K'=M K<>K' K<-M'
    <- get-objset-preserves-member M'=>S' K<-M' K=>N+F N<-S'
    <- set`add-preserves-member N<-S' S'+N'=S N<-S.

- : get-objset-preserves-member
    (get-objset/U M'=>S' N'+F'=K' K'!<M' M'+K'=M S'+N'=S) K<-M N+F=K N<-S
    <- nat`eq?-total EQ?
    <- get-objset-preserves-member/L M'=>S' N'+F'=K' K'!<M' M'+K'=M S'+N'=S K<-M N+F=K EQ? N<-S.

- : get-objset-preserves-member
    (get-objset/z M'=>S Z!<M' M'+Z=M) K<-M N+F=K N<-S
    <- nat`succ-implies-gt-zero _ GT
    <- set`add-preserves-member-converse K<-M M'+Z=M (nat`ne/> GT) K<-M'
    <- get-objset-preserves-member M'=>S K<-M' N+F=K N<-S.

%worlds () (get-objset-preserves-member _ _ _ _)
  (get-objset-preserves-member/L _ _ _ _ _ _ _ _ _).
%total (A A0) (get-objset-preserves-member A _ _ _)
  (get-objset-preserves-member/L A0 _ _ _ _ _ _ _ _).


%theorem get-objset-preserves-member-converse
  : forall* {M} {S} {N}
    forall {M=>S: get-objset M S}
    {N<-S: set`member S N}
    exists {F} {K} {N+F=K: pair2nat (pair/ N F) (s K)}
    {SK<-M: set`member M (s K)}
    true.

%theorem get-objset-preserves-member-converse/L
  : forall* {M} {S} {N} {M'} {S'} {N'} {F'} {K'} {B}
    forall {M'=>S': get-objset M' S'}
    {N'+F'=SK': pair2nat (pair/ N' F') (s K')}
    {SK!<M': set`not-member M' (s K')}
    {M'+SK=M: set`add M' (s K') M}
    {S'+N'=S: set`add S' N' S}
    {N<-S: set`member S N}
    {EQ?: nat`eq? N N' B}
    exists {F} {K} {N+F=K: pair2nat (pair/ N F) (s K)}
    {SK<-M: set`member M (s K)}
    true.

- : get-objset-preserves-member-converse/L
    M'=>S' N'+F'=SK' SK'!<M' M'+SK'=M S'+N'=S N<-S nat`eq?/yes
    _ _ N'+F'=SK' SK'<-M
    <- set`add-implies-member M'+SK'=M SK'<-M.

- : get-objset-preserves-member-converse/L M'=>S'
    (N'+F'=SK':nat2pair (s K') (pair/ N' F'))
    SK'!<M' M'+SK'=M S'+N'=S N<-S (nat`eq?/no (N<>N':nat`ne N N'))
    F K N+F=SK SK<-M
    <- set`add-implies-member M'+SK'=M SK'<-M
    <- set`add-preserves-member-converse N<-S S'+N'=S N<>N' N<-S'
    <- get-objset-preserves-member-converse M'=>S' N<-S' F K N+F=SK SK<-M'
    <- set`add-preserves-member SK<-M' M'+SK'=M SK<-M.

- : get-objset-preserves-member-converse (get-objset/z M'=>S Z!<M' M'+Z=M)
    N<-S _ _ N+F=K SK<-M
    <- get-objset-preserves-member-converse M'=>S N<-S _ _ N+F=K SK<-M'
    <- set`add-preserves-member SK<-M' M'+Z=M SK<-M.

- : get-objset-preserves-member-converse
    (get-objset/U M'=>S' N'+F'=SK' SK'!<M' M'+SK'=M S'+N'=S) N<-S _ _ N+F=K K<-M
    <- nat`eq?-total EQ?
    <- get-objset-preserves-member-converse/L
      M'=>S' N'+F'=SK' SK'!<M' M'+SK'=M S'+N'=S N<-S EQ? _ _ N+F=K K<-M.

%worlds () (get-objset-preserves-member-converse _ _ _ _ _ _)
  (get-objset-preserves-member-converse/L _ _ _ _ _ _ _ _ _ _ _).
%total (A A0) (get-objset-preserves-member-converse A _ _ _ _ _)
  (get-objset-preserves-member-converse/L A0 _ _ _ _ _ _ _ _ _ _).


%theorem get-objset-implies-not-member
  : forall* {M} {S} {N} {K} {N'} {F}
    forall {M=>S: get-objset M S}
    {N!<S: set`not-member S N}
    {K<-M: set`member M (s K)}
    {N'+F=K: nat2pair (s K) (pair/ N' F)}
    exists {N<>N': nat`ne N N'}
    true.

%theorem get-objset-implies-not-member/L
  : forall* {M} {S} {N} {K} {N'} {F} {B}
    forall {M=>S: get-objset M S}
    {N!<S: set`not-member S N}
    {K<-M: set`member M (s K)}
    {N'+F=K: nat2pair (s K) (pair/ N' F)}
    {N=N'?: nat`eq? N N' B}
    exists {N<>N': nat`ne N N'}
    true.

- : get-objset-implies-not-member/L _ _ _ _ (nat`eq?/no NE) NE.

- : get-objset-implies-not-member/L M=>S N!<S K<-M N+F=K (nat`eq?/yes) NE
    <- get-objset-preserves-member M=>S K<-M N+F=K N<-S
    <- set`member-not-member-contradiction N<-S N!<S V
    <- nat`false-implies-ne V NE.

- : get-objset-implies-not-member M=>S N!<S K<-M N+F=K NE
    <- nat`eq?-total EQ?
    <- get-objset-implies-not-member/L M=>S N!<S K<-M N+F=K EQ? NE.

%worlds () (get-objset-implies-not-member/L _ _ _ _ _ _).
%total { } (get-objset-implies-not-member/L _ _ _ _ _ _).

%worlds () (get-objset-implies-not-member _ _ _ _ _).
%total { } (get-objset-implies-not-member _ _ _ _ _).