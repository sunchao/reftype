%%% The definition of disjoint is not strong enough for
%%% us. We need to show that even if two map's domain
%%% have intersection, if the data involved in the intersection
%%% are disjoint, then the maps are still disjoint. We call
%%% this 'deep-disjoint'.

tgtmap`deep-disjoint : tgtmap -> tgtmap -> type.

tgtmap`deep-disjoint/L : tgtmap`deep-disjoint tgtmap/0 M.

tgtmap`deep-disjoint/R : tgtmap`deep-disjoint M tgtmap/0.

tgtmap`deep-disjoint/=
  : tgtmap`deep-disjoint (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)
    <- nat`eq N1 N2
    <- set`disjoint D1 D2
    <- tgtmap`deep-disjoint M1 M2.

tgtmap`deep-disjoint/< 
  : tgtmap`deep-disjoint (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)
    <- nat`plus (s N0) N1 N2
    <- tgtmap`deep-disjoint M1 (tgtmap/+ N0 D2 M2).

tgtmap`deep-disjoint/>
  : tgtmap`deep-disjoint (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)
    <- nat`plus (s N3) N2 N1
    <- tgtmap`deep-disjoint (tgtmap/+ N3 D1 M1) M2.



%%% Theorems 


%theorem tgtmap`false-implies-deep-disjoint :
    forall* {XM1} {XM2}
    forall  {F:void}
    exists  {D: tgtmap`deep-disjoint XM1 XM2}
    true.

%worlds () (tgtmap`false-implies-deep-disjoint _ _).
%total  {} (tgtmap`false-implies-deep-disjoint _ _).


%theorem tgtmap`meta-reduce-deep-disjoint :
    forall* {M1} {M2} {M3} {M4}
    forall  {F:void}
            {D1: tgtmap`deep-disjoint M1 M2}
            {D2: tgtmap`deep-disjoint M3 M4}
    true.

%worlds () (tgtmap`meta-reduce-deep-disjoint _ _ _).
%total  {} (tgtmap`meta-reduce-deep-disjoint _ _ _).
%reduces X < Y (tgtmap`meta-reduce-deep-disjoint _ X Y).


%theorem tgtmap`deep-disjoint-respects-eq :
	forall* {M1} {M2} {M1P} {M2P}
	forall {A:tgtmap`deep-disjoint M1 M2} 
         {E1:tgtmap`eq M1 M1P} {E2:tgtmap`eq M2 M2P} 
	exists {AP:tgtmap`deep-disjoint M1P M2P}
	true.

- : tgtmap`deep-disjoint-respects-eq A tgtmap`eq/ tgtmap`eq/ A.

%worlds () (tgtmap`deep-disjoint-respects-eq _ _ _ _).
%total {} (tgtmap`deep-disjoint-respects-eq _ _ _ _).
%reduces A = AP (tgtmap`deep-disjoint-respects-eq A _ _ AP).


%theorem tgtmap`disjoint-implies-deep-disjoint :
    forall* {M1} {M2}
    forall  {D: tgtmap`disjoint M1 M2}
    exists  {D: tgtmap`deep-disjoint M1 M2}
    true.

- : tgtmap`disjoint-implies-deep-disjoint 
    tgtmap`disjoint/L tgtmap`deep-disjoint/L.

- : tgtmap`disjoint-implies-deep-disjoint 
    tgtmap`disjoint/R tgtmap`deep-disjoint/R.

- : tgtmap`disjoint-implies-deep-disjoint 
    (tgtmap`disjoint/< D P) (tgtmap`deep-disjoint/< DP P)
    <- tgtmap`disjoint-implies-deep-disjoint D DP.

- : tgtmap`disjoint-implies-deep-disjoint 
    (tgtmap`disjoint/> D P) (tgtmap`deep-disjoint/> DP P)
    <- tgtmap`disjoint-implies-deep-disjoint D DP.

%worlds () (tgtmap`disjoint-implies-deep-disjoint _ _).
%total (D) (tgtmap`disjoint-implies-deep-disjoint D _).



%theorem tgtmap`deep-disjoint-symmetric :
	forall* {M1} {M2}
	forall {D:tgtmap`deep-disjoint M1 M2} 
	exists {D:tgtmap`deep-disjoint M2 M1}
	true.

- : tgtmap`deep-disjoint-symmetric tgtmap`deep-disjoint/L tgtmap`deep-disjoint/R.

- : tgtmap`deep-disjoint-symmetric tgtmap`deep-disjoint/R tgtmap`deep-disjoint/L.

- : tgtmap`deep-disjoint-symmetric
    (tgtmap`deep-disjoint/< D P) (tgtmap`deep-disjoint/> DP P)
    <- tgtmap`deep-disjoint-symmetric D DP.

- : tgtmap`deep-disjoint-symmetric
    (tgtmap`deep-disjoint/> D P) (tgtmap`deep-disjoint/< DP P)
    <- tgtmap`deep-disjoint-symmetric D DP.

- : tgtmap`deep-disjoint-symmetric
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 N1=N2) 
    (tgtmap`deep-disjoint/= M2<>M1 D2<>D1 N2=N1)
    <- tgtmap`deep-disjoint-symmetric M1<>M2 M2<>M1
    <- set`disjoint-symmetric D1<>D2 D2<>D1
    <- nat`eq-symmetric N1=N2 N2=N1.

%worlds () (tgtmap`deep-disjoint-symmetric _ _).
%total (D) (tgtmap`deep-disjoint-symmetric D _).



%theorem tgtmap`deep-disjoint/=-inversion :
    forall* {XM1} {XM2} {M1} {M2} {L1} {L2}
    forall  {D: tgtmap`deep-disjoint (tgtmap/+ L1 M1 XM1) (tgtmap/+ L2 M2 XM2)}
            {E: nat`eq L1 L2}
    exists  {D: set`disjoint M1 M2} {D: tgtmap`deep-disjoint XM1 XM2}
    true.

- : tgtmap`deep-disjoint/=-inversion 
    (tgtmap`deep-disjoint/= XM1<>XM2 M1<>M2 nat`eq/) nat`eq/
    M1<>M2 XM1<>XM2.

- : tgtmap`deep-disjoint/=-inversion
    (tgtmap`deep-disjoint/< J SL0+L1=L2) L1=L2 M1<>M2 XM1<>XM2
    <- nat`plus-implies-gt SL0+L1=L2 nat`eq/ L2>L1
    <- nat`eq-ne-implies-false L1=L2 (nat`ne/< L2>L1) F
    <- set`false-implies-disjoint F M1<>M2
    <- tgtmap`false-implies-deep-disjoint F XM1<>XM2
    <- tgtmap`meta-reduce-deep-disjoint F XM1<>XM2 J.

- : tgtmap`deep-disjoint/=-inversion
    (tgtmap`deep-disjoint/> J SL0+L2=L1) L1=L2 M1<>M2 XM1<>XM2
    <- nat`plus-implies-gt SL0+L2=L1 nat`eq/ L1>L2
    <- nat`eq-ne-implies-false L1=L2 (nat`ne/> L1>L2) F
    <- set`false-implies-disjoint F M1<>M2
    <- tgtmap`false-implies-deep-disjoint F XM1<>XM2
    <- tgtmap`meta-reduce-deep-disjoint F XM1<>XM2 J.

%worlds () (tgtmap`deep-disjoint/=-inversion _ _ _ _).
%total  {} (tgtmap`deep-disjoint/=-inversion _ _ _ _).
%reduces AP < A (tgtmap`deep-disjoint/=-inversion A _ _ AP).



%theorem tgtmap`deep-disjoint/<-inversion :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {N0}
	forall {A:tgtmap`deep-disjoint (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)}
         {P:plus (s N0) N1 N2} 	       
	exists {AP:tgtmap`deep-disjoint M1 (tgtmap/+ N0 D2 M2)}
	true.

- : tgtmap`deep-disjoint/<-inversion (tgtmap`deep-disjoint/< A P) PP AP
    <- nat`plus-right-cancels P PP nat`eq/ nat`eq/ N0+1=N0P+1
    <- succ-cancels N0+1=N0P+1 N0=N0P
    <- tgtmap`map/+-preserves-eq N0=N0P set`eq/ tgtmap`eq/ M022=M022P
    <- tgtmap`deep-disjoint-respects-eq A tgtmap`eq/ M022=M022P AP.

- : tgtmap`deep-disjoint/<-inversion
    (tgtmap`deep-disjoint/> AP N3+1+N2=N1) N0+1+N1=N2 A
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- tgtmap`false-implies-eq F M311=M1
    <- tgtmap`false-implies-eq F M2=M022
    <- tgtmap`deep-disjoint-respects-eq AP M311=M1 M2=M022 A.

- : tgtmap`deep-disjoint/<-inversion
    (tgtmap`deep-disjoint/= XM1<>XM2 M1<>M2 L1=L2) SN0+L1=L2 J
    <- nat`plus-implies-gt SN0+L1=L2 nat`eq/ L2>L1
    <- nat`eq-ne-implies-false L1=L2 (nat`ne/< L2>L1) F
    <- tgtmap`false-implies-deep-disjoint F J
    <- tgtmap`meta-reduce-deep-disjoint F J XM1<>XM2.

%worlds () (tgtmap`deep-disjoint/<-inversion _ _ _).
%total {}  (tgtmap`deep-disjoint/<-inversion _ _ _).
%reduces AP < A (tgtmap`deep-disjoint/<-inversion A _ AP).


%theorem tgtmap`deep-disjoint/>-inversion :
	forall* {N1} {D1} {M1} {N2} {D2} {M2} {N3}
	forall {A:tgtmap`deep-disjoint (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)}
	       {P:plus (s N3) N2 N1}
	exists {AP:tgtmap`deep-disjoint (tgtmap/+ N3 D1 M1) M2}
	true.

- : tgtmap`deep-disjoint/>-inversion (tgtmap`deep-disjoint/> A P) PP AP
    <- nat`plus-right-cancels P PP nat`eq/ nat`eq/ N3+1=N3P+1
    <- succ-cancels N3+1=N3P+1 N3=N3P
    <- tgtmap`map/+-preserves-eq N3=N3P set`eq/ tgtmap`eq/ M311=M311P
    <- tgtmap`deep-disjoint-respects-eq A M311=M311P tgtmap`eq/ AP.

- : tgtmap`deep-disjoint/>-inversion
    (tgtmap`deep-disjoint/< AP N0+1+N1=N2) N3+1+N2=N1 A
    <- nat`plus-implies-gt N3+1+N2=N1 nat`eq/ N1>N2
    <- nat`plus-implies-gt N0+1+N1=N2 nat`eq/ N2>N1
    <- nat`gt-anti-symmetric N1>N2 N2>N1 F
    <- tgtmap`false-implies-eq F M1=M311
    <- tgtmap`false-implies-eq F M022=M2
    <- tgtmap`deep-disjoint-respects-eq AP M1=M311 M022=M2 A.

- : tgtmap`deep-disjoint/>-inversion
    (tgtmap`deep-disjoint/= XM1<>XM2 M1<>M2 L1=L2) SN3+L2=L1 J
    <- nat`plus-implies-gt SN3+L2=L1 nat`eq/ L1>L2
    <- nat`eq-ne-implies-false L1=L2 (nat`ne/> L1>L2) F
    <- tgtmap`false-implies-deep-disjoint F J
    <- tgtmap`meta-reduce-deep-disjoint F J XM1<>XM2.

%worlds () (tgtmap`deep-disjoint/>-inversion _ _ _).
%total { } (tgtmap`deep-disjoint/>-inversion _ _ _).
%reduces AP < A (tgtmap`deep-disjoint/>-inversion A _ AP).



%theorem tgtmap`shift-left-preserves-deep-disjoint :
	forall* {N} {D} {M1} {M2} {SM1}
	forall {A:tgtmap`deep-disjoint M1 M2} {S1:tgtmap`shift N M1 SM1}
        exists {SA:tgtmap`deep-disjoint SM1 (tgtmap/+ N D M2)}
        true.

- : tgtmap`shift-left-preserves-deep-disjoint
    _ tgtmap`shift/0 tgtmap`deep-disjoint/L.

- : tgtmap`shift-left-preserves-deep-disjoint M111*M2 (tgtmap`shift/+ N+1+N1=N1P)
    (tgtmap`deep-disjoint/> M111*M2 N1+1+N=N1P)
    <- plus-swap-succ N+1+N1=N1P N+N1+1=N1P
    <- plus-commutative N+N1+1=N1P N1+1+N=N1P.

%worlds () (tgtmap`shift-left-preserves-deep-disjoint _ _ _).
%total { } (tgtmap`shift-left-preserves-deep-disjoint _ _ _).


%theorem tgtmap`shift-left-preserves-deep-disjoint-converse :
	forall* {N} {D} {M1} {M2} {SM1}
	forall {SA:tgtmap`deep-disjoint SM1 (tgtmap/+ N D M2)} 
         {S1:tgtmap`shift N M1 SM1}
  exists {A:tgtmap`deep-disjoint M1 M2}
	true.

- : tgtmap`shift-left-preserves-deep-disjoint-converse
    _ tgtmap`shift/0 tgtmap`deep-disjoint/L.

- : tgtmap`shift-left-preserves-deep-disjoint-converse 
    M111*M222 (tgtmap`shift/+ N2+1+N3=N1) M311*M2
    <- plus-swap-succ N2+1+N3=N1 N2+N3+1=N1
    <- plus-commutative N2+N3+1=N1 N3+1+N2=N1
    <- tgtmap`deep-disjoint/>-inversion M111*M222 N3+1+N2=N1 M311*M2.

%worlds () (tgtmap`shift-left-preserves-deep-disjoint-converse _ _ _).
%total { } (tgtmap`shift-left-preserves-deep-disjoint-converse _ _ _).


%theorem tgtmap`shift-right-preserves-deep-disjoint :
	forall* {N} {D} {M1} {M2} {SM2}
	forall {A:tgtmap`deep-disjoint M1 M2} {S2:tgtmap`shift N M2 SM2}
        exists {SA:tgtmap`deep-disjoint (tgtmap/+ N D M1) SM2}
	true.

- : tgtmap`shift-right-preserves-deep-disjoint _ tgtmap`shift/0 tgtmap`deep-disjoint/R.

- : tgtmap`shift-right-preserves-deep-disjoint M1*M222 (tgtmap`shift/+ N+1+N2=N2P)
                               (tgtmap`deep-disjoint/< M1*M222 N2+1+N=N2P)
    <- plus-swap-succ N+1+N2=N2P N+N2+1=N2P
    <- plus-commutative N+N2+1=N2P N2+1+N=N2P.

%worlds () (tgtmap`shift-right-preserves-deep-disjoint _ _ _).
%total { } (tgtmap`shift-right-preserves-deep-disjoint _ _ _).


%theorem tgtmap`shift-right-preserves-deep-disjoint-converse :
	forall* {N} {D} {M1} {M2} {SM2}
	forall {SA:tgtmap`deep-disjoint (tgtmap/+ N D M1) SM2} {S2:tgtmap`shift N M2 SM2}
        exists {A:tgtmap`deep-disjoint M1 M2}
	true.

- : tgtmap`shift-right-preserves-deep-disjoint-converse
    _ tgtmap`shift/0 tgtmap`deep-disjoint/R.

- : tgtmap`shift-right-preserves-deep-disjoint-converse
    M111*M322 (tgtmap`shift/+ N1+1+N2=N3) M1*M222
    <- plus-swap-succ N1+1+N2=N3 N1+N2+1=N3
    <- plus-commutative N1+N2+1=N3 N2+1+N1=N3
    <- tgtmap`deep-disjoint/<-inversion M111*M322 N2+1+N1=N3 M1*M222.

%worlds () (tgtmap`shift-right-preserves-deep-disjoint-converse _ _ _).
%total { } (tgtmap`shift-right-preserves-deep-disjoint-converse _ _ _).



%theorem tgtmap`shift-preserves-deep-disjoint :
    forall* {N} {M1} {M2} {SM1} {SM2}
	  forall {A:tgtmap`deep-disjoint M1 M2} 
           {S1:tgtmap`shift N M1 SM1} {S2:tgtmap`shift N M2 SM2}
  	exists {SA:tgtmap`deep-disjoint SM1 SM2}
	  true.

- : tgtmap`shift-preserves-deep-disjoint 
    _ tgtmap`shift/0 _ tgtmap`deep-disjoint/L.

- : tgtmap`shift-preserves-deep-disjoint 
    _ _ tgtmap`shift/0 tgtmap`deep-disjoint/R.

- : tgtmap`shift-preserves-deep-disjoint
    (tgtmap`deep-disjoint/< M1*M022 N0+1+N1=N2)
    (tgtmap`shift/+ N+1+N1=N4) (tgtmap`shift/+ N+1+N2=N5)
    (tgtmap`deep-disjoint/< M1*M022 N0+1+N4=N5)
    <- plus-swap-succ N+1+N1=N4 N+N1+1=N4
    <- plus-commutative N+N1+1=N4 N1+1+N=N4
    <- plus-commutative N0+1+N1=N2 N1+N0+1=N2
    <- plus-associative-converse* N1+N0+1=N2 N+1+N2=N5 N+1+N1=N4 N4+N0+1=N5
    <- plus-commutative N4+N0+1=N5 N0+1+N4=N5.

- : tgtmap`shift-preserves-deep-disjoint
    (tgtmap`deep-disjoint/> M311*M2 N3+1+N2=N1)
    (tgtmap`shift/+ N+1+N1=N4) (tgtmap`shift/+ N+1+N2=N5)
    (tgtmap`deep-disjoint/> M311*M2 N3+1+N5=N4)
    <- plus-swap-succ N+1+N2=N5 N+N2+1=N5
    <- plus-commutative N+N2+1=N5 N2+1+N=N5
    <- plus-commutative N3+1+N2=N1 N2+N3+1=N1
    <- plus-associative-converse* N2+N3+1=N1 N+1+N1=N4 N+1+N2=N5 N5+N3+1=N4
    <- plus-commutative N5+N3+1=N4 N3+1+N5=N4.

- : tgtmap`shift-preserves-deep-disjoint 
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 nat`eq/)
    (tgtmap`shift/+ P1) (tgtmap`shift/+ P2)
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 N3=N4)
    <- nat`plus-deterministic P1 P2 nat`eq/ nat`eq/ N3=N4.

%worlds () (tgtmap`shift-preserves-deep-disjoint _ _ _ _).
%total { } (tgtmap`shift-preserves-deep-disjoint _ _ _ _).



%theorem tgtmap`shift-preserves-deep-disjoint-converse :
	forall* {N} {M1} {M2} {SM1} {SM2}
	forall {SA:tgtmap`deep-disjoint SM1 SM2}
         {S1:tgtmap`shift N M1 SM1} {S2:tgtmap`shift N M2 SM2} 
	exists {A:tgtmap`deep-disjoint M1 M2}
	true.

- : tgtmap`shift-preserves-deep-disjoint-converse
    _ tgtmap`shift/0 _ tgtmap`deep-disjoint/L.

- : tgtmap`shift-preserves-deep-disjoint-converse
    _ _ tgtmap`shift/0 tgtmap`deep-disjoint/R.

- : tgtmap`shift-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/< M1*M055 N0+1+N4=N5)
    (tgtmap`shift/+ N+1+N1=N4) (tgtmap`shift/+ N+1+N2=N5)
    (tgtmap`deep-disjoint/< M1*M055 N0+1+N1=N2)
    <- plus-commutative N+1+N1=N4 N1+N+1=N4
    <- plus-swap-succ-converse N1+N+1=N4 N1+1+N=N4
    <- plus-associative-converse N1+N+1=N4 N0+1+N4=N5 N2P N0+1+N1=N2P N2P+N+1=N5
    <- plus-commutative N+1+N2=N5 N2+N+1=N5
    <- plus-right-cancels N2P+N+1=N5 N2+N+1=N5 nat`eq/ nat`eq/ N2P=N2
    <- plus-respects-eq N0+1+N1=N2P nat`eq/ nat`eq/ N2P=N2 N0+1+N1=N2.
                                  
- : tgtmap`shift-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/> M611*M2 N6+1+N5=N4)
    (tgtmap`shift/+ N+1+N1=N4) (tgtmap`shift/+ N+1+N2=N5)
    (tgtmap`deep-disjoint/> M611*M2 N6+1+N2=N1)
    <- plus-commutative N+1+N2=N5 N2+N+1=N5
    <- plus-swap-succ-converse N2+N+1=N5 N2+1+N=N5
    <- plus-associative-converse N2+N+1=N5 N6+1+N5=N4 N1P N6+1+N2=N1P N1P+N+1=N4
    <- plus-commutative N+1+N1=N4 N1+N+1=N4
    <- plus-right-cancels N1P+N+1=N4 N1+N+1=N4 nat`eq/ nat`eq/ N1P=N1
    <- plus-respects-eq N6+1+N2=N1P nat`eq/ nat`eq/ N1P=N1 N6+1+N2=N1.

- : tgtmap`shift-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 nat`eq/)
    (tgtmap`shift/+ P1) (tgtmap`shift/+ P2) 
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 EQ)
    <- nat`plus-left-cancels P1 P2 nat`eq/ nat`eq/ EQ.

%worlds () (tgtmap`shift-preserves-deep-disjoint-converse _ _ _ _).
%total { } (tgtmap`shift-preserves-deep-disjoint-converse _ _ _ _).


%theorem tgtmap`join-preserves-deep-disjoint* :
	forall* {M1} {M2} {M3} {M4}
	forall {D1: tgtmap`deep-disjoint M1 M4} {D2: tgtmap`deep-disjoint M2 M4}
	       {A: tgtmap`join M1 M2 M3}
	exists {D3:tgtmap`deep-disjoint M3 M4}
	true.

% a lemma that counts the size of maps to help prove termination
%theorem tgtmap`join-preserves-deep-disjoint*/L :
	forall* {M1} {M2} {M3} {M4}
	forall {S1} {S2} {SZ1:tgtmap`size M1 S1} {SZ2:tgtmap`size M2 S2}
         {D1:tgtmap`deep-disjoint M1 M4} {D2:tgtmap`deep-disjoint M2 M4}
	       {A:tgtmap`join M1 M2 M3}
	exists {D3:tgtmap`deep-disjoint M3 M4}
	true.

- : tgtmap`join-preserves-deep-disjoint* D1 D2 J D3
    <- tgtmap`size-total SZ1
    <- tgtmap`size-total SZ2
    <- tgtmap`join-preserves-deep-disjoint*/L _ _ SZ1 SZ2 D1 D2 J D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    _ _ _ _ tgtmap`deep-disjoint/R _ _ tgtmap`deep-disjoint/R.

- : tgtmap`join-preserves-deep-disjoint*/L
    _ _ _ _ _ tgtmap`deep-disjoint/R _ tgtmap`deep-disjoint/R.

- : tgtmap`join-preserves-deep-disjoint*/L _ _ _ _ _ D tgtmap`join/L D.

- : tgtmap`join-preserves-deep-disjoint*/L _ _ _ _ D _ tgtmap`join/R D.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/< D1 N5+1+N1=N4) D2X 
    (tgtmap`join/= J _ nat`eq/)
    (tgtmap`deep-disjoint/< D3 N5+1+N1=N4)
    <- tgtmap`deep-disjoint/<-inversion D2X N5+1+N1=N4 D2
    <- tgtmap`join-preserves-deep-disjoint*/L S1 S2 SZ1 SZ2 D1 D2 J D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/< D1 N5+1+N1=N4) D2X
    (tgtmap`join/> J N3+1+N2=N1)
    (tgtmap`deep-disjoint/< D3 N6+1+N2=N4)
    <- plus-swap-succ N3+1+N2=N1 N3+N2+1=N1
    <- plus-associative-converse N3+N2+1=N1 N5+1+N1=N4 N6 N5+1+N3=N6 N6+N2+1=N4
    <- plus-swap-succ-converse N6+N2+1=N4 N6+1+N2=N4
    <- tgtmap`deep-disjoint/<-inversion D2X N6+1+N2=N4 D2
    <- plus-swap-succ N5+1+N3=N6 N5+N3+1=N6
    <- plus-commutative N5+N3+1=N6 N3+1+N5=N6
    <- tgtmap`shift-right-preserves-deep-disjoint
      D1 (tgtmap`shift/+ N3+1+N5=N6) D1<<N3
    <- tgtmap`join-preserves-deep-disjoint*/L
      (s S1) S2 (tgtmap`size/+ SZ1) SZ2 D1<<N3 D2 J D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/> D1 N5+1+N4=N1) D2X 
    (tgtmap`join/= J DJ nat`eq/)
    (tgtmap`deep-disjoint/> D3 N5+1+N4=N1)
    <- tgtmap`deep-disjoint/>-inversion D2X N5+1+N4=N1 D2
    <- tgtmap`join-preserves-deep-disjoint*/L 
      _ _ (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2) 
      D1 D2 (tgtmap`join/= J DJ nat`eq/) D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/> D1 N5+1+N4=N1) D2X
    (tgtmap`join/< J N0+1+N1=N2)
    (tgtmap`deep-disjoint/> D3 N5+1+N4=N1)
    <- plus-swap-succ N5+1+N4=N1 N5+N4+1=N1
    <- plus-associative-converse N5+N4+1=N1 N0+1+N1=N2 N6 N0+1+N5=N6 N6+N4+1=N2
    <- plus-swap-succ-converse N6+N4+1=N2 N6+1+N4=N2
    <- tgtmap`deep-disjoint/>-inversion D2X N6+1+N4=N2 D2
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2) 
      D1 D2 (tgtmap`join/< J N0+1+N5=N6) D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    D1X (tgtmap`deep-disjoint/< D2 N6+1+N2=N4) 
    (tgtmap`join/< J N0+1+N1=N2)
    (tgtmap`deep-disjoint/< D3 N5+1+N1=N4)
    <- plus-swap-succ N0+1+N1=N2 N0+N1+1=N2
    <- plus-associative-converse N0+N1+1=N2 N6+1+N2=N4 N5 N6+1+N0=N5 N5+N1+1=N4
    <- plus-swap-succ-converse N5+N1+1=N4 N5+1+N1=N4
    <- tgtmap`deep-disjoint/<-inversion D1X N5+1+N1=N4 D1
    <- plus-swap-succ N6+1+N0=N5 N6+N0+1=N5
    <- plus-commutative N6+N0+1=N5 N0+1+N6=N5
    <- tgtmap`shift-right-preserves-deep-disjoint 
      D2 (tgtmap`shift/+ N0+1+N6=N5) D2<<N0
    <- tgtmap`join-preserves-deep-disjoint*/L 
      S1 (s S2) SZ1 (tgtmap`size/+ SZ2) D1 D2<<N0 J D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    D1X (tgtmap`deep-disjoint/> D2 N6+1+N4=N2)
    (tgtmap`join/> J N3+1+N2=N1)
    (tgtmap`deep-disjoint/> D3 N6+1+N4=N2)
    <- plus-swap-succ N6+1+N4=N2 N6+N4+1=N2
    <- plus-associative-converse N6+N4+1=N2 N3+1+N2=N1 N5 N3+1+N6=N5 N5+N4+1=N1
    <- plus-swap-succ-converse N5+N4+1=N1 N5+1+N4=N1
    <- tgtmap`deep-disjoint/>-inversion D1X N5+1+N4=N1 D1
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2) 
      D1 D2 (tgtmap`join/> J N3+1+N6=N5) D3.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/< D1 N5+1+N1=N4)
    (tgtmap`deep-disjoint/> D2 N6+1+N4=N2) JX D3X
    <- plus-swap-succ N5+1+N1=N4 N5+N1+1=N4
    <- plus-associative-converse N5+N1+1=N4 N6+1+N4=N2 N0 N6+1+N5=N0 N0+N1+1=N2
    <- plus-swap-succ-converse N0+N1+1=N2 N0+1+N1=N2
    <- tgtmap`join/<-inversion JX N0+1+N1=N2 _ J M=M113
    <- tgtmap`eq-symmetric M=M113 M113=M
    <- tgtmap`join-preserves-deep-disjoint*/L
      S1 (s S2) SZ1 (tgtmap`size/+ SZ2)
      D1 (tgtmap`deep-disjoint/> D2 N6+1+N5=N0) J D3
    <- tgtmap`deep-disjoint-respects-eq
      (tgtmap`deep-disjoint/< D3 N5+1+N1=N4) M113=M tgtmap`eq/ D3X.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/> D1 N5+1+N4=N1)
    (tgtmap`deep-disjoint/< D2 N6+1+N2=N4) JX D3X
    <- plus-swap-succ N6+1+N2=N4 N6+N2+1=N4
    <- plus-associative-converse N6+N2+1=N4 N5+1+N4=N1 N3 N5+1+N6=N3 N3+N2+1=N1
    <- plus-swap-succ-converse N3+N2+1=N1 N3+1+N2=N1
    <- tgtmap`join/>-inversion JX N3+1+N2=N1 _ J M=M223
    <- tgtmap`eq-symmetric M=M223 M223=M
    <- tgtmap`join-preserves-deep-disjoint*/L _ _ (tgtmap`size/+ SZ1) SZ2
      (tgtmap`deep-disjoint/> D1 N5+1+N6=N3) D2 J D3
    <- tgtmap`deep-disjoint-respects-eq
      (tgtmap`deep-disjoint/< D3 N6+1+N2=N4) M223=M tgtmap`eq/ D3X.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= D1 X1 nat`eq/) (tgtmap`deep-disjoint/= D2 X2 nat`eq/)
    (tgtmap`join/= J MJ nat`eq/) (tgtmap`deep-disjoint/= DD XX nat`eq/)
    <- tgtmap`join-preserves-deep-disjoint*/L _ _ SZ1 SZ2 D1 D2 J DD
    <- set`union-preserves-disjoint* X1 X2 MJ XX.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= D1 X nat`eq/) 
    (tgtmap`deep-disjoint/> D2 P1) (tgtmap`join/< J P2)
    (tgtmap`deep-disjoint/= DDD X nat`eq/)
    <- nat`plus-right-cancels P1 P2 nat`eq/ nat`eq/ EQ
    <- nat`succ-cancels EQ EQP
    <- tgtmap`map/+-preserves-eq EQP set`eq/ tgtmap`eq/ MEQ
    <- tgtmap`eq-symmetric MEQ MEQP
    <- tgtmap`join-respects-eq J tgtmap`eq/ MEQP tgtmap`eq/ JP
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ SZ1 (tgtmap`size/+ SZ2) D1 D2 JP DDD.
      
- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= D1 X nat`eq/) 
    (tgtmap`deep-disjoint/< D2 P1) (tgtmap`join/> J P2) 
    (tgtmap`deep-disjoint/< DDD P1)
    <- nat`plus-right-cancels P2 P1 nat`eq/ nat`eq/ EQ
    <- nat`succ-cancels EQ EQP
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ (tgtmap`size/+ SZ1) SZ2 (tgtmap`deep-disjoint/= D1 X EQP) D2 J DDD.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/> D1 P) (tgtmap`deep-disjoint/= D2 X nat`eq/) 
    (tgtmap`join/> J PP) (tgtmap`deep-disjoint/= DD X nat`eq/)
    <- nat`plus-right-cancels PP P nat`eq/ nat`eq/ EQ
    <- nat`succ-cancels EQ EQP
    <- tgtmap`map/+-preserves-eq EQP set`eq/ tgtmap`eq/ MEQ
    <- tgtmap`join-respects-eq J MEQ tgtmap`eq/ tgtmap`eq/ JP
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ (tgtmap`size/+ SZ1) SZ2 D1 D2 JP DD.
      
- : tgtmap`join-preserves-deep-disjoint*/L 
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/< D1 P1) (tgtmap`deep-disjoint/= D2 X nat`eq/)
    (tgtmap`join/< J P2)
    (tgtmap`deep-disjoint/< DDD P1)
    <- nat`plus-right-cancels P2 P1 nat`eq/ nat`eq/ EQ
    <- nat`succ-cancels EQ EQP
    <- tgtmap`join-preserves-deep-disjoint*/L
      _ _ SZ1 (tgtmap`size/+ SZ2) D1 (tgtmap`deep-disjoint/= D2 X EQP) J DDD.

%%% all the impossible cases

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= D1 X1 nat`eq/) (tgtmap`deep-disjoint/= D2 X2 nat`eq/)
    (tgtmap`join/> J P) DD
    <- nat`plus-implies-gt P nat`eq/ GT
    <- nat`eq-ne-implies-false nat`eq/ (nat`ne/> GT) F
    <- tgtmap`false-implies-deep-disjoint F DD.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= D1 X1 nat`eq/) (tgtmap`deep-disjoint/= D2 X2 nat`eq/)
    (tgtmap`join/< J P) DD
    <- nat`plus-implies-gt P nat`eq/ GT
    <- nat`eq-ne-implies-false nat`eq/ (nat`ne/> GT) F
    <- tgtmap`false-implies-deep-disjoint F DD.
 
- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= _ _ nat`eq/) 
    (tgtmap`deep-disjoint/< _ P) (tgtmap`join/= _ _ nat`eq/) DD
    <- nat`plus-implies-gt P nat`eq/ GT
    <- nat`eq-ne-implies-false nat`eq/ (nat`ne/> GT) F
    <- tgtmap`false-implies-deep-disjoint F DD.

- : tgtmap`join-preserves-deep-disjoint*/L
    (s S1) (s S2) (tgtmap`size/+ SZ1) (tgtmap`size/+ SZ2)
    (tgtmap`deep-disjoint/= _ _ nat`eq/) 
    (tgtmap`deep-disjoint/> _ P) (tgtmap`join/= _ _ nat`eq/) DD
    <- nat`plus-implies-gt P nat`eq/ GT
    <- nat`eq-ne-implies-false nat`eq/ (nat`ne/> GT) F
    <- tgtmap`false-implies-deep-disjoint F DD.

%worlds () (tgtmap`join-preserves-deep-disjoint*/L _ _ _ _ _ _ _ _).
%total {S1 S2 D1} (tgtmap`join-preserves-deep-disjoint*/L S1 S2 _ _ D1 _ _ _).

%worlds () (tgtmap`join-preserves-deep-disjoint* _ _ _ _).
%total { } (tgtmap`join-preserves-deep-disjoint* _ _ _ _).


%abbrev join-left-preserves-deep-disjoint* = tgtmap`join-preserves-deep-disjoint*.


%theorem join-right-preserves-deep-disjoint* :
	forall* {M1} {M2} {M3} {M0}
	forall {D1:tgtmap`deep-disjoint M0 M1} 
         {D2:tgtmap`deep-disjoint M0 M2}
	       {A:tgtmap`join M1 M2 M3}
	exists {D3:tgtmap`deep-disjoint M0 M3}
	true.

- : join-right-preserves-deep-disjoint* D1 D2 U D3
    <- tgtmap`deep-disjoint-symmetric D1 D1s
    <- tgtmap`deep-disjoint-symmetric D2 D2s
    <- join-left-preserves-deep-disjoint* D1s D2s U D3s
    <- tgtmap`deep-disjoint-symmetric D3s D3.

%worlds () (join-right-preserves-deep-disjoint* _ _ _ _).
%total { } (join-right-preserves-deep-disjoint* _ _ _ _).


%theorem tgtmap`join-preserves-deep-disjoint-converse:
	forall* {M1} {M2} {M3} {M4}
	forall {D3:tgtmap`deep-disjoint M3 M4}
	       {A:tgtmap`join M1 M2 M3}
	exists {D1:tgtmap`deep-disjoint M1 M4} {D2:tgtmap`deep-disjoint M2 M4}
	true.

- : tgtmap`join-preserves-deep-disjoint-converse
    D tgtmap`join/L tgtmap`deep-disjoint/L D.

- : tgtmap`join-preserves-deep-disjoint-converse
    D tgtmap`join/R D tgtmap`deep-disjoint/L.

- : tgtmap`join-preserves-deep-disjoint-converse
    tgtmap`deep-disjoint/R _
    tgtmap`deep-disjoint/R tgtmap`deep-disjoint/R.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/< D P)
    (tgtmap`join/= J _ nat`eq/)
    (tgtmap`deep-disjoint/< D1 P) (tgtmap`deep-disjoint/< D2 P)
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/< D P1)
    (tgtmap`join/< J P2)
    (tgtmap`deep-disjoint/< D1 P1) D2S
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- tgtmap`shift-preserves-deep-disjoint 
      D2 (tgtmap`shift/+ P2sc) (tgtmap`shift/+ P1sc) D2S.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/< D P1)
    (tgtmap`join/> J P2)
    D1S (tgtmap`deep-disjoint/< D2 P1)
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- plus-swap-succ P2 P2s
    <- plus-commutative P2s P2sc
    <- tgtmap`shift-preserves-deep-disjoint 
      D1 (tgtmap`shift/+ P2sc) (tgtmap`shift/+ P1sc) D1S.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/> D P1)
    (tgtmap`join/= J DE nat`eq/)
    D1S D2S
    <- tgtmap`join-preserves-deep-disjoint-converse
      D (tgtmap`join/= J DE nat`eq/) D1 D2
    <- plus-swap-succ P1 P1s
    <- plus-commutative P1s P1sc
    <- tgtmap`shift-left-preserves-deep-disjoint D1 (tgtmap`shift/+ P1sc) D1S
    <- tgtmap`shift-left-preserves-deep-disjoint D2 (tgtmap`shift/+ P1sc) D2S.

- : tgtmap`join-preserves-deep-disjoint-converse
	(tgtmap`deep-disjoint/> D P1)
	(tgtmap`join/< J P2)
        D1S D2S
    <- plus-swap-succ P1 P1s
    <- plus-associative-converse P1s P2 _ P3 P4s
    <- tgtmap`join-preserves-deep-disjoint-converse D (tgtmap`join/< J P3) D1 D2
    <- plus-commutative P1s P1sc
    <- plus-commutative P4s P4sc
    <- tgtmap`shift-left-preserves-deep-disjoint D1 (tgtmap`shift/+ P1sc) D1S
    <- tgtmap`shift-left-preserves-deep-disjoint D2 (tgtmap`shift/+ P4sc) D2S.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/> D P1)
    (tgtmap`join/> J P2)
    D1S D2S
    <- plus-swap-succ P1 P1s
    <- plus-associative-converse P1s P2 _ P3 P4s
    <- tgtmap`join-preserves-deep-disjoint-converse D (tgtmap`join/> J P3) D1 D2
    <- plus-commutative P1s P1sc
    <- plus-commutative P4s P4sc
    <- tgtmap`shift-left-preserves-deep-disjoint D1 (tgtmap`shift/+ P4sc) D1S
    <- tgtmap`shift-left-preserves-deep-disjoint D2 (tgtmap`shift/+ P1sc) D2S.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/= D X nat`eq/) (tgtmap`join/= J P nat`eq/) 
    (tgtmap`deep-disjoint/= D1 X1 nat`eq/) (tgtmap`deep-disjoint/= D2 X2 nat`eq/)
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2
    <- set`union-preserves-disjoint-converse X P X1 X2.

- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/= D X nat`eq/) (tgtmap`join/< J P) 
    (tgtmap`deep-disjoint/= D1 X nat`eq/) (tgtmap`deep-disjoint/> D2 P)
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2.
    
- : tgtmap`join-preserves-deep-disjoint-converse
    (tgtmap`deep-disjoint/= D X nat`eq/) (tgtmap`join/> J P) 
    (tgtmap`deep-disjoint/> D1 P) (tgtmap`deep-disjoint/= D2 X nat`eq/)
    <- tgtmap`join-preserves-deep-disjoint-converse D J D1 D2.
    

%worlds () (tgtmap`join-preserves-deep-disjoint-converse _ _ _ _).
%total (D) (tgtmap`join-preserves-deep-disjoint-converse D _ _ _).



%% short of good name for this one

%theorem tgtmap`shift-inside-right-preserves-deep-disjoint :
    forall* {M1} {M2} {D} {N} {SM2}
    forall  {D: tgtmap`deep-disjoint M1 (tgtmap/+ N D M2)}
            {S: tgtmap`shift N M2 SM2}
    exists  {D: tgtmap`deep-disjoint M1 SM2}
    true.

- : tgtmap`shift-inside-right-preserves-deep-disjoint D SH DD
    <- tgtmap`can-construct-unit-join SH J
    <- tgtmap`deep-disjoint-symmetric D D1
    <- tgtmap`join-preserves-deep-disjoint-converse D1 J DD1 DD2
    <- tgtmap`deep-disjoint-symmetric DD1 DD.
    
%worlds () (tgtmap`shift-inside-right-preserves-deep-disjoint _ _ _).
%total  (D) (tgtmap`shift-inside-right-preserves-deep-disjoint D _ _).


%theorem tgtmap`shift-inside-left-preserves-deep-disjoint :
    forall* {M1} {M2} {D} {N} {SM1}
    forall  {D: tgtmap`deep-disjoint (tgtmap/+ N D M1) M2}
            {S: tgtmap`shift N M1 SM1}
    exists  {D: tgtmap`deep-disjoint SM1 M2}
    true.

- : tgtmap`shift-inside-left-preserves-deep-disjoint D SH DD
    <- tgtmap`can-construct-unit-join SH J
    <- tgtmap`join-preserves-deep-disjoint-converse D J DD _.

%worlds () (tgtmap`shift-inside-left-preserves-deep-disjoint _ _ _).
%total  (D) (tgtmap`shift-inside-left-preserves-deep-disjoint D _ _).
    

%theorem tgtmap`shift-inside-preserves-deep-disjoint :
    forall* {M1} {M2} {D1} {D2} {N1} {N2} {SM1} {SM2}
    forall  {D: tgtmap`deep-disjoint
                (tgtmap/+ N1 D1 M1) (tgtmap/+ N2 D2 M2)}
            {S: tgtmap`shift N1 M1 SM1} {S: tgtmap`shift N2 M2 SM2}
    exists  {D: tgtmap`deep-disjoint SM1 SM2}
    true.

- : tgtmap`shift-inside-preserves-deep-disjoint
    D tgtmap`shift/0 _ tgtmap`deep-disjoint/L.

- : tgtmap`shift-inside-preserves-deep-disjoint
    D _ tgtmap`shift/0 tgtmap`deep-disjoint/R.

- : tgtmap`shift-inside-preserves-deep-disjoint
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 nat`eq/)
    (tgtmap`shift/+ P1) (tgtmap`shift/+ P2) M3<>M4
    <- tgtmap`shift-preserves-deep-disjoint 
      M1<>M2 (tgtmap`shift/+ P1) (tgtmap`shift/+ P2) M3<>M4.

- : tgtmap`shift-inside-preserves-deep-disjoint
    (tgtmap`deep-disjoint/< A P) (tgtmap`shift/+ P1) (tgtmap`shift/+ P2) DD
    <- nat`plus-swap-succ P PS
    <- nat`plus-commutative PS PP
    <- tgtmap`shift-preserves-deep-disjoint
      A (tgtmap`shift/+ P1) (tgtmap`shift/+ PP) DPPP
    <- tgtmap`shift-inside-right-preserves-deep-disjoint
      DPPP (tgtmap`shift/+ P2) DD.

- : tgtmap`shift-inside-preserves-deep-disjoint
    (tgtmap`deep-disjoint/> D P) (tgtmap`shift/+ P1) (tgtmap`shift/+ P2) DD
    <- nat`plus-swap-succ P PS
    <- nat`plus-commutative PS PP
    <- tgtmap`shift-preserves-deep-disjoint 
      D (tgtmap`shift/+ PP) (tgtmap`shift/+ P2) DPPP
    <- tgtmap`shift-inside-left-preserves-deep-disjoint
      DPPP (tgtmap`shift/+ P1) DD.

%worlds () (tgtmap`shift-inside-preserves-deep-disjoint _ _ _ _).
%total  {} (tgtmap`shift-inside-preserves-deep-disjoint _ _ _ _).


%%% shouldn't need this lemma!

%theorem tgtmap`update-disjoint-implies-disjoint/L :
    forall* {N} {D1} {D2} {M} {MP}
    forall  {U: tgtmap`update M N D2 MP}
            {D: set`disjoint D1 D2}
    exists  {D: tgtmap`deep-disjoint (tgtmap/+ N D1 tgtmap/0) MP}
    true.

- : tgtmap`update-disjoint-implies-disjoint/L
    tgtmap`update/0 X
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L X nat`eq/).

- : tgtmap`update-disjoint-implies-disjoint/L
    (tgtmap`update/> U P) X (tgtmap`deep-disjoint/> DD P)
    <- tgtmap`update-disjoint-implies-disjoint/L U X DD.

- : tgtmap`update-disjoint-implies-disjoint/L 
    (tgtmap`update/< P) X 
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L X nat`eq/).

- : tgtmap`update-disjoint-implies-disjoint/L
    (tgtmap`update/= nat`eq/) X
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L X nat`eq/).

%worlds () (tgtmap`update-disjoint-implies-disjoint/L _ _ _).
%total  (U) (tgtmap`update-disjoint-implies-disjoint/L U _ _).


%theorem tgtmap`update-disjoint-implies-disjoint :
    forall* {XM1} {XM2} {M1} {M2} {L} {XM1S} {XM2S}
    forall  {D: tgtmap`deep-disjoint XM1 XM2}
            {U: tgtmap`update XM1 L M1 XM1S}
            {U: tgtmap`update XM2 L M2 XM2S}
            {D: set`disjoint M1 M2}
    exists  {D: tgtmap`deep-disjoint XM1S XM2S}
    true.

- : tgtmap`update-disjoint-implies-disjoint 
    _ tgtmap`update/0 tgtmap`update/0 X
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L X nat`eq/).

- : tgtmap`update-disjoint-implies-disjoint
    _ tgtmap`update/0 (tgtmap`update/= nat`eq/)
    M1<>M2 (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L M1<>M2 nat`eq/).

- : tgtmap`update-disjoint-implies-disjoint
    _ (tgtmap`update/= nat`eq/) tgtmap`update/0 X
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/R X nat`eq/).

- : tgtmap`update-disjoint-implies-disjoint
    tgtmap`deep-disjoint/L tgtmap`update/0 (tgtmap`update/> U P) X 
    (tgtmap`deep-disjoint/> DD P)
    <- tgtmap`update-disjoint-implies-disjoint/L U X DD.
      
- : tgtmap`update-disjoint-implies-disjoint
    tgtmap`deep-disjoint/R (tgtmap`update/> U P) tgtmap`update/0 X 
    (tgtmap`deep-disjoint/< DD P)
    <- set`disjoint-symmetric X XP
    <- tgtmap`update-disjoint-implies-disjoint/L U XP DP
    <- tgtmap`deep-disjoint-symmetric DP DD.

- : tgtmap`update-disjoint-implies-disjoint
    _ (tgtmap`update/< P) tgtmap`update/0 X 
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/R X nat`eq/).
      
- : tgtmap`update-disjoint-implies-disjoint
    XM1<>XM2 (tgtmap`update/= nat`eq/) (tgtmap`update/= nat`eq/)
    IM1<>IM2 (tgtmap`deep-disjoint/= XM1S<>XM2S IM1<>IM2 nat`eq/)
    <- tgtmap`deep-disjoint/=-inversion XM1<>XM2 nat`eq/ _ XM1S<>XM2S.

- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/< P) (tgtmap`update/= nat`eq/) M1<>M2
    (tgtmap`deep-disjoint/= DP M1<>M2 nat`eq/)
    <- tgtmap`deep-disjoint/>-inversion D P DP.

- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/= nat`eq/) (tgtmap`update/< P) M1<>M2
    (tgtmap`deep-disjoint/= DP M1<>M2 nat`eq/)
    <- tgtmap`deep-disjoint/<-inversion D P DP.


- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/> U P) (tgtmap`update/= nat`eq/) M1<>M2 
    (tgtmap`deep-disjoint/< DD P)
    <- tgtmap`deep-disjoint/<-inversion D P DP
    <- tgtmap`update-disjoint-implies-disjoint 
      DP U (tgtmap`update/= nat`eq/) M1<>M2 DD.

- : tgtmap`update-disjoint-implies-disjoint D
    (tgtmap`update/= nat`eq/) (tgtmap`update/> U P) X 
    (tgtmap`deep-disjoint/> DD P)
    <- tgtmap`deep-disjoint/>-inversion D P D1
    <- tgtmap`update-disjoint-implies-disjoint
      D1 (tgtmap`update/= nat`eq/) U X DD.
         
- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/> U P) (tgtmap`update/< P1) M1<>M2
    (tgtmap`deep-disjoint/< DPP P)
    <- nat`plus-associative-converse P P1 N6 SN4+SN5=N6 N6+N1=N2
    <- nat`plus-left-preserves-positive SN4+SN5=N6 nat`eq/ _ EQ
    <- nat`plus-respects-eq SN4+SN5=N6 nat`eq/ nat`eq/ EQ (nat`plus/s N4+SN5=N6)
    <- nat`plus-right-preserves-positive N4+SN5=N6 nat`eq/ _ EQ2
    <- nat`plus-respects-eq N6+N1=N2 EQ nat`eq/ nat`eq/ SN8+N1=N2
    <- tgtmap`deep-disjoint/<-inversion D SN8+N1=N2 DP
    <- nat`plus-swap-succ-converse N4+SN5=N6 SN4+N5=N6
    <- tgtmap`update-disjoint-implies-disjoint 
      DP U (tgtmap`update/< SN4+N5=N6) M1<>M2 DPP.

- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/< P1) (tgtmap`update/> U P) M1<>M2 
    (tgtmap`deep-disjoint/> DPP P)
    <- nat`plus-associative-converse P P1 N6 SN4+SN5=N6 N6+N1=N2
    <- nat`plus-left-preserves-positive SN4+SN5=N6 nat`eq/ _ EQ
    <- nat`plus-respects-eq SN4+SN5=N6 nat`eq/ nat`eq/ EQ (nat`plus/s N4+SN5=N6)
    <- nat`plus-right-preserves-positive N4+SN5=N6 nat`eq/ _ EQ2
    <- nat`plus-respects-eq N6+N1=N2 EQ nat`eq/ nat`eq/ SN8+N1=N2
    <- tgtmap`deep-disjoint/>-inversion D SN8+N1=N2 DP
    <- nat`plus-swap-succ-converse N4+SN5=N6 SN4+N5=N6
    <- tgtmap`update-disjoint-implies-disjoint
      DP (tgtmap`update/< SN4+N5=N6) U M1<>M2 DPP.

- : tgtmap`update-disjoint-implies-disjoint
    D (tgtmap`update/< P1) (tgtmap`update/< P2) M1<>M2 
    (tgtmap`deep-disjoint/= DP M1<>M2 nat`eq/)
    <- nat`plus-swap-succ P1 P1S
    <- nat`plus-commutative P1S P1P
    <- nat`plus-swap-succ P2 P2S
    <- nat`plus-commutative P2S P2P
    <- tgtmap`shift-preserves-deep-disjoint-converse
      D (tgtmap`shift/+ P1P) (tgtmap`shift/+ P2P) DP.

- : tgtmap`update-disjoint-implies-disjoint
    (tgtmap`deep-disjoint/= M1<>M2 D1<>D2 nat`eq/)
    (tgtmap`update/> U1 P1) (tgtmap`update/> U2 P2) DDD
    (tgtmap`deep-disjoint/= M1<>M2' D1<>D2 nat`eq/)
    <- nat`plus-right-cancels P1 P2 nat`eq/ nat`eq/ E
    <- nat`succ-cancels E EP
    <- tgtmap`update-respects-eq 
      U1 tgtmap`eq/ EP set`eq/ tgtmap`eq/ U1P
    <- tgtmap`update-disjoint-implies-disjoint M1<>M2 U1P U2 DDD M1<>M2'.
      
- : tgtmap`update-disjoint-implies-disjoint
    (tgtmap`deep-disjoint/< D P)
    (tgtmap`update/> U1 P1)
    (tgtmap`update/> U2 P2) DDD (tgtmap`deep-disjoint/< XXX P)
    <- nat`plus-associative-converse P P2 _ P3 P4
    <- nat`plus-right-cancels P4 P1 nat`eq/ nat`eq/ E
    <- nat`plus-respects-eq P3 nat`eq/ nat`eq/ E P5
    <- nat`plus-right-decrease P5 _ EE P7
    <- nat`succ-cancels EE EQ
    <- nat`eq-symmetric EQ EQP
    <- nat`plus-respects-eq P7 nat`eq/ nat`eq/ EQP P6
    <- tgtmap`update-disjoint-implies-disjoint 
      D U1 (tgtmap`update/> U2 P6) DDD XXX. 

- : tgtmap`update-disjoint-implies-disjoint
    (tgtmap`deep-disjoint/> D P)
    (tgtmap`update/> U1 P1) (tgtmap`update/> U2 P2) DDD
    (tgtmap`deep-disjoint/> D1 P)
    <- nat`plus-associative-converse P P1 _ P3 P4
    <- nat`plus-right-cancels P4 P2 nat`eq/ nat`eq/ E
    <- nat`plus-respects-eq P3 nat`eq/ nat`eq/ E P5
    <- nat`plus-right-decrease P5 _ EE P7
    <- nat`succ-cancels EE EQ
    <- nat`eq-symmetric EQ EQP
    <- nat`plus-respects-eq P7 nat`eq/ nat`eq/ EQP P6
    <- tgtmap`update-disjoint-implies-disjoint
      D (tgtmap`update/> U1 P6) U2 DDD D1.

- : tgtmap`update-disjoint-implies-disjoint
    _ tgtmap`update/0 (tgtmap`update/< _) M1<>M2 
    (tgtmap`deep-disjoint/= tgtmap`deep-disjoint/L M1<>M2 nat`eq/).

%worlds () (tgtmap`update-disjoint-implies-disjoint _ _ _ _ _).
%total (D) (tgtmap`update-disjoint-implies-disjoint D _ _ _ _).


